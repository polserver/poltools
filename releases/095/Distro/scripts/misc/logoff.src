use uo;
use util;
use math;
use os;

include "include/math";
include "include/eventID";
include "include/attributes";

const EVID_UNEQUIP   := 0x123456;
const EVID_DISCHARGE := 0x123457;

program onlogoff(who)
  var acct := FindAccount(who.acctname);
  acct.setprop("LastLogin", readGameclock());
  var sessiontime := ReadGameClock() - GetObjProperty(who,"logontime");
  var timer := GetObjProperty(who,"onlinetimer");
  if(!timer)
    timer := 0;
  endif
  SetObjProperty(who,"onlinetimer",timer+sessiontime);
  EraseObjProperty(who,"LoggedIn");
  RemoveMods(who);
  var mount := GetEquipmentByLayer(who, 25 );
  if(mount)
    if(GetObjProperty(mount, "summoned"))
      var ser := Cint(GetObjProperty(mount, "serial"));
      var critter := SystemFindObjectBySerial(ser);
      if(critter)
        ApplyRawDamage(critter, 2000);
      endif
      DestroyItem(mount);
    else
      var animal := SystemFindObjectBySerial(CInt(GetObjProperty(mount,"serial")));
      SetObjProperty(animal,"onhold", 1);
    endif
  endif
  staffnotify(who);
endprogram

function staffnotify(who)
  var pid := 0;
  while(!pid)
    sleepms(50);
    pid := GetGlobalProperty("#stafflist");
  endwhile
  pid := getprocess(pid);
  if(!pid)
    return;
  endif
  var k := struct;
  k.+player := who;
  k.+type  := EVID_PLAYER_LOGOFF;
  pid.sendevent(k);
endfunction

function RemoveMods(who)
  var mods := {};
  foreach item in ListEquippedItems(who)
    var proc := GetProcess(Cint(GetObjProperty(item, "Pid")));
    if(proc)
      var ev := {};
      ev.+type := EVID_UNEQUIP;
      proc.sendevent(ev);
    endif
  endforeach
  EraseObjProperty(who, "EquipTimers");
  return;
endfunction