use uo;
use util;

include "include/client";
include "include/attributes";

program skill_stealth( who )
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(GetEffectiveSkill(who, SKILLID_HIDING) < 80)
    PrintTextAbovePrivate( who, "You are not good enough at Hiding to do that", who );
    return;
  endif
  if(!who.hidden)
    PrintTextAbovePrivate( who, "You must be hidden to use stealth", who );
    return;
  endif
  var mount := GetEquipmentByLayer(who, 25 );
  if(mount)
    who.hidden := 0;
    PrintTextAbovePrivate( who, "You cant stealth while riding a horse!", who );
    return;
  endif
  var skill := GetEffectiveSkill(who, SKILLID_STEALTH);
  if(CheckSkill(who, SKILLID_STEALTH, -1, skill))
    skill := (skill / 10);
    who.stealthsteps := skill + RandomInt(skill);
    PrintTextAbovePrivate( who, "You are now moving stealthily", who );
  else
    who.hidden := 0;
    PrintTextAbovePrivate( who, "You have revealed yourself!", who );
  endif
endprogram