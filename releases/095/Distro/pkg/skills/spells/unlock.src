use os;
use uo;
use util;

include "include/client";
include "include/spellRestrictions";
include "include/skillPoints";

program cast_strength( parms )
  var circle := 3;
  var delivery := "direct";
  var noto := "neutral";
  var result := maincast(parms, delivery, circle, noto, 23);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := result[2];
  if (!cast_on.locked)
    SendSysMessage(caster,"That is not locked!");
    return;
  endif
  if ((cast_on.multi) || (GetObjProperty(cast_on,"houseserial")) || (GetObjProperty(cast_on,"house_serial")))
    SendSysMessage(caster, "You cannot magic unlock that!");
    PlayLightningBoltEffect(caster);
    return;
  endif
  if (GetObjProperty(cast_on, "#U"))
    if (GetObjProperty(cast_on, "#U") > ReadGameClock())
      SendSysMessage(caster, "You must wait to pick this lock!");
      return;
    endif
  endif
  var diff := CInt(GetObjProperty(cast_on,"MLockPickDiff"));
  if(diff)
    PlaySoundEffect(caster, SFX_SPELL_UNLOCK);
    if(diff <= 25)
      cast_on.locked := 0;
      PrintTextAbovePrivate(cast_on, "*Unlocked!*", caster);
      EraseObjProperty(cast_on, "MLockPickDiff");
      return;
    else
      SendSysMessage(caster, "Your spell doesnt seem to work on that lock");
      return;
    endif
  else
    var diffi := CInt(GetObjProperty(cast_on,"LockPickDiff"));
    if(diffi)
      PlaySoundEffect(caster, SFX_SPELL_UNLOCK);
      if(diffi <= 25)
        cast_on.locked := 0;
        PrintTextAbovePrivate(cast_on, "*Unlocked!*", caster);
        return;
      else
        SendSysMessage(caster, "Your spell doesnt seem to work on that lock");
        return;
      endif
    else
      if(GetObjProperty(cast_on, "level"))
        PickTreasureChest(caster, cast_on);
      else
        SendSysMessage(cast_on, "You can't seem to unlock that!");
        return;
      endif
    endif
  endif
endprogram

function PickTreasureChest(me, chest)
  var lvl := GetObjProperty(chest,"level");
  if(!lvl)
    SendSysMessage(me, "You cannot pick that.");
    return;
  endif
  if(!chest.locked)
    SendSysMessage(me,"That doesn't appear to be locked");
    return;
  endif
  PlaySoundEffect(me, SFX_242);
  sleep(1);
  var diff := (lvl * 25) - 10;
  if(diff <= 25)
    PlaySoundEffect(chest,SFX_200);
    PrintTextAbovePrivate(chest, "*Unlocked!*", me);
    chest.locked := 0;
  else
    SendSysMessage(me, "Your spell doesnt seem to work on that lock");
    return;
  endif
endfunction