use uo;
use os;

include "include/spellRestrictions";
include "include/attributes";

program cast_paralyze( parms )
  var circle := 5;
  var delivery := "direct";
  var noto := "harmful";
  var result := maincast(parms, delivery, circle, noto, 38);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := result[2];
  SetObjProperty(cast_on, "LastHit", {caster.name,caster.serial, "paralyze" });
  if (Reflected(cast_on))
	cast_on := caster;
  endif
  var amt := 10 + RandomInt(CInt(GetEffectiveSkill(caster, SKILLID_MAGERY)/5));
  var duration := pararesist(2, caster, cast_on, amt);
  PlayStationaryEffect( cast_on.x, cast_on.y, cast_on.z, FX_CURSE_EFFECT, 10, 0xa, 0 );
  PlaySoundEffect( cast_on, SFX_SPELL_PARALYZE );
  DoTempMod(cast_on, "p", 1, duration);
endprogram

function pararesist(circle, caster, cast_on, amt)
  var magery := CInt(GetEffectiveSkill(caster, SKILLID_MAGERY));
  var evalint := CInt(GetEffectiveSkill(caster, SKILLID_EVALINT));
  var resist := CInt(GetEffectiveSkill(cast_on, SKILLID_MAGICRESISTANCE));
  var chk1 := (resist / 5);
  var chk2 := resist - (((magery - 20) / 5) + (circle * 5));
  if(chk1 < 1)
    chk1 := 1;
  endif
  if(chk2 < 1)
    chk2 := 1;
  endif
  var diff := 0;
  if(chk1 > chk2)
    diff := chk1;
  else
    diff := chk2;
  endif
  var points := getresistpoints(circle, cast_on, SKILLID_MAGICRESISTANCE);
  if(cast_on.dead)
    return 0;
  endif
  if((RandomInt(99) + 1) <= diff)
	SendSysMessage(cast_on, "You feel yourself resisting magical energy!");
	PlaySoundEffect(cast_on, SFX_SPELL_WEAKEN);
	PlayObjectCenteredEffect(cast_on, FX_CURSE_EFFECT, 5,5);
	AwardRawSkillPoints(cast_on, SKILLID_MAGICRESISTANCE, points);
	amt := 0;
	return amt;
  endif
  var modamt := 1;
  if(resist > evalint)
    modamt := (1 + ((evalint - resist) / 200.0));
  elseif(evalint > resist)
    modamt := (1 + ((evalint - resist) / 500.0));
  endif
  amt := (amt * modamt);
  return CInt(amt);
endfunction