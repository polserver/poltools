use uo;
use os;
use cfgfile;

include "include/client";
include "include/objtype";
include "include/spellRestrictions";
include "include/isValidLoc";

program recall(parms)
  var circle := 4;
  var failure := 0;
  var delivery := "notarget";
  var noto := "neutral";
  var result := maincast(parms, delivery, circle, noto, 32);
  if(result == 0)
    return;
  endif
  var caster := result[1];
  var cast_on := Target(caster);
  if((cast_on.objtype != UOBJ_RUNE) && (cast_on.objtype != 0x6100))
    SendSysMessage(caster, "You can only cast that on a recall rune!");
	return;
  endif
  if(!ReserveItem(cast_on))
    return;
  endif
  if(GetObjProperty(cast_on, "Vendored"))
    SendSysMessage(caster, "You cannot recall off of runes on a vendor");
    return;
  endif
  var tox := CInt(GetObjProperty( cast_on, "x" ));
  var toy := CInt(GetObjProperty( cast_on, "y" ));
  var toz := CInt(GetObjProperty( cast_on, "z" ));
  if(!tox)
    SendSysMessage(caster, "the rune must be marked.");
    return;
  endif
  if(!isValidLoc(tox,toy))
    SendSysMessage(caster, "You may not recall into Green Acres.");
    DestroyItem(cast_on);
    return;
  endif
  PlaySoundEffect(caster, 0x210);
  Detach();
  var chk := 0;
  if((caster.x >= 5120) && (caster.x <= 5375) && (caster.y >= 0) && (caster.y <= 255))
    SendSysMessage(caster, "Your spell fails and consumes the rune!");
    if(cast_on.objtype == UOBJ_RUNE)
      DestroyItem(cast_on);
    elseif(cast_on.objtype == 0x6100)
      removeentry(cast_on);
    endif
    chk := 1;
  endif
  var multicheck := CreateItemAtLocation(5833, 1125, 0, 0xeed, 1);
  multicheck.hidden := 1;
  if(!MoveItemToLocation(multicheck, tox, toy, toz, MOVEITEM_NORMAL))
    SendSysMessage(caster, "Something is blocking the location");
    if(cast_on.objtype == UOBJ_RUNE)
      DestroyItem(cast_on);
    elseif(cast_on.objtype == 0x6100)
      removeentry(cast_on);
    endif
    chk := 1;
  endif
  if(multicheck.multi)
    SendSysMessage(caster,"something is blocking the location");
    if(cast_on.objtype == UOBJ_RUNE)
      DestroyItem(cast_on);
    elseif(cast_on.objtype == 0x6100)
      removeentry(cast_on);
    endif
    chk := 1;
  endif
  DestroyItem(multicheck);
  if(chk == 1)
    return;
  endif
  if (failure == 0);
	MoveCharacterToLocation( caster, tox, toy, toz );
  elseif (failure == 2)
    SendSysMessage(caster, "your spell fails and consumes the rune!");
    if(cast_on.objtype == UOBJ_RUNE)
      DestroyItem(cast_on);
    endif
  endif
  PlaySoundEffect(caster, 0x1fd);
endprogram

function removeentry(cast_on)
  var tox := CInt(GetObjProperty( cast_on, "x" ));
  var toy := CInt(GetObjProperty( cast_on, "y" ));
  var toz := CInt(GetObjProperty( cast_on, "z" ));
  var defs := GetObjProperty(cast_on, "runedefs");
  var holder := {};
  foreach thing in defs
    if((Cint(thing[2]) != tox) or (Cint(thing[3]) != toy) or (Cint(thing[4]) != toz))
      holder.append(thing);
    endif
  endforeach
  SetObjProperty(cast_on, "runedefs", holder);
  EraseObjProperty(cast_on, "x");
  EraseObjProperty(cast_on, "y");
  EraseObjProperty(cast_on, "z");
  return;
endfunction