/*
        Maintened by *Edwards

        2010-02-27
        
        Reagents Counter by Jaleem[rMn] - ZuluHotel Italy
*/
use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";
        
program textcmd_myReags( mobile )

	if( GetProcess( CInt( GetObjProperty( mobile, "#CmdReagsPid" ))))
		return 0;
	else
		SetObjProperty( mobile, "#CmdReagsPid", GetPid() );
	endif
 
        var reagents := myReag( mobile );
        if( !reagents )
                SendSysMessage( mobile, "You have no more reagents!" );
                return 0;
        endif
 
        //Initialize the gump
        var gump := GFCreateGump();

        //BackgroundAreas
        GFGumpPic( gump, 30, 40, 2080 );
        GFGumpPic( gump, 46, 77, 2081 );
        GFGumpPic( gump, 46, 130, 2082 );
        GFGumpPic( gump, 46, 183, 2083 );
        
        GFTextLine( gump, 140, 45, 1720, "Reagents List" );

        GFTextLine( gump, 100, 191, 1720, "Set warning level ("+GetWarningLevel( mobile )+")" );
        GFAddButton( gump, 85, 195, 2104, 2103, GF_CLOSE_BTN, 0xA1 );
        
        //Load ReagsPic
        GFTextLine( gump, 110, 75, GetAmountColor( mobile, reagents[1] ), reagents[1] );
        GFTilePic( gump, 70, 80, 3962 ); //black pearl

        GFTextLine( gump, 110, 105, GetAmountColor( mobile, reagents[2] ), reagents[2] );
        GFTilePic( gump, 70, 110, 3963 ); //blood moss
        
        GFTextLine( gump, 110, 135, GetAmountColor( mobile, reagents[3] ), reagents[3] );
        GFTilePic( gump, 70, 140, 3972 ); //garlic

        GFTextLine( gump, 110, 165, GetAmountColor( mobile, reagents[4] ), reagents[4] );
        GFTilePic( gump, 70, 170, 3973 ); //ginseng

        GFTextLine( gump, 230, 75, GetAmountColor( mobile, reagents[5] ), reagents[5] );
        GFTilePic( gump, 190, 80, 3974 ); //mandrake root

        GFTextLine( gump, 230, 105, GetAmountColor( mobile, reagents[6] ), reagents[6] );
        GFTilePic( gump, 190, 110, 3976 ); //nightshade

        GFTextLine( gump, 230, 135, GetAmountColor( mobile, reagents[7] ), reagents[7] );
        GFTilePic( gump, 190, 140, 3980 ); //sulfurous ash

        GFTextLine( gump, 230, 165, GetAmountColor( mobile, reagents[8] ), reagents[8] );
        GFTilePic( gump, 190, 170, 3981 ); //spider silk

        //And now we need to know the answer...
        var input := GFSendGump( mobile, gump );
        if( !input )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        elseif( input[0xA1] )
        	var amount := CInt( SendTextEntryGump( mobile, "Enter a value (1-1000)", TE_CANCEL_ENABLE, TE_STYLE_NUMERICAL, 1000 ));
		if( amount < 1 )
			SendSysMessage( mobile, "Cancelled." );
                        return 0;
		endif
                SetObjProperty( mobile, "ReagWarn", amount );
        endif
        
	return 1;
endprogram

function myReag( mobile )

        var gid_reags := array{0, 0, 0, 0, 0, 0, 0, 0};
            
        var reags_count := 0;
        foreach item in EnumerateItemsInContainer( mobile.backpack )
                if( item.IsReagent() )
        		case( item.objtype )
				0xF7A: gid_reags[1] += item.amount; //black pearl
                                         break;
				0xF7B: gid_reags[2] += item.amount; //blood moss
                                         break;
				0xF84: gid_reags[3] += item.amount; //garlic
                                         break;
				0xF85: gid_reags[4] += item.amount; //ginseng
                                         break;
				0xF86: gid_reags[5] += item.amount; //mandrake root
                                         break;
				0xF88: gid_reags[6] += item.amount; //nightshade
                                         break;
				0xF8C: gid_reags[7] += item.amount; //sulfurous ash
                                         break;
				0xF8D: gid_reags[8] += item.amount; //spider silk
                                         break;
			endcase
                        reags_count := 1;
                endif
                SleepMS(5);
        endforeach
        
        if( !reags_count )
                return 0;
        endif
        
	return gid_reags;
endfunction

function GetWarningLevel( mobile )

        var level := GetObjProperty( mobile, "ReagWarn" );
        if( !level )
                level := 100;
        endif
        
	return level;
endfunction

function GetAmountColor( mobile, amount )

        var level := GetWarningLevel( mobile ),
            color := 2100;
        
        if( amount < level )
                color := 33;
        endif

	return color;
endfunction
