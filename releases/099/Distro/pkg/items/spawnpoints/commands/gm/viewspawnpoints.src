/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-11-16
*/
use uo;
use os;

include "include/security";
include ":datafile:datafile";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":spawnpoints:settings";

program textcmd_listBanned( mobile )

        if( !AuthorizationCode( mobile ))
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        endif
        
        var datafile := SPM_GetDataFile();
 	var serial := DFGetElemNames( datafile );
	var num_serial := serial.Size();
 
        if( !num_serial )
                SendSysMessage( mobile, "No spawnpoints yet." );
                return 0;
        endif
        
        //Initialize the gump
        var gump := GFCreateGump();

        GFClosable( gump, 0 );

        //Load Main Background
	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 650, 425 );

        GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 620, 25 );
	GFTextMid( gump, 15, 17, 620, 1720, "Spawnpoint Viewer" );

        //BackgroundAreas
	GFResizePic( gump, 15, 45, GFGetCfgConst( "Defaults", "ForeGround" ), 620, 370 );
        GFTextMid( gump, 15, 60, 620, 1720, " Serial            Template                Coordinates              Goto" );

        //Okay Button
        GFAddButton( gump, 300, 382, 2128, 2129, GF_CLOSE_BTN, 0xA00 );
        
        GFPage( gump, 1);

	var y_pos := 90;
	foreach entry in ( serial )

                var data_elem := DFFindElement( datafile, entry );
		GFTextLine( gump, 45, y_pos, 1153, entry );
  
                var spawnpoint := SystemFindObjectBySerial( CInt( entry )),
                    sp_settings := GetSpawnpointSettings( spawnpoint ),
                    template_name := sp_settings[2];
                
		GFTextLine( gump, 135, y_pos, 1153, template_name );
                        
		GFTextLine( gump, 355, y_pos, 1153, data_elem.GetProp( "X" )+","+data_elem.GetProp( "Y" )+","+data_elem.GetProp( "Z" )+","+data_elem.GetProp( "Realm" ));
		GFAddButton( gump, 543, y_pos+5, 0x837, 0x838, GF_CLOSE_BTN, _entry_iter );

		y_pos := y_pos+20;


		if ( _entry_iter % 14 == 0 && _entry_iter < num_serial )
			GFAddButton( gump, 30, 348, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1 );
			GFPage( gump,  gump.cur_page+1 );
			GFAddButton( gump, 30, 95, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1 );
			y_pos := 90;
		endif

		SleepMS(2);
	endforeach

        //And now we need to know the answer...
        var input := GFSendGump( mobile, gump );
        if( input[0xA00] )
                 return 1;
        endif
        
        input := input[0];
        
        var item := SystemFindObjectBySerial( CInt( serial[input] ));
        MoveObjectToLocation( mobile, item.x, item.y, item.z, item.realm, MOVEOBJECT_FORCELOCATION );

        return 1;
endprogram
