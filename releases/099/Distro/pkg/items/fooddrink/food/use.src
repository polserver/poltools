/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2009-08-29
        
        Last Modifications 2009-10-21
             * Cleaned codes for better performances

        Last Modifications 2009-11-16
             * Removed while loop hunger.ecl because now core handle it via attributes
             * Moved Stamina check if no poison detected only
             * Fixed value with hunger so now we get a message of hunger when we eat
*/
use uo;
use os;
use util;
use cfgfile;

include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":itemutils:itemdesc";
include "include/client";
include "include/itemUtil";

program eat( who, food )

	if( !useItem_check( who, food, DEFAULT_ITEM ))
		return 0;
	endif

	var cfg_elem := GetItemDescInfo( food.objtype );
 
	var foodvalue := CInt( cfg_elem.foodvalue );
	if( !foodvalue )
		foodvalue := 1;
	endif
 
	var hunger := CInt( AP_GetVital( who, HUNGER ));
	if( hunger >= 10 )
		SendSysMessage( who, "You are too full to eat any more." );
		return 0;
	endif
	
	if( !SubtractAmount( food, 1 ))
		SendSysMessage( who, "Error - unable to subtract item." );
		return 0;
	endif

	PerformAction( who, ANIM_EAT );
	case( RandomInt(3) + 1 )
		1:
			PlaySoundEffect( who, 0x3b );
			break;
		2:
			PlaySoundEffect( who, 0x3c );
			break;
		3:
			PlaySoundEffect( who, 0x3d );
			break;
	endcase

	hunger += foodvalue;
 
        var themsg;
	case( hunger )
		0: 1: 2:
			themsg := "You eat the food but are still extremely hungry.";
			break;
		3: 4: 5: 6:
			themsg := "After eating the food you feel much less hungry.";
			break;
	        7: 8:
			themsg := "You feel quite full after consuming the food.";
			break;
		default:
			themsg := "You manage to eat the food, but you are stuffed.";
			break;
	endcase

	AP_SetVital( who, HUNGER, hunger );

        SendSysMessage( who, themsg );

        var poison := CInt( GetObjProperty( food, "PoisonLevel" ));
	if( poison )
                var prop_creator := GetObjProperty( food, "Createdby" );
                var find_creator := SystemFindObjectBySerial( CInt( prop_creator ));
		TS_StartTimer( who, "defaultPoison", 120, poison, find_creator );
        else
                var stam := CInt( AP_GetVital( who, "Stamina" ));
		AP_SetVital( who, "Stamina", stam + ( RandomInt(6) * foodvalue ));
	endif

	return 1;
endprogram
