/*
        Maintened by *Edwards

        2009-10-22
*/
use uo;
use os;
use polsys;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":moongates:moongate";

program walkOnScript( mobile, tile )

	if( mobile.npctemplate )
		return 0;
	elseif( JustTeleported( mobile ))
		return 0;
        elseif( mobile.criminal || mobile.murderer )
                SendSysMessage( mobile, "Thou'rt a criminal and cannot escape so easily." );
                return 1;
	endif
	
	var gump := tile.GetGump();
	var input := GFSendGump( mobile, gump );
	
	if( !input[MG_OKAY_BTN] )
		SendSysMessage( mobile, "Cancelled." );
		return 0;
	elseif( Distance( mobile, tile ) > 1 )
		SendSysMessage( mobile, "No longer close enough to the moongate." );
		return 0;
	endif
 
        mobile.paralyzed := 1;
	
	var select := 0;
	foreach key in ( input.Keys )
		if ( key >= MG_LOCATION_START )
			select := key-MG_LOCATION_START;
			break;
		endif
		SleepMS(2);
	endforeach
	
	var dest := MG_FindDestination( select );
	var result := MoveObjectToLocation( mobile, dest.x, dest.y, dest.z, dest.realm, MOVEOBJECT_NORMAL );

        mobile.paralyzed := 0;
	if( result.errortext )
		SendSysMessage( mobile, "Unable to move ->"+result.errortext );
	endif
 
 	SetObjProperty( mobile, "#Teleported", ReadMillisecondClock() );
		
	return 1;
endprogram
