/*
        Maintened by *Edwards
        
        2009-08-29
*/
use uo;
use os;
use polsys;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":moongates:moongate";

program useScript( mobile, tile )

        if( mobile.criminal || mobile.murderer )
                SendSysMessage( mobile, "Thou'rt a criminal and cannot escape so easily." );
                return 1;
        endif
        
	var gump := tile.GetGump();
	var input := GFSendGump( mobile, gump );
	
	if( !input[MG_OKAY_BTN] )
		SendSysMessage( mobile, "Cancelled." );
		return 0;
	elseif( Distance( mobile, tile ) > 1 )
		SendSysMessage( mobile, "Not close enough to the moongate." );
		return 0;
	endif
	
	var dest;
	foreach key in ( input.Keys )
		if( key >= MG_LOCATION_START )
			var temp := key-MG_LOCATION_START;
			dest := MG_FindDestination( temp );
			break;
		endif
		SleepMS(2);
	endforeach

	var result := MoveObjectToLocation( mobile, dest.x, dest.y, dest.z, dest.realm, MOVEOBJECT_NORMAL );
	if( result.errortext )
		SendSysMessage( mobile, "Unable to move ->"+result.errortext );
	endif

 	SetObjProperty( mobile, "#Teleported", ReadMillisecondClock() );
		
	return 1;
endprogram
