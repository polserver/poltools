/*
        Created by *Edwards
        For FantasiaShard.com
        
        Edwards@FantasiaShard.com
        
        2009-11-05
        
        Last Modifications 2009-12-04
             * Added loop messages
             * Removed deprecated var counter and now using only amt for while loop
*/
use uo;
use os;
use cfgfile;

include "include/itemUtil";
include ":areas:managment";
include ":crafting:crafting";

var item_config := ReadConfigFile( ":cartography:itemdesc" );
var craft_config := ReadConfigFile( ":cartography:cartography" );

program use_MapmakersPen( who, tool )

	if( Lower( TypeOf( who )) == "array" )
		tool := who[2];
		who := who[1];
	endif

	if( !useItem_check( who, tool, ITEM_INCONTAINER ))
		return 0;
	endif

	var rinfo := sendCraftGump( who, CARTOGRAPHY );
	if( rinfo == 0 )
		return 0;
	endif

	var the_objtype := rinfo[1];
	var amt := rinfo[2];

	makeCartographyMap( who, tool, the_objtype, amt );

	return 1;
endprogram

function makeCartographyMap( who, tool, the_objtype, amt )

	if( !the_objtype || !amt )
		SendSysMessage( who, "Cancelled." );
		return 0;
	elseif( who.realm != "britannia" )
		SendSysMessage( who, "Sorry, only britannia is chartable for the moment." );
		return 0;
	elseif( !A_IsInArea( who, "Britannia-Fantasia" ))
		SendSysMessage( who, "Magical protections prevent you from charting here." );
		return 0;
	endif
 
 	var craft_elem := craft_config[the_objtype],
 	    skill_elem := menu_config[craft_skill],
            x := who.x,
	    y := who.y;

        SendSysMessage( who, "You start "+skill_elem.EndMessage+"...", 3, 89 );

	while( amt )
		if( !tool )
			break;
		endif

                if( amt != -1 )
                	SendSysMessage( who, "Looping ["+amt+" more to go].", 3, 89 );
                else
                	SendSysMessage( who, "Looping.", 3, 89 );
                endif
                
                Sleep( CRAFT_DELAY );
                
                Craft_PlayEffects( who, skill_elem );
                
		if( who.x != x || who.y != y )
                        SendSysMessage( who, "Cancelled." );
			break;
                endif

                var parts_list := Craft_CheckParts( who, craft_elem );
                if( !parts_list )
                	break;
                elseif( !Craft_ConsumeParts( who, parts_list ))
                	break;
		elseif( Craft_SkillCheck( who, craft_elem, CRAFT_ADV_ALL ) > 0 )
			var the_item := Craft_CreateItem( who, the_objtype, craft_elem );

			var props;
			case( the_item.objtype )
				0x6500:	props := array{"local", who.x - 265, who.y - 260, who.x + 265, who.y + 260, 200, 200};
					the_item.encodeMap( props );
                                        break;
				0x6501:	props := array{"regional", who.x - 850, who.y - 800, who.x + 850, who.y + 800, 400, 400};
					the_item.encodeMap( props );
                                        break;
			endcase

                        Craft_ExpCheck( who, craft_elem, the_item );
		else
			SendSysMessage( who, "You lost some materials." );
		endif

                CheckToolWear( who, tool, CARTOGRAPHY );

                if( amt != -1 )
                	amt -= 1;
                endif
	endwhile

        SendSysMessage( who, "You stop "+skill_elem.EndMessage+"...", 3, 89 );

	return 1;
endfunction
