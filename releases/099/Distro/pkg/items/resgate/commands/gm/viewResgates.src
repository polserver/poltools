/*
        Created by *Edwards

        2010-01-24
*/
use uo;
use os;
use cfgfile;

include "include/security";
include ":gumps:gumps";
include ":gumps:gumps_ex";

program textcmd_viewResgates( mobile )

        if( !AuthorizationCode( mobile ))
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        endif
        
        var cfg := ReadConfigFile( ":resgate:init" );
        if( cfg.errortext )
                SysLog( "Error reading config <:resgate:init> -->"+cfg.errortext );
                return 0;
        endif
        
        //Initialize the gump
        var gump := GFCreateGump();

        GFClosable( gump, 0 );

        //Load Main Background
	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 450, 425 );

        GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 420, 25 );
	GFTextMid( gump, 15, 17, 620, 1720, "Resgate Viewer" );

        //BackgroundAreas
	GFResizePic( gump, 15, 45, GFGetCfgConst( "Defaults", "ForeGround" ), 420, 370 );
        GFTextLine( gump, 45, 60, 1720, "Elem             Coordinates                 Goto" );

        //Okay Button
        GFAddButton( gump, 370, 382, 2128, 2129, GF_CLOSE_BTN, 0xA00 );
        
        GFPage( gump, 1);

 	var cfg_elem := GetConfigIntKeys( cfg );
	var num_elem := cfg_elem.Size();

	var y_pos := 90;
	foreach entry in ( cfg_elem )
 
		var cfg_elem := FindConfigElem( cfg, entry );
  
		GFTextLine( gump, 45, y_pos, 1153, entry );
                        
		GFTextLine( gump, 175, y_pos, 1153, cfg_elem.X+", "+cfg_elem.Y+", "+cfg_elem.Z+", "+cfg_elem.Realm );
		GFAddButton( gump, 380, y_pos, 0x837, 0x838, GF_CLOSE_BTN, _entry_iter );

		y_pos := y_pos+20;

		if( _entry_iter % 14 == 0 && _entry_iter < num_elem )
			GFAddButton( gump, 30, 348, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1 );
			GFPage( gump,  gump.cur_page+1 );
			GFAddButton( gump, 30, 95, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1 );
			y_pos := 90;
		endif

		SleepMS(2);
	endforeach

        //And now we need to know the answer...
        var input := GFSendGump( mobile, gump );
        if( input[0xA00] )
                 return 1;
        endif
        
        input := input[0];
        
        var elem := FindConfigElem( cfg, cfg_elem[input] );
        
        MoveObjectToLocation( mobile, elem.X, elem.Y, elem.Z, elem.Realm, MOVEOBJECT_FORCELOCATION );

        return 1;
endprogram
