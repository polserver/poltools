/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-06-01 //Happy Birthay Momy!

        Thanks to ZuluHotel and his developers for this original Idea!
*/
use os;
use uo;
use util;

include ":damage:damage";
include ":warMachine:explosion";

program warMachine_Catapult( mobile, ammo )

        SendSysMessage( mobile, "Select Catapult." );
        var catapult := Target( mobile, TGTOPT_CHECK_LOS );

        if( catapult.objtype != 0x1663 && catapult.objtype != 0x168d && catapult.objtype != 0x16b1 && catapult.objtype != 0x16d6 )
               SendSysMessage( mobile, "Canceled.");
               return 0;
        endif
        if( Distance( mobile, catapult ) > 2 )
	       SendSysMessage( mobile, "This is too far." );
	       return 0;
        endif

        SendSysMessage( mobile, "Select Target" );
        var location := TargetCoordinates( mobile );
        if( !location )
               SendSysMessage( mobile, "Canceled." );
               return 0;
        endif

        DestroyItem( ammo );

        var item := catapult.graphic;
        catapult.graphic := item+2;
        PlaySoundEffect( catapult, 0x221 );
        SleepMS(500);
        catapult.graphic := item+3;
        SleepMS(500);
        FireWorks( mobile, catapult, location, "Catapult" );
        Sleep(1);
        catapult.graphic:=item+2;
        SleepMS(500);
        catapult.graphic:=item;
        
        return 1;
endprogram
