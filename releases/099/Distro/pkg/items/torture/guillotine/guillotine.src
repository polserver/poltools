/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        Last Modifications 2009-08-29
             * Added useItem_check()
*/
use uo;
use cfgfile;
use os;

include "include/uo_extend";
include "include/yesNo";
include ":damage:damage";
include "include/itemUtil";

const SOUND_MOVEDOWN := 557;
const SOUND_MOVEUP := 241;
const ACTION_BOW := 32;
const ITEM_HEAD := 0x1CE1;

program UseGuillotine( who, guillotine )

        if( !useItem_check( who, guillotine, DEFAULT_ITEM, 1 ))
		return 0;
	elseif( !YesNo( who, "Are you sure you want to put your head in this thing?" ))
		SendSysMessage( who, "Cancelled" );
		return 0;
	endif
	
	if( !GetObjProperty( guillotine, "direction" ))
		SetObjProperty( guillotine, "direction", "MoveDown" );
	endif
	
	var direction := GetObjProperty( guillotine, "direction" );
	
	if( direction["Up"] )
		MoveUp( guillotine );
	else
		who.frozen := 1;
		PerformAction( who, ACTION_BOW );
		MoveDown( guillotine );
		who.Kill();
		who.paralyzed := 0;
		who.frozen := 0;
		SleepMS(500);
  
		var corpse := GetObjProperty( who, "corpse_serial" );
		corpse := SystemFindObjectBySerial( corpse );
		corpse.Behead( who );
	endif
 
        return 1;
endprogram

function MoveUp( guillotine )

	var config := NewGraphic( guillotine );
	guillotine.graphic := GetConfigInt( config, "MoveUp" );
	
	PlaySoundEffect( guillotine, SOUND_MOVEUP );
	
	config := NewGraphic( guillotine );
	if(!GetConfigInt( config, "MoveUp" ))
		SetObjProperty( guillotine, "direction", "MoveDown" );
	endif
 
        return 1;
endfunction

function MoveDown( guillotine )

	var config := NewGraphic( guillotine );
	while( GetConfigInt( config, "MoveDown" ))
		SleepMS(400);
		guillotine.graphic := GetConfigInt( config, "MoveDown" );
		config := NewGraphic( guillotine );
	endwhile
 
	PlaySoundEffect( guillotine, SOUND_MOVEDOWN );
	SetObjProperty( guillotine, "direction", "MoveUp" );
 
        return 1;
endfunction

function NewGraphic( guillotine )

	var guillotine_cfg := ReadConfigFile( ":torture:guillotine" );
	if( guillotine_cfg.errortext )
		SendSysMessage( guillotine, "Error - Could not open :torture:guillotine.cfg ->"+guillotine_cfg.errortext );
		return guillotine.graphic;
	endif
	
	return guillotine_cfg[guillotine.graphic];
endfunction
