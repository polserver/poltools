/*
        Maintened by *Edwards

        2009-01-18

        Last Modifications 2009-08-29
             * Added useItem_check()
*/
use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":areas:coords";
include "include/itemUtil";

program use_message( mobile, SOSMessage )

       	if( !useItem_check( mobile, SOSMessage, ITEM_INCONTAINER ))
		return 0;
	endif

        var bottle_x := CInt( GetObjProperty( SOSMessage, "XPos" )),
            bottle_y := CInt( GetObjProperty( SOSMessage, "YPos" )),
            messageID := CInt( GetObjProperty( SOSMessage, "Message" ));

        if( !bottle_x || !bottle_y )
	       SendSysMessage( mobile, "Error --> No location given for this bottle." );
	       return 0;
        endif

        var gump := GFCreateGump();

        GFClosable( gump, 0 );

        GFResizePic( gump, 200, 100, 3500, 300, 200 );

        GFAddButton( gump, 220, 120, 2128, 2129, GF_CLOSE_BTN, 0xA00 );
        GFTilePic( gump, 330, 260, 0x14f3 );

        var message := "";
	case( messageID )
	    0: 	message := "Help, our ship is sinking! We are at coordinates: "+
                           ToSextant( bottle_x, bottle_y ) +". Hurry, we are slipping away fast!";
	    1: 	message := "We are under attack by a gigantic sea serpent! We are at coordinates: "+
                           ToSextant( bottle_x, bottle_y ) +". Save us!";
	    2: 	message := "A savage beast has holed our ship at location: "+
                           ToSextant( bottle_x, bottle_y ) +". We will hang on as long as possible.";
	    3: 	message := "We have encountered a terrible storm at coordinates: "+
                           ToSextant( bottle_x, bottle_y ) +". The ship may capsize soon, hurry!";
	    4: 	message := "My ship sank close to the coordinates: "+
                           ToSextant( bottle_x, bottle_y ) +". I hold great hope that I am rescued!";
	endcase
 
	GFDrawWrappedText( gump, 220, 160, GFWordWrap( message, 270 ));

        GFSendGump( mobile, gump );

        return 1;
endprogram
