/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-06-10

        Last Modifications 2009-11-29
             * Removed race bonus for stats because I actually plan to not place race gate
             on player creation...

        Last Modifications 2009-12-21
             * Added: Changed my mind... race bonus will be given on player creation... :-O

        Last Modifications 2010-03-13
             * Added: Froze status when selecting race.
             * Added: Will now teleport the character to britain center.
             
        Last Modifications 2010-09-07

        Thanks to ZuluHotel for this original Idea and Fantasia for PowerScoll system
*/
use uo;
use os;
use cfgfile;

include "include/packets";
include "include/string";
include ":attributes:attributes";
include ":death:resurrection";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":races:races";
include ":security:report";

program walkon_raceGate( mobile, raceGate )

        var mobile_race := RACE_GetMobileRaceName( mobile );
        if( mobile_race && !mobile.cmdlevel )
                SendSysMessage( mobile, "You are already "+mobile_race+"." );
                return 0;
        endif
        
        mobile.frozen := 1;
        
        if( !SendInfoRace( mobile, raceGate ))
               mobile.frozen := 0;
               return 0;
        endif

        mobile.frozen := 0;
        mobile.facing := 2;
        
        var move := MoveObjectToLocation( mobile, 2242, 1233, 9, "britannia", MOVEOBJECT_FORCELOCATION ); //Cove Center
        if( move.errortext )
                SendSysMessage( mobile, "Error couldn't move to destination -->"+move.errortext );
                return 0;
        endif

        FakeDead( mobile );

        var race_name := GetObjProperty( raceGate, "Race" );
        var set_Race := RACE_SetMobileRaceName( mobile, race_name );
        if( set_Race.errortext )
                SysLog( "Error setting race to mobile -->"+set_Race.errortext );
                return 0;
        endif

        var race_elem := RACE_GetConfigFile( race_name );

        AP_SetTrueStat( mobile, DEXTERITY, AP_GetTrueStat( mobile, DEXTERITY ) + CInt( race_elem.Bonus_Dex ));
        AP_SetTrueStat( mobile, INTELLIGENCE, AP_GetTrueStat( mobile, INTELLIGENCE ) + CInt( race_elem.Bonus_Int ));
        AP_SetTrueStat( mobile, STRENGTH, AP_GetTrueStat( mobile, STRENGTH ) + CInt( race_elem.Bonus_Str ));

        var affection_skillIds := SplitWords( CStr( race_elem.CapSkill ));

        foreach skill_name in affection_skillIds
                AP_SetSkillCap( mobile, skill_name, 135 );
                SleepMS(2);
        endforeach

        var race_color := SplitWords( CStr( race_elem.True_Color ));
        var select_color := RandomInt( CInt( race_color[2] )) + CInt( race_color[1] );
        
       	ResurrectionGump( mobile );

        mobile.color := select_color;
        mobile.truecolor := select_color;

        SendSysMessage( mobile, "You are now the race of "+race_name+"." );
        ReportText( "IP: "+mobile.ip+" Account: "+mobile.acctname+" Character: "+mobile.name+" Selected: "+race_name, REPORT_SYSLOG );
        
        Sleep(5);
        
        SendSysMessage( mobile, "You awake from your dream..." );

        return 1;
endprogram

function SendInfoRace( mobile, raceGate )

        var race_name := GetObjProperty( raceGate, "Race" ),
            race_elem := RACE_GetConfigFile( race_name );
        
        //Initialize the gump
        var gump := GFCreateGump();

        GFMovable( gump, 0 );
        GFClosable( gump, 0 );

        //Load Main Background
	GFResizePic( gump, 290, 50, GFGetCfgConst( "Defaults", "BackGround" ), 250, 425 );
        GFResizePic( gump, 305, 65, GFGetCfgConst( "Defaults", "ForeGround" ), 220, 25 );
	GFResizePic( gump, 305, 95, GFGetCfgConst( "Defaults", "ForeGround" ), 220, 370 );
        
        //Title
	GFTextMid( gump, 315, 67, 220, 1720, "Race Informations" );
	GFTextMid( gump, 305, 100, 220, 1720, race_name );
 
        //Race RPG Info
        var tooltip := race_elem.Tooltip;
        GFHTMLArea( gump, 330, 140, 170, 60, tooltip, 0, 1 );
        
        //Bonus
        GFTextLine( gump, 330, 220, 1720, "Dexterity Bonus:" );
        GFTextLine( gump, 330, 240, 1720, "Intelligence Bonus:" );
        GFTextLine( gump, 330, 260, 1720, "Strength Bonus:" );
        
        GFTextLine( gump, 450, 220, 2100, "+"+CInt( race_elem.Bonus_Dex ));
        GFTextLine( gump, 450, 240, 2100, "+"+CInt( race_elem.Bonus_Int ));
        GFTextLine( gump, 450, 260, 2100, "+"+CInt( race_elem.Bonus_Str ));
        
        GFTextLine( gump, 330, 300, 1720, "Skill cap bonus:" );
        
        var cap_skills := SplitWords( race_elem.CapSkill );

        var cap_string := "";
        foreach skill in cap_skills
        
                skill := UCFirst( skill );
                skill["_"] := " ";
                
                cap_string += skill+" ";
                SleepMS(5);
        endforeach
        
        GFDrawWrappedText( gump, 330, 320, GFWordWrap( cap_string, 170 ), 2100 );

        //Buttons
        GFAddButton( gump, 325, 430, 0xee, 0xef, GF_CLOSE_BTN, 0xA00 ); //Accept
        GFAddButton( gump, 450, 430, 0xf2, 0xf1, GF_CLOSE_BTN, 0xA01 ); //Cancel

        //And now we need to know the answer...
        var input := GFSendGump( mobile, gump );
        if( !input || input[0xA01] )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        endif

        return 1;
endfunction
