/*
        Maintened by *Edwards

        2008-06-01

        Last Modifications 2009-08-29
             * Added useItem_check()
        
        Checks the mobile's Cartogrphy skill to see if
        the map can be read. If so, it creates a new, readable
        map and a digging tool. Also picks a random location
        for the treasure to be. Author: Racalac, 4/24/99
*/
use uo;
use os;
use cfgfile;

include ":attributes:attributes";
include "include/itemUtil";

const DIGGINGTOOL := 0x607;

program decode_Map( mobile , map )

       	if( !useItem_check( mobile, map, ITEM_INCONTAINER ))
		return 0;
	endif

        var map_level := GetObjProperty( map, "Level" ),
            carto_skill :=  AP_GetSkill( mobile, CARTOGRAPHY ),
	    difficulty := CInt( 25 * map_level );

	if( difficulty > carto_skill )
		SendSysmessage( mobile , "You cannot comprehend the map." );
	else
		if( SkillCheck( mobile, CARTOGRAPHY, -1 , map_level*100 ))
                        SendSysmessage( mobile , "You successfully decode the map!" );
			CreateTreasureMap( mobile, map, map_level );
		else
			SendSysmessage( mobile , "You understand the map, but fail to decode it." );
		endif
	endif

        return 1;
endprogram

function CreateTreasureMap( mobile, oldmap, maplevel );

	var map := CreateItemInBackpack( mobile, 0x641E, 1 );
 
	SetObjProperty( map, "Level", maplevel );
	SetObjProperty( map, "Decoder", mobile.serial );

	SetTreasureCoords( map, maplevel );
	DestroyItem( oldmap );

        return 1;
endfunction

function SetTreasureCoords( map, maplevel )

	var cfg_file := ReadConfigFile( ":maps:treasure" ),
	    cfg_keys := GetConfigMaxIntKey( cfg_file ),
	    random_key := RandomInt( cfg_keys ) + 1,
	    info_coords := FindConfigElem( cfg_file, random_key );

	var x := GetConfigInt( info_coords, "X" ),
	    y := GetConfigInt( info_coords, "Y" ),
	    z := GetConfigInt( info_coords, "Z" );
        
	var coords := struct;
	coords.+X := x;
	coords.+Y := y;
	coords.+Z := z;
	coords.+Realm := "britannia";

        SetObjProperty( map, "Chest", coords );

	SetName( map , "a treasure map level "+maplevel );
 
	if( x < 265 )
	       map.xwest := 0;
	else
	       map.xwest := x-265;
	endif

	if( y < 260 )
	       map.ynorth := 0;
	else
	       map.ynorth := y-260;
	endif

	map.xeast := x+265;
	map.ysouth := y+260;
	map.gumpwidth := 200;
	map.gumpheight := 200;
        map.appendpin( x, y );
 
        return 1;
endfunction
