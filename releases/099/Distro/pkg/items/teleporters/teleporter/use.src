/*
        Maintened by *Edwards
        For FantasiaShard.com
        
        Edwards@FantasiaShard.com
        
        2009-08-29
*/
use uo;
use os;
use polsys;

include ":gumps:gumps";
include ":gumps:gumps_ex";

CONST REALM_TEXT	:= 0xA001;
CONST X_TEXT		:= 0xA002;
CONST Y_TEXT		:= 0xA003;
CONST Z_TEXT		:= 0xA004;
CONST KEYWORD_TEXT	:= 0xA005;
CONST NAME_TEXT		:= 0xA006;
CONST GRAPHIC_TEXT	:= 0xA007;

CONST CANCEL_BTN	:= 0xB000;
CONST OKAY_BTN		:= 0xB001;

program UseScript(who, tile)

	if( who.cmdlevel <= 2 )
		return 0;
	endif
	
	SetUpWalkTele( who, tile );

        return 1;
endprogram

function SetUpWalkTele( who, tile )

	var dest := tile.GetDestination();
		
	var gump := GFCreateGump();
	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 450, 235 );
	GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 420, 35 );
	GFTextMid( gump, 15, 20, 430, 2100, "Teleporter Setup Menu" );
	
	GFResizePic( gump, 15, 55, GFGetCfgConst( "Defaults", "ForeGround" ), 420, 50);
	GFTextLine( gump, 20, 60, 2100, "Use the fields below to change the coordinates the" );
	GFTextLine( gump, 20, 80, 2100, "teleporter sends any mobiles to that walk over it." );
	
	GFResizePic( gump, 15, 110, GFGetCfgConst( "Defaults", "ForeGround" ), 420, 111 );
	GFTextLine( gump, 20, 120, 2100, "Realm" );
	GFResizePic( gump, 120, 115, GFGetCfgConst( "Backgrounds", "GOLDTRIM" ), 100, 30 );
	GFTextEntry( gump, 127, 120, 80, 20, 2100, dest.realm, REALM_TEXT);
	
	GFTextLine( gump, 20, 143, 2100, "X Coordinate" );
	GFResizePic( gump, 120, 138, GFGetCfgConst( "Backgrounds", "GOLDTRIM" ), 100, 30 );
	GFTextEntry( gump, 127, 143, 80, 20, 2100, CInt( dest.x ), X_TEXT );
	
	GFTextLine( gump, 20, 166, 2100, "Y Coordinate");
	GFResizePic( gump, 120, 161, GFGetCfgConst("Backgrounds", "GOLDTRIM"), 100, 30);
	GFTextEntry( gump, 127, 166, 80, 20, 2100, CInt( dest.y ), Y_TEXT );
	
	GFTextLine( gump, 20, 189, 2100, "Z Coordinate" );
	GFResizePic( gump, 120, 184, GFGetCfgConst( "Backgrounds", "GOLDTRIM" ), 100, 30 );
	GFTextEntry( gump, 127, 189, 80, 20, 2100, CInt( dest.z ), Z_TEXT );
	
	GFAddButton( gump, 304, 135, 2076, 2075, GF_CLOSE_BTN, OKAY_BTN );
	GFAddButton( gump, 300, 175, 2121, 2120, GF_CLOSE_BTN, CANCEL_BTN );
	
	var input := GFSendGump( who, gump );
	if( input[OKAY_BTN] )
		dest.x := CInt( GFExtractData( input, X_TEXT ));
		dest.y := CInt( GFExtractData( input, Y_TEXT ));
		dest.z := CInt( GFExtractData( input, Z_TEXT ));
		dest.realm := CStr( GFExtractData( input, REALM_TEXT ));
				
		tile.SetDestination( dest.x, dest.y, dest.z, dest.realm );
		
		SendSysMessage( who, "Done." );
	else
		SendSysMessage( who, "Cancelled." );
	endif
 
        return 1;
endfunction
