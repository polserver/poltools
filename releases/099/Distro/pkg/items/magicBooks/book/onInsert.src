/*
        Created by *Edwards
        
        2009-08-15
        
        To-Do
             * Destroy scroll if failure.
                       - Must wait for pol development to fix bug when destroying item into the process.
                       - 2009-10-21 pol099 fixed destroyItem() inside caninsert
                       
        Last Modifications 2009-10-30
*/
use uo;
use os;

include "include/itemUtil";
include ":attributes:attributes";
include "include/string";
include "include/security";

program on_insert( mobile, book, movetype, inserttype, item, existing_stack, amt );

        //Avoid compiler warnings
        existing_stack := existing_stack;
        inserttype := inserttype;
        amt := amt;
 
        if( movetype == MOVETYPE_COREMOVE )
		return 1;
       	elseif( !useItem_check( mobile, book, ITEM_INCONTAINER ))
                FinishAction( mobile, item );
		return 0;
        elseif( !item.IsMagicScroll() )
                FinishAction( mobile, item, "This is not a magic scroll." );
                return 0;
	endif
 
        var book_type := book.BookType(),
            item_type := item.ScrollType();
            
        if( book_type != item_type )
                FinishAction( mobile, item, "You cannot inscribe this "+Lower( item_type )+" scroll in the "+Lower( book_type )+" book." );
                return 0;
        endif
 
        var difficulty := item.GetScrollDifficulty(),
            spell_id := item.ScrollID();
            
        if( difficulty.errortext )
                FinishAction( mobile, item, "Error reading difficulty -->"+difficulty.errortext );
		return 0;
        elseif( spell_id.errortext )
                FinishAction( mobile, item, "Error reading spell_id -->"+spell_id.errortext );
		return 0;
        elseif( book.HasSpellID( spell_id ))
                FinishAction( mobile, item, "This scroll is already scribe into the "+Lower( book_type )+" book." );
		return 0;
        endif
        
        if( SubtractAmount( item, 1 ))
                if( SkillCheck( mobile, INSCRIPTION, difficulty ) > 0 )
                        FinishAction( mobile, item, "You inscribe the magic scroll into the "+Lower( book_type )+" book." );
                        PlaySoundEffect( mobile, 0x50 );
                        book.AddSpellID( spell_id );
                else
                        FinishAction( mobile, item, "You fail and destroy the magic scroll." );
                endif
        endif

	return 1;
endprogram

function FinishAction( mobile, item, message:="" )

        if( message )
                SendSysMessage( mobile, message );
        endif
        
        if( item )
		MoveItemToContainer( item, mobile.backpack );
        endif
 
        return 1;
endfunction
