/*
        Maintened by *Edwards

        2009-10-21

        Last Modifications 2009-10-26
             * Added elemental damage functions
*/
use uo;
use os;
use util;

include "include/sounds";
include ":timedscripts:timedScripts";
include ":spells:spells_math";

program controlScript( field )

	SetObjProperty( field, "#PID", GetPid() );

	var creator := field.GetOwner();
	if( creator.IsA( POLCLASS_MOBILE ))
		SetScriptController( creator );
	endif

	// Kludge to keep this script from running before cprops are set
	SleepMS(500);

	while( field )
		var duration := "1d"+GetObjProperty( field, "ParaDuration" );
		if( !duration )
			duration := "5d3";
		endif
  
		ParalyzeNearbyMobiles( field, duration, creator );

		if( ReadGameClock() % 5 == 0 )
			// Play once every 5 seconds.
			PlaySoundEffect( field, SFX_PARALYZED );
		endif

		var event := Wait_For_Event(1);
		if( event )
			//Someone just walked onto the tile.
			//ParalyzeMobile( event, duration, creator );
		endif

		if( field.IsExpired() )
			DestroyItem( field );
			return 1;
		endif
	endwhile
endprogram

function ParalyzeNearbyMobiles( field, duration, creator )

	var nearby := ListMobilesNearLocationEX( field.x, field.y, field.z, 0, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN, field.realm );
	foreach mobile in ( nearby )
		ParalyzeMobile( mobile, duration, creator );

		SleepMS(2);
	endforeach

	return( nearby.Size() );
endfunction

function ParalyzeMobile( mobile, duration, creator  )

        duration := SPELL_GetResist( creator, mobile, 6, duration );
        TS_StartTimer( mobile, "Paralysis", RandomDiceRoll( duration ));
  
	return 1;
endfunction
