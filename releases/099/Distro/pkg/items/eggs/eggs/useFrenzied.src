/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@ Fantasia@Shard.com

        2009-07-27

        Last Modifications 2009-08-29
             * Added useItem_check()

        Last Modifications 2009-10-21
             * Cleaned codes for better performances
             * Changed difficulty of "taming" creature on creation

        Last Modifications 2010-02-27
             * Added "CanTrain" flag for attributes system
*/
use uo;
use util;
use cfgfile;

include "include/itemUtil";
include ":animaltaming:taming";
include ":attributes:attributes";
include ":brainAI:npcUtil";

program frenziedEgg( mobile, egg )

	if( !useItem_check( mobile, egg, ITEM_INCONTAINER ))
		return 0;
	endif
 
        SubtractAmount( egg, 1 );
 
        SendSysMessage( mobile, "The egg begins to move..." );
        Sleep(5);
        
 	var eggs_cfg := ReadConfigFile(":eggs:eggs"),
	    npc_elem := eggs_cfg["frenzied"],
	    npc_desc := GetConfigStringArray( npc_elem, "Entry" ),
            frenzied_template := npc_desc.RandomEntry();
 
        var the_npc := CreateNPCFromTemplate( frenzied_template, mobile.x, mobile.y, mobile.z, 0, mobile.realm );
        if( the_npc.errortext )
                SendSysMessage( mobile, "Error creating NPC -->"+the_npc.errortext );
                return 0;
        endif
        SetObjProperty( mobile, "CanTrain", mobile.serial );
        
        var npc_cfg := NPC_GetNPCConfig( frenzied_template ),
            npc_taming := CInt( npc_cfg.TameSkill );
        
        if( SkillCheck( mobile, ANIMAL_LORE, npc_taming, "", ADV_DISABLE ) > 0 )
                if( RandomInt( 100 ) < 80 )
		        SendSysMessage( mobile, "A frenzied ostard appears and accepts you as his master!" );
                        tameCreature( the_npc, mobile );
                        return 0;
                endif
        endif
        
        if( RandomInt( 100 ) < 15 )
                the_npc.Kill();
                SendSysMessage( mobile, "A frenzied ostard appears but instantly died!" );
        else
                SendSysMessage( mobile, "A frenzied ostard appears!" );
        endif

        return 1;
endprogram
