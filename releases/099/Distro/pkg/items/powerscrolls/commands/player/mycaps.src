/*
        Created by *Edwards
        For FantasiaShard.com

        2008-04-07
*/
use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":attributes:attributes";

program command_myCaps( mobile )

	if( GetProcess( CInt( GetObjProperty( mobile, "#CmdPSPid" ))))
		return 0;
	else
		SetObjProperty( mobile, "#CmdPSPid", GetPid() );
	endif
 
        var targ := mobile;
        if( mobile.cmdlevel )
                SendSysMessage( mobile, "Select the player you would like to see cap skills." );
                targ := Target( mobile );
                if( !targ )
                        SendSysMessage( mobile, "Cancelled." );
                        return 0;
                elseif( targ.IsA( POLCLASS_NPC ))
                        SendSysMessage( mobile, "You must select a player." );
                        return 0;
                elseif( !targ.IsA( POLCLASS_MOBILE ))
                        SendSysMessage( mobile, "You must select a player." );
                        return 0;
                endif
        endif
 
        var gump := GFCreateGump();
        GFClosable( gump, 0 );

	GFResizePic( gump, 0, 0, GFGetCfgConst("Defaults", "BackGround"), 310, 480 );
	GFResizePic( gump, 15, 15, GFGetCfgConst("Defaults", "ForeGround"), 280, 450 );
 
	GFTextMid( gump, 15, 30, 280, 1720, "My-Caps Screen" );
	GFAddButton( gump, 135, 440, 2128, 2129, GF_CLOSE_BTN, 0xA00 );

        GFPage( gump, 1 );

	var attributes := AP_ListAttributesByType( "Skill" );
	var num_attribs := attributes.Size();

        var y_pos := 55;
	foreach entry in ( attributes )

		GFTextLine( gump, 40, y_pos, 1153, entry );

                var cap_value := AP_GetSkillCap( targ, entry );

		GFTextLine( gump, 235, y_pos, 2100, cap_value );

                y_pos := y_pos+20;

                if( _entry_iter % 19 == 0 && _entry_iter < num_attribs )
			GFAddButton( gump, 273, 415, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1 );
			GFPage( gump, gump.cur_page+1 );
			GFAddButton( gump, 273, 55, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1 );
			y_pos := 55;
		endif

		SleepMS(2);
	endforeach

        GFSendGump( mobile, gump );

        return 1;
endprogram
