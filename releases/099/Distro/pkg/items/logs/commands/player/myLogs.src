/*
        Created by *Edwards

        2009-08-13
*/
use uo;
use os;
use cfgfile;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include "include/string";

program textcmd_mylogs( mobile )

	if( GetProcess( CInt( GetObjProperty( mobile, "#CmdLogsPid" ))))
		return 0;
	else
		SetObjProperty( mobile, "#CmdLogsPid", GetPid() );
	endif

        //Initialize the gump
        var gump := GFCreateGump();

        //Load Main Background
	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 250, 425 );

        GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 220, 25 );
	GFTextMid( gump, 15, 17, 220, 1720, "My Logs" );

        //BackgroundAreas
	GFResizePic( gump, 15, 45, GFGetCfgConst( "Defaults", "ForeGround" ), 220, 370 );

        var lumber_cfg := ReadConfigFile( ":logs:itemdesc" );

        var log_list := array{};
        foreach item in EnumerateItemsInContainer( mobile.backpack );
                if( !item.IsLog() )
                        continue;
                endif

                log_list.Append({ lumber_cfg[item.objtype].IName, item.amount });
                SleepMS(5);
        endforeach

        if( Len( log_list ) < 1 )
                SendSysMessage( mobile, "You have no logs in your backpack." );
		return 0;
        endif

	var num := log_list.Size();

	var countery := 0;

	var y_pos := 80,
            x_pos := 80;

	GFPage( gump, 1 );
	foreach log in ( log_list )

		GFTextLine( gump, x_pos, y_pos, 2100, UCFirst( log[1] )+" logs" );
                GFTextLine( gump, x_pos, y_pos+20, 2100, log[2] );

		y_pos += 60;
		countery += 1;

		if ( countery >= 5 && _log_iter < num )
			GFAddButton( gump, 220, 385, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1 );
			GFPage( gump, gump.cur_page+1 );
			GFAddButton( gump, 220, 50, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1 );
			countery := 0;
			y_pos := 80;
		endif
		SleepMS(2);
	endforeach

        GFSendGump( mobile, gump );

        return 1;
endprogram
