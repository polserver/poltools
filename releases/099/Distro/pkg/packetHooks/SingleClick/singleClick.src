/*
        Maintened by *Edwards
        for FantasiaShard.com
        
        Edwards@FantasiaShard.com
        
        2008-06-15
        
        Last Modifications 2009-10-18
             * Added NameSpec option for specification of a given object ex.: active or disable
*/
use uo;
use polsys;

program Install()

        print("INSTALLING: Singleclick." );
        return 1;
endprogram

exported function OnSingleClick( who, byref packet )

         var serial := packet.GetInt32(1);
         var object := SystemFindObjectBySerial( serial );

         if( object.IsA( POLCLASS_ITEM ))
                  define_Packet( who, serial, object, object.desc );
           
                  var forgedType := GetObjProperty( object, "ExceptionalType" ),
                      forgedTag := CInt( GetObjProperty( object, "Exceptional" )),
                      poison_charges := CInt( GetObjProperty( object, "PoisonCharges" )),
                      skinned_by := CInt( GetObjProperty( object, "Skinned" ));
                  
                  if( forgedType && forgedTag )
                           var nameID := SystemFindObjectBySerial( forgedTag );
                           define_Packet( who, serial, object, "["+forgedType+" "+nameID.name+"]" );
                  elseif( poison_charges )
                           define_Packet( who, serial, object, "[Poison Charges: "+poison_charges+"]" );
                  elseif( object.newbie )
                           define_Packet( who, serial, object, "[Blessed]" );
                  elseif( skinned_by )
                           var nameID := SystemFindObjectBySerial( skinned_by );
                           define_Packet( who, serial, object, "[Skinned by "+nameID+"]" );
                  elseif( GetObjProperty( object, "TrapsRevealed" ))
                           define_Packet( who, serial, object, "[Trapped]" );
                  elseif( GetObjProperty( object, "Fish" ))
                           var nameID := SystemFindObjectBySerial( GetObjProperty( object, "Fish" ));
                           define_Packet( who, serial, object, "["+nameID.name+"]" );
                  elseif( GetObjProperty( object, "NameSpec" ))
                           define_Packet( who, serial, object, "["+GetObjProperty( object, "NameSpec" )+"]" );
                  endif
           
                  return 1;
         endif

         return 0;
endfunction

function define_Packet( who, serial, object, description := "" )

         var item_objtype := object.objtype,
             text_color := GetObjectColor( object ),
             newpacket := CreatePacket( 0x1C, MSGLEN_VARIABLE );

         newpacket.SetInt16( 1, newpacket.GetSize() );
         newpacket.SetInt32( 3, serial );
         newpacket.SetInt16( 7, item_objtype );
         newpacket.SetInt16( 10, text_color );
         newpacket.SetInt8( 9, 6 );
         newpacket.SetInt16( 12, 3 );
         newpacket.SetString( 14, "System", 0 );

         var nameLen := Len( description ),
             fillers_needed := ( 30 - nameLen ),
             offset := 14 + nameLen;

         var i;
         for( i:=1; i<=fillers_needed; i+=1 )
                  newpacket.setint8( offset+i, 0 );
         endfor

         var new_offset := offset + fillers_needed;
         
         newpacket.SetString( new_offset, description, 1 );
         newpacket.SetInt16( 1, newpacket.GetSize() );
         newpacket.SendPacket( who );
           
         return 1;
endfunction

function GetobjectColor( object )

         var new_color := GetObjProperty( object, "HookColor" );
         if( new_color )
                  return new_color;
         elseif( GetObjProperty( object, "UnIDedMagicalItem" ))
                  return 55;
         elseif( object.IsA( POLCLASS_CORPSE ))
                  var mobile := SystemFindObjectBySerial( object.ownerserial );
                  if( mobile.murderer )
                           return 38;
                  elseif( mobile.criminal )
                           return 905;
                  else
                           return 90;
                  endif
         elseif( GetObjProperty( object, "swings" ))
                  var hp := CInt( GetObjProperty( object, "swings" )),
                      max_hp := CInt( 100 );
                      
                  return GetHPPercent( hp, max_hp );
         elseif( object.IsA( POLCLASS_EQUIPMENT ))
                  var hp := CInt( object.hp ),
                      max_hp := CInt( object.maxhp );
                      
                  return GetHPPercent( hp, max_hp );
         else
                  return 90;
         endif
endfunction

function GetHPPercent( hp, max_hp )

         var percent_hp := CInt(( hp * 100 ) / max_hp ),
             color := 50;
             
         if( percent_hp >= 85 )
                  color := 90;
         elseif( percent_hp <= 15 )
                  color := 33;
         endif
             
         return color;
endfunction
