/*
        Created by *Edwards

        2010-01-03
*/
use uo;

include ":accounts:mailSystem";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":gumps:gumpprompt";
include ":datafile:datafile";

const UPDATE_EMAIL := 0x0A1;
const UPDATE_PASS := 0x0A2;

var email_datafile := DFOpenDataFile( ":accounts:emails", DF_CREATE );
var password_datafile := DFOpenDataFile( ":accounts:password", DF_CREATE );
 
program textcmd_myaccount( mobile )

	if( GetProcess( CInt( GetObjProperty( mobile, "#CmdMAPid" ))))
		return 0;
	else
		SetObjProperty( mobile, "#CmdMAPid", GetPid() );
	endif

        var account := FindAccount( mobile.acctname );

        var gump := GFCreateGump();
        
	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 320, 225 );
	GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 290, 195 );
	GFTextMid( gump, 15, 20, 290, 1720, "Account Manager" );

	GFPage( gump, 1 );
 
        GFAddButton( gump, 40, 103, 2117, 2118, GF_PAGE_BTN, 2 );
	GFTextLine( gump, 60, 100, 1720, "Email Address" );

        GFAddButton( gump, 40, 123, 2117, 2118, GF_PAGE_BTN, 3 );
	GFTextLine( gump, 60, 120, 1720, "Password" );

        var email := BuildEmailMenu( gump, mobile );
        var password := BuildPasswordMenu( gump );

	var input := GFSendGump( mobile, gump );
        if( !input )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        endif
        
	if( input[UPDATE_PASS] )
		var new_pass := GFExtractData( input, password );
		if( new_pass[" "] )
			GumpPrompt( mobile, "Invalid Password: Your password can not have any spaces in it." );
		elseif( len( new_pass ) < 4 )
			GumpPrompt( mobile, "Invalid Password: Your password must be atleast 4 characters." );
		elseif( len( new_pass ) > 10 )
			GumpPrompt( mobile, "Invalid Password: Your password can not be more than 10 characters." );
		else
			var res := account.SetPassword( new_pass );
			if( res.errortext )
				GumpPrompt( mobile, res.errortext );
			else
                                var data_elem := DFFindElement( password_datafile, mobile.acctname, DF_CREATE );
				data_elem.SetProp( "LastPasswordUpdate", polcore().systime );
				GumpPrompt( mobile, "Your password has been changed to ["+new_pass+"]." );
			endif
		endif
	elseif( input[UPDATE_EMAIL] )
 
                if( !SaveMail( mobile, input, email ))
			GumpPrompt( mobile, "Invalid email: Your email address has not been updated." );
                        return 0;
                endif
  
		GumpPrompt( mobile, "Your email address has been updated." );
	endif
 
        return 1;
endprogram

function BuildEmailMenu( byref gump, mobile )

	GFPage( gump, 2 );

	GFTextLine( gump, 40, 50, 1720, "Email Address:" );
	GFResizePic( gump, 40, 70, GFGetCfgConst( "Backgrounds", "GOLDTRIM" ), 240, 30 );

	var email := GFTextEntry( gump, 50, 75, 230, 25, 1720, CheckEmail( mobile ));

        var text := "Notice: Your mail address will be kept confidential, and will only be used
                     for official shard business.";
                     
        GFDrawWrappedText( gump, 40, 110, GFWordWrap( text, 300 ));

        GFAddButton( gump, 30, 183, 2117, 2118, GF_CLOSE_BTN, UPDATE_EMAIL );
	GFTextLine( gump, 50, 180, 1720, "Update" );
 
        GFAddButton( gump, 240, 183, 2117, 2118, GF_PAGE_BTN, 1 );
	GFTextLine( gump, 260, 180, 1720, "Back" );
 
        return email;
endfunction

function BuildPasswordMenu( byref gump )

	GFPage( gump, 3 );

	GFTextLine( gump, 40, 50, 1720, "New Password:" );
	GFResizePic( gump, 40, 70, GFGetCfgConst( "Backgrounds", "GOLDTRIM" ), 240, 30 );

	var password := GFTextEntry( gump, 50, 75, 230, 25, 1720 );

        var text := "Notice: We will NEVER ask you your password. Never share your accounts.";

        GFDrawWrappedText( gump, 40, 110, GFWordWrap( text, 300 ));

        GFAddButton( gump, 30, 183, 2117, 2118, GF_CLOSE_BTN, UPDATE_PASS );
	GFTextLine( gump, 50, 180, 1720, "Update" );
 
        GFAddButton( gump, 240, 183, 2117, 2118, GF_PAGE_BTN, 1 );
	GFTextLine( gump, 260, 180, 1720, "Back" );
 
        return password;
endfunction
