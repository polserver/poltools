/*
        Created by *Edwards

        2011-06-27
*/
use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":security:report";
include ":jailKeeper:jailKeeper";
include ":datafile:datafile";
include ":security:commandReport";
include "include/security";

program textcmd_listAccts( mobile )

        if( !AuthorizationCode( mobile ))
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        endif

        var gump := GFCreateGump();

	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 310, 420 );
	GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 280, 390 );

	GFTextLine( gump, 25, 20, 2100, "Player Name" );
	GFTextLine( gump, 240, 20, 2100, "Status" );

	var acct_list := ListAccounts();
	var num := acct_list.Size();

	var counter := 0;
	var y_pos := 45;

	GFPage( gump, 1 );
	foreach acctname in ( acct_list )

                var account := FindAccount( acctname );

                GFAddButton( gump, 25, y_pos+3, 2117, 2118, GF_CLOSE_BTN, _acctname_iter );
                GFTextLine( gump, 45, y_pos, 2100, account.name );

                if( account.banned )
		        GFTextLine( gump, 240, y_pos, 33, "Banned" );
                elseif( !account.enabled )
		        GFTextLine( gump, 240, y_pos, 48, "Disabled" );
                else
		        GFTextLine( gump, 240, y_pos, 63, "Active" );
                endif


		y_pos += 20;
		counter += 1;

		if ( counter >= 18 && _acctname_iter < num )
			GFAddButton( gump, 283, 385, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1 );
			GFPage( gump, gump.cur_page+1 );
			GFAddButton( gump, 283, 40, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1 );
			counter := 0;
			y_pos := 40;
		endif
		SleepMS(2);
	endforeach

	var input := GFSendGump( mobile, gump );
        if( !input )
		SendSysMessage( mobile, "Cancelled" );
		return 0;
	endif

	input := input[0];

        var acct_name := acct_list[input];

        var script := Start_Script( ":accounts:commands/admin/acctInfo", array{mobile, acct_name} );
        if( script.errortext )
                SendSysMessage( mobile, "Error starting script -->"+script.errortext );
                return 0;
        endif

        return 1;
endprogram
