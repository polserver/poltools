/*
        Maintened by *Edwards
        
        2009-11-08
*/
use uo;
use os;
use polsys;

include ":brainAI:npcNerves";
include ":brainAI:npcWatchList";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include "include/security";

program textcmd_NPCStatus(who)

        if( !AuthorizationCode( who ))
                SendSysMessage( who, "Cancelled." );
                return 0;
        endif
        
	var targ := Target( who, TGTOPT_NOCHECK_LOS );
	if( !targ.IsA( POLCLASS_NPC ))
		SendSysMessage( who, "Cancelled." );
		return 0;
	endif
	
	var gump := GFCreateGump();
	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 400, 480 );
	GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 370, 450 );
	GFTextMid( gump, 0, 15, 300, 2100, "Brain NPC Info" );
	
	var text := "<B>NERVES</B><BR>";			
	foreach nerve in ( LoadNerves( targ ))
		var state := GetProcess( nerve.pid ).state;
		if( !state )
			state := "PID Killed";
		endif
		
		text += "<U>"+_nerve_iter+"</U><BR>"+
			"Pid: "+nerve.pid+"<BR>"+
			"Flags: "+nerve.flags+"<BR>"+
			"Status: "+state+"<BR><BR>";
			
		SleepMS(2);
	endforeach
	
	text := text+"<BR><B>WATCH LIST</B><BR>";
	foreach serial in ( NPC_LoadWatchList( targ ).Keys() )
		var name := SystemFindObjectBySerial( CInt( serial )).name;
		text += Hex( serial )+" "+name+"<BR>";
		SleepMS(2);
	endforeach
	
	GFHTMLArea( gump, 25, 40, 357, 423, text, 0, 1 );
	
	GFSendGump( who, gump );
 
	return 1;
endprogram
