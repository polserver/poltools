/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2009-12-28
        
        Last Modifications 2010-02-04
             * Cleaned codes for better performances.
*/
use uo;
use os;

include "include/facings";
include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcWatchList";

program BrainNerve( params )

	var npc		:= params[1];
	//var nerve_name	:= params[2];
	var event	:= params[3];
	var gsettings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.

	while( npc )
		if( event )
			case( event.type )
				SYSEVENT_ENTEREDAREA:
					EnteredArea( npc, event, gsettings, scripts );
					break;
				default:
					break;
			endcase

			event := 0;
		endif

		SleepMS(5);
		event := Wait_For_Event(1);
	endwhile

	return 1;
endprogram

function EnteredArea( npc, byref event, byref settings, byref scripts )
        
        var source := event.source;

	if( NPC_ShouldWatch( npc, source, settings, scripts ))
		if( NPC_AddToWatchList( npc, source ))
			AI_WakeUp( npc );
                        SpoutTheNews( npc, source );
			return 1;
		endif
	endif

	return 1;
endfunction

function SpoutTheNews( npc, source )

        var next_spam := CInt( GetObjProperty( npc, "#DelaySpam" ));
        if( next_spam > ReadGameClock() )
                return 0;
        else
                SetObjProperty( npc, "#DelaySpam", ReadGameClock() + 30 );
        endif
        
	var news := GetObjProperty( npc, "News" );
	if( !news )
                AI_Speak( npc, "No news is good news!" );
		return 0;
	endif
 
	var i;
	for( i:=1; i<=Len( news ); i+=1 )
                TurnObjectToward( npc, source.x, source.y );
                SleepMS(5);
                AI_Speak( npc, news[i] );
		Sleep(2);
	endfor

	return 1;
endfunction
