/*
        Maintened by *Edwards

        Last Modifications 2009-02-08

        Last Modifications 2009-12-21
             * Added check if player hidden or NPC to return 0
*/
use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":attributes:attributes";
include ":loot:identify";

program BrainNerve( params )

	var npc		:= params[1];
	var event	:= params[3];
	params := 0; // Not needed anymore.

	while( npc )
		if( event )
			case( event.type )
				SYSEVENT_ITEM_GIVEN:
					ItemGivenEvent( npc, event );
					break;
				default:
					break;
			endcase

			event := 0;
		endif

		SleepMS(5);
		event := Wait_For_Event(1);
	endwhile

        return 1;
endprogram

function ItemGivenEvent( npc, byref event )

	var item := event.item;
	var source := event.source;

        ReleaseItem( item );
 
        if( source.hidden || source.IsA( POLCLASS_NPC ))
		return 1;
        endif

        AI_Turn( npc, event.source, NETURN_TOWARD );
        
	if( item.objtype == 0xeed )
		SendCommands( npc, source, item );
	else
                AI_Speak( npc, "Thou art most kind of wanting to give away such a thing." );
		MoveItemToContainer( item, source.backpack );
		Sleep( 4 );
                AI_Speak( npc, "But giving a money donation instead would be more appreciated." );
		Sleep( 3 );
                AI_Speak( npc, "*smile*" );
	endif

	return 1;
endfunction

function SendCommands( npc, byref source, item )

	var demand := GetObjProperty( source, "#PriestDemand" );
	if( !demand )
		if( GetObjProperty( source, "PriestUpset" ))
			if( item.amount >= AP_GetTrueStat( source, STRENGTH ) * 2 )
                                AI_Speak( npc, "Thee be blessed for this generous donation." );
				EraseObjProperty( source, "PriestUpset" );
			else
				AI_Speak( npc, "Dost thou think that he can be forgiven for such a pitiful amount of gold?" );
			endif
		else
			AI_Speak( npc, "Thee be blessed for this generous donation." );
		endif
  
		DestroyItem( item );
		return 1;
	endif
 
        var item_effect := SystemFindObjectBySerial( CInt( demand[3] ));
        if( !item_effect )
                AI_Speak( npc, "I could not find what you are asking for help." );
                return 0;
        endif

	EraseObjProperty( source, "#PriestDemand" );

	var price := demand[2];
	if( CInt( price ))
		if( item.amount < price )
                        AI_Speak( npc, "I was very generous by asking thee such a low price as "+price+" gold pieces for that service." );
			MoveItemToContainer( item, source.backpack );
			Sleep( 3 );
			AI_Speak( npc, "So I had expected thee to have the politeness to give me what I asked for!" );
			SetObjProperty( source, "PriestUpset", 1 );
			return 0;
		endif
	endif

	case( demand[1] )
		"RemoveCurse":		RemoveCurse( npc, source, item_effect, item, price );
					break;

		"DetectCurse":		DetectCurse( npc, item_effect, item, price );
					break;

		"Purify":		Purify( npc, item_effect, item, price );
					break;

		"bless":                DestroyItem( item );
                                        SendSpell( npc, source, "bless" );
					break;

		"cure":                 DestroyItem( item );
                                        SendSpell( npc, source, "cure" );
					break;

		"heal":		        DestroyItem( item );
                                        SendSpell( npc, source, "heal" );
					break;

		"protection":		DestroyItem( item );
                                        SendSpell( npc, source, "protection" );
					break;

		"dispel":		DestroyItem( item );
                                        SendSpell( npc, source, "dispel" );
					break;
	endcase
 
 	EraseObjProperty( source, "#PriestDemand" );
 
        return 1;
endfunction

function RemoveCurse( npc, byref source, byref item_effect, byref item, price )

	var chance := 75,
	    chance_mod;

	if( item.amount > price )
		chance_mod := CInt(( item.amount - price ) / 10 );
                chance += chance_mod;
	endif

	DestroyItem( item );
        item.movable := 0;

        AI_Speak( npc, "Powers of the Virtues, I'm calling upon you to remove the curse from that item!" );
	PerformAction( npc, 0x11 );
	Sleep( 6 );
 
        item.movable := 1;

	if( RandomInt( 100 ) <= chance )
 		PlaySoundEffect( source, 0x01eb );
		PlayObjectCenteredEffect( source, 0x373a, 10,10 );
  
		SetObjProperty( item_effect, "Cursed", 3 );
                AI_Speak( npc, "From now on thee can unequip this "+item_effect.desc+" as any normal item." );
	else
                PlaySoundEffect( npc, 0x005b );
		PlayObjectCenteredEffect( npc, 0x3735, 7, 7 );
  
                AI_Speak( npc, "I failed in removing that curse!" );
		Sleep( 2 );
                AI_Speak( npc, "Maybe a greater donation will be worth!" );
        endif
 
        return 1;
endfunction

function DetectCurse( npc, byref item_effect, byref item, price )

	var chance := 90,
	    chance_mod;

	if( item.amount > price )
		chance_mod := CInt(( item.amount - price ) / 2 );
                chance += chance_mod;
	endif

	DestroyItem( item );
        item.movable := 0;

        AI_Speak( npc, "Powers of the Virtues, I'm calling upon you to show us if that item is under malefic influences!" );
	PerformAction( npc, 0x11 );
	Sleep( 6 );
 
        item.movable := 1;

	if( RandomInt( 100 ) <= chance )
                if( GetObjProperty( item_effect, "Cursed" ) == 1 )
 		        PlaySoundEffect( npc, 0x01eb );
		        PlayObjectCenteredEffect( npc, 0x373a, 10,10 );
          
                        //RevealCurse( item );
			AI_Speak( npc, "Ah ah! That item is cursed and it revealed itself to be a "+item_effect.desc+"." );

		else
			AI_Speak( npc, "I don't sense presence of any curses in that item." );
		endif
	else
                PlaySoundEffect( npc, 0x005b );
		PlayObjectCenteredEffect( npc, 0x3735, 7, 7 );

                AI_Speak( npc, "I failed to see if that item is cursed!" );
		Sleep( 2 );
                AI_Speak( npc, "Maybe a greater donation will be worth!" );
	endif
  
        return 1;
endfunction

function Purify( npc, byref item_effect, byref item, price )

	var chance := 50,
	    chance_mod;

	if( item.amount > price )
		chance_mod := CInt(( item.amount - price ) / 100 );
                chance += chance_mod;
	endif

	DestroyItem( item );
        item.movable := 0;

        AI_Speak( npc, "Great Essence of the Virtues, I'm calling upon you to purify this item from all curses!" );
	PerformAction( npc, 0x11 );
	Sleep( 2 );
	PerformAction( npc, 0x11 );
	Sleep( 2 );
	PerformAction( npc, 0x11 );
	Sleep( 2 );
	PerformAction( npc, 0x11 );
	Sleep( 2 );
	PerformAction( npc, 0x11 );
	Sleep( 10 );
 
        item.movable := 1;

	if( RandomInt( 100 ) <= chance )
 		PlaySoundEffect( npc, 0x01eb );
		PlayObjectCenteredEffect( npc, 0x373a, 10,10 );

		BanishCurse( item_effect );
                AI_Speak( npc, "I made it!!! That item is now completely free from all curses!" );
	else
                PlaySoundEffect( npc, 0x005b );
		PlayObjectCenteredEffect( npc, 0x3735, 7, 7 );

                AI_Speak( npc, "That curse is too strong, I failed to obliterate it!" );
		Sleep( 2 );
                AI_Speak( npc, "Maybe a greater donation will be worth!" );
        endif
        
        return 1;
endfunction

function SendSpell( npc, byref source, spell )

        var spell_id;
	case( spell )
		"bless":	spell_id := 17;
                                AI_Speak( npc, "I humbly call upon the Virtues to lay down thine blessing on this person!" );
		                break;

		"cure":		spell_id := 11;
                                AI_Speak( npc, "Essence of Virtues, cure this corpse from all poison!" );
				break;

		"heal":		spell_id := 29;
                                AI_Speak( npc, "Great Virtue of Compassion, bring down thine kindness upon that soul by healing him!" );
				break;

		"protection":	spell_id := 15;
                                AI_Speak( npc, "Valor and Courage, I'm calling upon you to harden this folk's skin!" );
				break;

		"dispel":	spell_id := 41;
                                AI_Speak( npc, "Virtue of Darkness, dispel this folk's skin!" );
				break;
	endcase
 
	PerformAction( npc, 0x11 );
	Sleep( 2 );
 
 	var script := Start_Script( ":magery:spellStarter", array{npc, spell_id, 0, source} );
	if( script.errortext )
		PrintTextAbove( npc, script.errortext );
		return 0;
	endif
 
        return 1;
endfunction
