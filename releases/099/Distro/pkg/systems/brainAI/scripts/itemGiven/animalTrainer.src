/*
        Maintened by *Edwards
        For FantasiaShard.com
        
        Edwards@FantasiaShard.com

        Last Modifications 2009-02-08
        
        Last Modifications 2009-12-21
             * Added check if player hidden or NPC to return 0
*/
use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:merchantUtil";
include ":animaltaming:taming";

program BrainNerve( params )

	var npc		:= params[1];
	var event	:= params[3];
	params := 0; // Not needed anymore.

	while( npc )
		if( event )
			case( event.type )
				SYSEVENT_ITEM_GIVEN:
					ItemGivenEvent( npc, event );
					break;
				default:
					break;
			endcase

			event := 0;
		endif

		SleepMS(5);
		event := Wait_For_Event(1);
	endwhile

        return 1;
endprogram

function ItemGivenEvent( npc, byref event )

	var item := event.item;
	var source := event.source;

        ReleaseItem( item );
 
        if( source.hidden || source.IsA( POLCLASS_NPC ))
		return 1;
        endif

        AI_Turn( npc, event.source, NETURN_TOWARD );
        
        if( item.objtype == 0xeed )
                TrainSkill( npc, source, item );
	elseif( LoadTicket( npc, source, item ))
                return 1;
        endif

	return 0;
endfunction

function LoadTicket( npc, byref source, byref item );

        var template := GetObjProperty( item, "Template" );
        var color := GetObjProperty( item, "Color" );
        var name := GetObjProperty( item, "Name" );

        if( item.objtype != 0xF022 || !template )
                AI_Speak( npc, "I have nowhere to put '"+item.desc+"', but thank you anyway.", SPEAK_TEXTTYPE_DEFAULT, SPEAK_DOEVENT_ENABLE );
                MoveItemToContainer( item, source.backpack );
                return 0;
        endif

        DestroyItem( item );
        
        Sleep(5);

        var creature := CreateNpcFromTemplate( template, npc.x, npc.y, npc.z, 0, npc.realm );
        creature.color := color;
        creature.name := name;

        SetObjProperty( creature, "#Peacemaking", ReadGameClock() + 5 );
        tameCreature( creature, source );
 
        AI_Speak( npc, "Take care of "+creature.name+" and be sure to feed it!" );
        
        return 1;
endfunction
