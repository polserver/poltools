/*
        Created by *Edwards
        For FantasiaShard.com
        
        Edwards@FantasiaShard.com

        2009-12-22
*/
use uo;
use os;
use util;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":totem:totem";

var following := 0,
    chaseloop := 0;

program BrainNerve( params )

	var npc		:= params[1];
	var nerve_name	:= params[2];
	var source	:= params[3];
	//var scripts	:= params[4];
	params := 0; // Not needed anymore.
        
        SetObjProperty( npc, "#Follow", source.serial );
        AI_SetAnchor( npc, npc.x, npc.y, 0, 100 );
        
	while( npc )
                if( !canFollow( npc, source ))
			// No need to follow any longer.
			AI_EndNerve( npc, nerve_name );
			break;
                endif

                Follow( npc, source );

		SleepMS(400);
		AI_ClearThoughts( npc, CLR_NERVE );
	endwhile
 
        return 1;
endprogram

function canFollow( npc, source )
        
        if( npc.opponent )
                return 0;
        elseif( npc.frozen || npc.paralyzed )
                return 0;
        elseif( npc.hidden || npc.concealed )
                return 0;
        elseif( source.hidden || source.concealed )
                return 0;
        elseif( source.dead )
                return 0;
        elseif( !source.connected )
                return 0;
        elseif( source.warmode && !IsTotem( npc ))
                PrintTextAbovePrivate( npc, "The creature looks hesitant to follow you.", source );
                return 0;
        elseif( GetObjProperty( npc, "#Follow" ) != source.serial )
                return 0;
        endif
        
        return 1;
endfunction

function Follow( npc, source )
                
	var dist := CoordinateDistance( npc.x, npc.y, source.x, source.y );
	if( dist < 2 )
		chaseloop := 0;
        elseif( chaseloop > 10 )
                npc.facing := source.facing;
                MoveObjectToLocation( npc, source.x, source.y, source.z, source.realm, MOVEOBJECT_FORCELOCATION );
		chaseloop := 0;
	elseif( dist > 10 && chaseloop > 10 )
                chaseloop += 1;
        else
		AI_Move( npc, source.x, source.y, source.z, NEMOVE_TOWARD, NEMOVE_RUN, 1 );
	endif

	if( !CheckLineOfSight( npc, source ))
		chaseloop += 1;
	endif
 
        return 1;
endfunction
