/*
        Created by *Edwards

        2011-07-02
*/
use uo;
use os;

include "include/security";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":remoteScripts:settings";

program textcmd_forceScript( mobile, text )

        if( !AuthorizationCode( mobile ))
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        endif

        //Initialize the gump
        var gump := GFCreateGump();

        //Load Main Background
	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 250, 425 );

        GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 220, 25 );
	GFTextMid( gump, 15, 17, 250, 1720, "Force Remote Script" );

	GFResizePic( gump, 15, 45, GFGetCfgConst( "Defaults", "ForeGround" ), 220, 370 );
        GFTextMid( gump, 15, 60, 250, 1720, "Script Menu" );

        var scripts_list := RS_GetScripts(),
	    num := scripts_list.Size(),
	    counter := 0,
	    y_pos := 100;

	GFPage( gump, 1 );
	foreach scriptName in ( scripts_list )

		GFTextLine( gump, 30, y_pos, 2100, scriptName );
		GFAddButton( gump, 200, y_pos+3, 2117, 2118, GF_CLOSE_BTN, _scriptName_iter );

		y_pos += 20;
		counter += 1;

		if( counter >= 15 && _scriptName_iter < num )
			GFAddButton( gump, 220, 380, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1 );
			GFPage( gump, gump.cur_page+1 );
			GFAddButton( gump, 220, 100, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1 );
			counter := 0;
			y_pos := 100;
		endif
		SleepMS(2);
	endforeach

        //And now we need to know the answer...
        var input := GFSendGump( mobile, gump );
        if( !input )
                 SendSysMessage( mobile, "Cancelled." );
                 return 0;
        endif

        input := input[0];

        var selected_script := scripts_list[input],
            cfg_elem := RS_GetSettingsCfgElem( selected_script ),
            script_path := CStr( cfg_elem.Path );
            
        var script := Start_Script( script_path );
        if( script.errortext )
                SendSysMessage( mobile, "Error starting script -->"+script.errortext );
                return 0;
        endif
        
        SendSysMessage( mobile, "Done!" );
        
        return 1;
endprogram
