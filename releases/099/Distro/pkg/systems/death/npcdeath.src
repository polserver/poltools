/*
        Maintened by *Edwards

        Last Modifications 2008-12-02
        
        Last Modifications 2008-12-02
             * LCFirst with corpse name

        Last Modifications 2009-08-29
             * Removed loot-creation for creature resurrected.

        Last Modifications 2009-10-25
             * Cleaned codes for better performances
*/
use uo;
use os;
use util;
use cfgfile;

include ":containers:useCorpse";
include ":loot:lootParser";
include ":merchants:storage";
include ":timedscripts:timedScripts";
include ":death:death";
include ":mounts:mounts";
include ":spawnpoints:manager";
include ":spawnpoints:spawnpoints";
include "include/string";

program core_npcDeath( params )

	var corpse := params[1];

	TS_Death( corpse ); // Will still get the PID from the corpse.
        SP_OnDeath( corpse );

	var template := corpse.NPCTemplate();
 
        SetName( corpse, corpse.name );

        if( GetObjProperty( corpse, "NPCBoss" ))
                SetObjProperty( corpse, "HookColor", 23 );
        else
                case( template.Alignment )
                       "good" :        SetObjProperty( corpse, "HookColor", 90 );     break;
                       "neutral" :     SetObjProperty( corpse, "HookColor", 905 );    break;
                       "evil" :        SetObjProperty( corpse, "HookColor", 38 );     break;
                endcase
        endif
        
        var master := GetObjProperty( corpse, "Master" );
        if( master )
                var npc_master := SystemFindObjectBySerial( master );
                SendStatus( npc_master );
        endif

        CPM_DeleteContainers( corpse );
        MP_DeathUnmount( corpse );
        DeleteNPCEquip( corpse );
        DP_PlayDeathSound( corpse );

	var death_script := template.DeathScript;
	if( death_script )
		var result := Start_Script( death_script, corpse );
		if( result.errortext )
			PrintTextAbove( corpse, "Error::Death Script -->"+result.errortext );
		endif
	endif
        
 	if( GuardKillCheck( corpse, template ))
		return 1;
        endif
        
        if( !GetObjProperty( corpse, "PreviouslyRes" ) && !GetObjProperty( corpse, "NoLoot" ) && !GetObjProperty( corpse, "SummonedBy" )) //Prevents multiple loot if creature resurrected.
                var loot := Loot_Generate( corpse, template );
                if( loot.errortext )
                        Print( loot.errortext );
                endif
        endif
        
        NoCorpseCheck( corpse, template );
        AutoOpen( corpse );

        var reputation := Start_Script( ":reputation:adviseAttacker", corpse );
	if( reputation.errortext )
		PrintTextAbove( corpse, "Error::Death Script -"+reputation.errortext);
	endif

        return 1;
endprogram

function GuardKillCheck( corpse, template )

	if( template.GuardKill || GetObjProperty( corpse, "GuardKill" ));
	        // No corpse. Don't drop backpack contents.
	        //PrintTextAbove(corpse, "*guardkilling*");
	        foreach item in ( EnumerateItemsInContainer( corpse ))
		        if( item.container == corpse )
			        DestroyItem( item );
		        endif
		        SleepMS(2);
	        endforeach
 
	        DestroyItem( corpse );
         
                return 1;
        endif

	return 0;
endfunction

function NoCorpseCheck( corpse, template )

	if( template.NoCorpse || GetObjProperty( corpse, "NoCorpse" ) || GetObjProperty( corpse, "SummonedBy" ))
	        // No corpse. Place backpack contents where the corpse is.
	        foreach item in ( EnumerateItemsInContainer( corpse ))
		        if( item.container == corpse )
			        MoveObjectToLocation( item, corpse.x, corpse.y, corpse.z, corpse.realm, MOVEOBJECT_FORCELOCATION );
		        endif
		        SleepMS(2);
	        endforeach

	        DestroyItem( corpse );
        endif

	return 1;
endfunction

function AutoOpen( corpse )

        var mobiles := ListMobilesNearLocationEx( corpse.x, corpse.y, corpse.z, 2, LISTEX_FLAG_NORMAL, corpse.realm );
        foreach mobile in mobiles
                UseItem( corpse, mobile );
                SleepMS(5);
        endforeach

        return 1;
endfunction

function DeleteNPCEquip( corpse )

        foreach item in ( EnumerateItemsInContainer( corpse ))
                if( GetObjProperty( item, "NoLoot" ))
		        DestroyItem( item );
                endif
                SleepMS(5);
        endforeach
         
        return 1;
endfunction
