/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";

program SpellScript_Reveal( params )

	var mobile := params[1];
	var info := params[2];
	//var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	var area_size := CInt( AP_GetSkill( mobile, MAGERY ) / 25 ) + 1;
	var targs := ListMobilesNearLocationEx( mobile.x, mobile.y, mobile.z, area_size, LISTEX_FLAG_HIDDEN, mobile.realm );
        var caster_skill := AP_GetSkill( mobile, MAGERY ) + AP_GetSkill( mobile, DETECTING_HIDDEN );
        caster_skill *= ClasseBonus( mobile, MAGE );
        
        var found := 0;
	foreach targ in targs
		if( targ.hidden )
                        if( SPELL_Reflected( mobile, targ, circle ) == "Reflected" )
				continue;
                        endif
			// I just made these up. uo.stratics and uo.com didn't give any good info.
			var hider_skill := AP_GetSkill( targ, HIDING ) + AP_GetSkill( targ, RESISTING_SPELLS );
                        hider_skill *= ClasseBonus( targ, THIEF );
                        
			if( caster_skill >= hider_skill || mobile.multi )
                                SPELL_PlaySpellSFX( info.spell_id, targ );
				targ.hidden := 0;
    
                                PrintTextAbovePrivate( targ, "You have been revealed!", targ );
				PrintTextAbovePrivate( targ, "Ah ha!!", mobile );
                                found := 1;
                        else
				SendSysMessage( targ, mobile.name+" is trying to discover you!" );
			endif
		endif
                SleepMS(5);
	endforeach
 
        if( !found )
                SendSysMessage( mobile, "You didn't find anyone." );
        endif
	
	return 1;
endprogram
