/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include "include/itemUtil";
include ":areas:managment";
include ":spells:spells";

program SpellScript_Mark( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        //var circle := SPELL_GetCircle( info.spell_id );
	params := 0;
 
	if( !targ.IsRecallRune() )
		SendSysMessage( mobile, "You can only cast that on a recall rune." );
		return 0;
	elseif( !useItem_check( mobile, targ, ITEM_INCONTAINER ))
		return 0;
        elseif( GetObjProperty( targ, "Destination" ))
		SendSysMessage( mobile, "This rune alrady have a destination." );
		return 0;
	elseif( ListMultisInBox( targ.x-3, targ.y-3, targ.z-10, targ.x+3, targ.y+3, targ.z+10, targ.realm ).Size() > 0 && !mobile.cmdlevel )
		SendSysMessage( mobile, "This ability cannot be used to recall too near or in a house!" );
		return 0;
	elseif( A_IsIn( mobile, AREAS_NO_MARK ))
		SendSysMessage( mobile, "You cannot mark here." );
		return 0;
	endif

	SPELL_PlaySpellSFX( info.spell_id, targ );
	SPELL_PlaySpellGFX( info.spell_id, targ, mobile );

	targ.SetDestination( mobile.x, mobile.y, mobile.z, mobile.realm );

	SetName( targ, "a marked recall rune" );

	return 1;
endprogram
