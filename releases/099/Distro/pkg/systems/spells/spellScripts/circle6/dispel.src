/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":areas:managment";
include ":spells:spells";
include ":timedscripts:timedScripts";

program SpellScript_Dispel( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;
 
        if( !SPELL_MobileCheck( mobile, targ ))
		return 0;
        elseif( mobile.serial != targ.serial && !mobile.IsA( POLCLASS_NPC ))
                if( A_IsIn( targ, AREAS_NO_DAMAGE ))
                        SendSysMessage( mobile, "Cancelled." );
                	return 0;
                endif
	endif
 
        SPELL_PlaySpellSFX( info.spell_id, targ );
	SPELL_PlaySpellGFX( info.spell_id, targ, mobile );
 
	if( GetObjProperty( targ, "SummonedBy" ) && targ.IsA( POLCLASS_NPC ))
                SetObjProperty( targ, "GuardKill", 1 );
		targ.Kill();
                return 1;
        elseif( SPELL_Reflected( mobile, targ, circle ) == "Reflected" )
                SPELL_PlaySpellSFX( info.spell_id, mobile );
		SPELL_PlaySpellGFX( info.spell_id, mobile, targ );

                targ := mobile;
        endif

        var protection := SPELL_GetImmunity( mobile, targ, circle );
	if( protection == "Immuned" )
		return 0;
	endif

	TS_WipeMods( targ );

	return 1;
endprogram
