/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";
include ":keys:key";
include ":lockpicking:lockpicking";

program SpellScript_Unlock( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        //var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

        if( !targ )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        elseif( !useItem_check_lockpicking( mobile, targ ))
                return 0;
	elseif( !KP_IsLockable( targ ))
		SendSysMessage( mobile, "That is not unlockable." );
		return 0;
        elseif( !targ.locked )
                SendSysMessage( mobile, "That doesn't appear to be locked." );
                return 0;
        elseif( targ.IsA( POLCLASS_DOOR ) && targ.multi )
                SendSysMessage( mobile, "The Gods frown on picking this door!" );
                return 0;
        elseif( GetObjProperty( targ, "NoLockpick" ))
                SendSysMessage( mobile, "It's seem to be magically protected." );
                return 0;
        endif

        var lock_level := GetLockPickLevel( targ );
        if( !lock_level )
                SendSysMessage( mobile, "You have no chances of picking that lock!" );
                return 0;
        endif

	if( CInt( GetObjProperty( mobile, MAGE )) >= lock_level )
                PrintTextAbove( targ, "*Unlocked*" );
		SPELL_PlaySpellSFX( info.spell_id, targ );
  
                targ.locked := 0;
        else
                SendSysMessage( mobile, "You fail to unlock the lock." );
	endif
	
	return 1;
endprogram

function useItem_check_lockpicking( mobile, item )

        //This function replace the original in include/itemUil because of the "lock" feature.

	if( !ReserveItem( item ))
                SendSysMessage( mobile, "You cannot use that item." );
		return 0;
	elseif( !Accessible( mobile, item ))
                SendSysMessage( mobile, "You do not have access to that item." );
		return 0;
        endif

        return 1;
endfunction
