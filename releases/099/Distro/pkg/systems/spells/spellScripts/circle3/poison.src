/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";
include ":timedScripts:timedScripts";

program SpellScript_Poison( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	if( !SPELL_MobileCheck( mobile, targ ))
		return 0;
	endif
 
        SPELL_PlaySpellSFX( info.spell_id, targ );
	SPELL_PlaySpellGFX( info.spell_id, targ, mobile );
 
        if( SPELL_Reflected( mobile, targ, circle ) == "Reflected" )
                SPELL_PlaySpellSFX( info.spell_id, mobile );
	        SPELL_PlaySpellGFX( info.spell_id, mobile, targ );

                targ := mobile;
        endif
 
        var poisonLevel := CInt(( AP_GetSkill( mobile, MAGERY ) + AP_GetSkill( mobile, POISONING )) / 100 ),
	    duration := CInt( 3 + AP_GetSkill( mobile, MAGERY ) * 0.4 );
     
        poisonLevel := SPELL_GetResist( mobile, targ, circle, poisonLevel );
        if( poisonLevel < 1 )
                poisonLevel := 1;
        endif
        
        var protection := SPELL_GetImmunity( mobile, targ, circle );
	if( protection == "Immuned" )
		return 0;
        elseif( protection == "Cursed" )
                duration *= 1.50;
	endif

	TS_StartTimer( targ, "DefaultPoison", CInt( duration ), CInt( poisonLevel ), mobile );

	return 1;
endprogram
