/*
        Maintened by *Edwards

        2008-12-10

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
        
        Original Idea from ZuluHotel
*/
use uo;
use os;

include ":spells:spells";

program SpellScript_Sacrifice( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;
 
	if( !SPELL_MobileCheck( mobile, targ ))
		return 0;
        elseif( GetObjProperty( targ, "Master" ) != mobile.serial )
		SendSysMessage( mobile, "You can only sacrifice one of yours pets." );
		return 0;
	endif

	var area_size := CInt( AP_GetSkill( mobile, MAGERY ) / 25 ) + 1;
	var excludes := array{ mobile.serial };

	var targs := SPELL_GetValidTargets( mobile, area_size, 1, excludes );
 
        var amount := CInt( AP_GetStat( targ, STRENGTH ) / targs.len );
        if( GetObjProperty( mobile, MAGE ))
                amount *= ClasseBonus( mobile, MAGE );
        endif
        
        SetObjProperty( targ, "GuardKill", 1 );
        targ.Kill();
        
        if( amount < 1 )
		amount := 1;
	endif
        
	foreach vtarg in targs
		SleepMS(5);

                if( vtarg.dead || vtarg.hidden || vtarg.concealed )
			continue;
                elseif( !CheckLineOfSight( mobile, vtarg ))
			continue;
                elseif( vtarg.IsA( POLCLASS_NPC ) && vtarg.master == mobile )
			continue;
                endif
  
                var protection := SPELL_GetImmunity( mobile, vtarg, circle );
	        if( protection == "Immuned" )
		       continue;
                elseif( protection == "Cursed" )
                       amount *= 1.50;
	        endif

                SPELL_PlaySpellSFX( info.spell_id, vtarg );
                SPELL_PlaySpellGFX( info.spell_id, vtarg, mobile );
                
                SPELL_ApplyDamage( vtarg, CInt( amount / 2 ), "Sacrifice", mobile, DMG_NECRO );
	endforeach

	return 1;
endprogram
