/*
        Maintened by *Edwards

        2008-12-10

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
        
        Original Idea from ZuluHotel
*/
use uo;
use os;

include ":spells:spells";
include ":timedScripts:timedScripts";

program SpellScript_Plague( params )

	var mobile := params[1];
	var info := params[2];
	//var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	// Get all targets in area excluding party and self. Damaged is divided by number of valid targets.
	var area_size := CInt( AP_GetSkill( mobile, MAGERY ) / 25 ) + 1,
	    targs := SPELL_GetValidTargets( mobile, area_size, 1, {mobile}, mobile.party ),
            attri := CInt( AP_GetSkill( mobile, MAGERY ) / 50 ) + 2;

	foreach vtarg in targs
		SleepMS(5);

                if( vtarg.dead || vtarg.hidden || vtarg.concealed )
			continue;
                elseif( !CheckLineOfSight( mobile, vtarg ))
			continue;
                elseif( vtarg.IsA( POLCLASS_NPC ) && vtarg.master == mobile )
			continue;
                elseif( SPELL_Reflected( mobile, vtarg, circle ) == "Reflected" )
			continue;
                endif
  
 	        var duration := CInt( 3 + AP_GetSkill( mobile, MAGERY ) * 0.4 );

                var protection := SPELL_GetImmunity( mobile, vtarg, circle );
	        if( protection == "Immuned" )
		       continue;
                elseif( protection == "Cursed" )
                       duration *= 1.50;
	        endif
         
                var poisonLevel := CInt( SPELL_GetResist( mobile, vtarg, circle, CInt( SPELL_GetEfficiency( mobile, attri ))));
                if( poisonLevel < 2 )
                        poisonLevel := 2;
                endif

                SPELL_PlaySpellSFX( info.spell_id, mobile );
                SPELL_PlaySpellGFX( info.spell_id, vtarg, mobile );
                
                TS_StartTimer( vtarg, "DefaultPoison", CInt( duration ), CInt( poisonLevel ), mobile);
	endforeach

	return 1;
endprogram
