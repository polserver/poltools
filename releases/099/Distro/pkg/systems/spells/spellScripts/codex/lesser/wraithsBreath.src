/*
        Maintened by *Edwards

        2008-12-10

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
        
        Original Idea from ZuluHotel
*/
use uo;
use os;

include ":spells:spells";
include ":timedscripts:timedScripts";

program SpellScript_WraithBreath( params )

	var mobile := params[1];
	var info := params[2];
	//var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	// Get all targets in area excluding party and self.
	var area_size := CInt( AP_GetSkill( mobile, MAGERY ) / 25 ) + 1;
	var excludes := array{mobile.serial};
        var magery := AP_GetSkill( mobile, MAGERY );

	var targs := SPELL_GetValidTargets( mobile, area_size, 1, excludes );
	foreach vtarg in targs
                SleepMS(5);

                if( vtarg.dead || vtarg.hidden || vtarg.concealed )
			continue;
                elseif( !CheckLineOfSight( mobile, vtarg ))
			continue;
                elseif( vtarg.IsA( POLCLASS_NPC ) && vtarg.master == mobile )
			continue;
                elseif( SPELL_Reflected( mobile, vtarg, circle ) == "Reflected" )
			continue;
                endif

                var duration := SPELL_GetEfficiency( mobile, CInt( magery / 10 ) + 2 );

                var protection := SPELL_GetImmunity( mobile, vtarg, circle );
	        if( protection == "Immuned" )
		       continue;
                elseif( protection == "Cursed" )
                       duration *= 1.50;
	        endif

                SPELL_PlaySpellSFX( info.spell_id, vtarg );
		SPELL_PlaySpellGFX( info.spell_id, vtarg, mobile );
  
                PrintTextAbovePrivate( vtarg, "You get paralyzed from the power of "+mobile.name+"'s spell!", vtarg );
                
		TS_StartTimer( vtarg, "Paralysis", duration );
	endforeach

	return 1;
endprogram
