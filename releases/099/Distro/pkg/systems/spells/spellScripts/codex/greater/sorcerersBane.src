/*
        Maintened by *Edwards

        2008-12-10

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
        
        Original Idea from ZuluHotel
*/
use uo;
use os;

include ":spells:spells";

program SpellScript_SorcerersBane( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

        SPELL_PlaySpellSFX( info.spell_id, mobile );
        
        if( SPELL_Reflected( mobile, targ, circle ) == "Reflected" )
                SPELL_PlaySpellSFX( info.spell_id, mobile );
	        SPELL_PlaySpellGFX( info.spell_id, mobile, targ );

                targ := mobile;
        endif

        var amount := SPELL_GetResist( mobile, targ, circle, CInt( SPELL_MathDamage( mobile, circle ) / 2 ));

        var protection := SPELL_GetImmunity( mobile, targ, circle );
	if( protection == "Immuned" )
	        return 0;
        elseif( protection == "Cursed" )
                amount *= 1.50;
	endif
                
        var x := targ.x,
            y := targ.y,
            z := targ.z,
            realm := targ.realm;

        var spell_color := 33,
            spell_name := "blood";
            
        if( GetObjProperty( mobile, "Type" ) == "Dragonkin" )
                spell_color := 0;
                spell_name := "water";
        endif

        var script := start_script( ":summoning:waterFall", {targ, 5, spell_color, 0x0209, spell_name} );
        if( script.errortext )
                SendSysMessage( mobile, "Error || Cannot start script <:summoning:waterFall> -->"+script.errortext );
        endif
                
        amount *= 0.80;
        
        SPELL_ApplyDamage( targ, CInt( amount * 1.2 ), "SorcerersBane", mobile, DMG_NECRO+DMG_WATER );

	Sleep(2);
 
        if( targ.x == x && targ.y == y && targ.z == z && targ.realm == realm )
                SPELL_PlaySpellSFX( info.spell_id, mobile );
                SPELL_ApplyDamage( targ, CInt( amount * 0.8 ), "SorcerersBane", mobile, circle );
        endif

	return 1;
endprogram
