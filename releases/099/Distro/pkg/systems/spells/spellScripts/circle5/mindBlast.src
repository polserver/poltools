/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";

program SpellScript_MindBlast( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	if( !SPELL_MobileCheck( mobile, targ ))
		return 0;
        elseif( mobile.serial == targ.serial )
                SendSysMessage( mobile, "You shall consider therapy!" );
                return 0;
	endif

        SPELL_PlaySpellSFX( info.spell_id, targ );
	SPELL_PlaySpellGFX( info.spell_id, targ, mobile );
	
	var mobile_int := SPELL_GetEfficiency( mobile, AP_GetStat( mobile, INTELLIGENCE )),
	    targ_int := SPELL_GetEfficiency( targ, AP_GetStat( targ, INTELLIGENCE ));
     
        if( mobile_int == targ_int )
                SendSysMessage( mobile, "You are of equal intellect!" );
		return 1;
        elseif( targ_int > mobile_int )
                SendSysMessage( mobile, "Their superior intellect reflects the spell!" );

                targ := mobile;
        endif

        var amount := SPELL_GetResist( mobile, targ, circle, SPELL_MathDamage( mobile, circle ));
            
        var protection := SPELL_GetImmunity( mobile, targ, circle );
	if( protection == "Immuned" )
		return 0;
        elseif( protection == "Cursed" )
                amount *= 1.50;
	endif

        SPELL_ApplyDamage( targ, amount, "MindBlast", mobile );
        
	return 1;
endprogram
