/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";
include ":spells:fields";

program SpellScript_PoisonField( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        //var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	SPELL_PlaySpellSFX( info.spell_id, targ );
	SPELL_PlaySpellGFX( info.spell_id, targ, mobile );

	var objtype;
	var direction := GetDirectionByFacing(mobile);
	case( direction )
		FIELD_SHAPE_HORIZONTAL: objtype := "PoisonField_EW"; break;
		FIELD_SHAPE_VERTICAL: objtype := "PoisonField_NS"; break;
	endcase
 
        var max_poisonLevel := SPELL_GetEfficiency( mobile, 4 ),
            modifier := AP_GetSkill( mobile, MAGERY ) + AP_GetSkill( mobile, POISONING ) / 2,
            poison_level := SPELL_GetEfficiency( mobile, modifier + 1 );

        if( poison_level > max_poisonLevel )
                poison_level := max_poisonLevel;
        endif
        
	var area_size := CInt( AP_GetSkill( mobile, MAGERY ) / 25 ) + 1,
	    duration := 30 * ClasseBonus( mobile, MAGE );

	var coordinates := GetCoordinatesForField( targ.x, targ.y, direction, CInt( area_size / 2 ));
	foreach coord in ( coordinates )
		var z := targ.z;
		if( z < GetStandingHeight(coord.x, coord.y, targ.z, mobile.realm ))
			z := GetStandingHeight( coord.x, coord.y, targ.z, mobile.realm );
		endif
  
		var mobiles := ListMobilesNearLocationEx( coord.x, coord.y, z, 1, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN, mobile.realm );
		if( mobiles.Size() > 0 )
			continue; // Dont create walls on top of mobiles.
		elseif( !CheckLosAt( mobile, coord.x, coord.y, z ))
			continue; // Only create a field piece if the caster has line of sight.
		endif

		var item := CreateItemAtLocation( coord.x, coord.y, z, objtype, 1, mobile.realm );
		if( item.errortext )
			SendSysMessage( mobile, "Error - "+item.errortext );
			continue;
		endif

		SetObjProperty( item, "SummonedField", mobile.serial );
		SetObjProperty( item, "Poisonlevel", poison_level );
  
		item.SetOwner( mobile );
		item.SetDuration( duration );

		SleepMS(2);
	endforeach

	return 1;
endprogram
