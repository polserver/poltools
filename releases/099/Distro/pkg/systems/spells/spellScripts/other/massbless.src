/*
        Created by *Edwards

        2009-08-16

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";
include ":timedscripts:timedScripts";

program SpellScript_Bless( params )

	var mobile := params[1];
	var info := params[2];
	params := 0;
        
        var magery := AP_GetSkill( mobile, MAGERY ),
	    area_size := CInt( magery / 15 ) + 1,
	    targs := SPELL_GetValidTargets( mobile, area_size ),
            amount := SPELL_GetAmount( mobile ),
	    duration := SPELL_GetDuration( mobile );

	foreach vtarg in targs
		SleepMS(5);

                if( vtarg.dead || vtarg.hidden || vtarg.concealed )
			continue;
                elseif( vtarg.IsA( POLCLASS_NPC ))
			continue;
                elseif( !CheckLineOfSight( mobile, vtarg ))
			continue;
                endif

		if( !GetObjProperty( vtarg, "BlessMod" ))
			SPELL_PlaySpellSFX( info.spell_id, mobile );
			SPELL_PlaySpellGFX( info.spell_id, vtarg, mobile );
   
			TS_StartTimer( vtarg, "Bless", duration, amount, mobile );
		endif
	endforeach
	
	return 1;
endprogram
