/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";
include ":timedscripts:timedScripts";

program SpellScript_ManaDrain( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	if( !SPELL_MobileCheck( mobile, targ ))
		return 0;
	endif

	if( mobile.serial == targ.serial )
		SendSysMessage( mobile, "You should consider therapy!" );
		return 0;
	endif
	
	SPELL_PlaySpellSFX( info.spell_id, targ );
	SPELL_PlaySpellGFX( info.spell_id, targ, mobile );
 
        if( SPELL_Reflected( mobile, targ, circle ) == "Reflected" )
                SPELL_PlaySpellSFX( info.spell_id, mobile );
		SPELL_PlaySpellGFX( info.spell_id, mobile, targ );

                targ := mobile;
        endif

        var magery := SPELL_GetEfficiency( mobile, AP_GetSkill( mobile, MAGERY )),
            resistance := AP_GetSkill( mobile, RESISTING_SPELLS ) * ClasseBonus( mobile, MAGE );
 
        if( resistance >= magery + 5 )
		PrintTextAbovePrivate( mobile, targ.name + "'s will is too strong!", mobile );
	else
                var amount := SPELL_GetResist( mobile, targ, circle, SPELL_MathDamage( mobile, circle )),
                    vital := AP_GetVital( targ, MANA );
                    
                var protection := SPELL_GetImmunity( mobile, targ, circle );
	        if( protection == "Immuned" )
		        return 0;
                elseif( protection == "Cursed" )
                        amount *= 1.50;
	        endif
                    
		if( amount > vital )
			amount := CInt( vital );
		endif

		AP_SetVital( targ, MANA, vital - amount );
		PrintTextAbovePrivate( targ, "You expelled "+amount+" mana out of "+targ.name+ "'s aura!", mobile );
	endif
	
	return 1;
endprogram
