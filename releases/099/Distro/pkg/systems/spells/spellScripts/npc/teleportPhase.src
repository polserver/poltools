/*
        Created by *Edwards

        2008-11-25

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";
include ":brainAI:npcNerves";

program SpellScript_TeleportPhase( params )

	var mobile := params[1];
	//var info := params[2];
	var targ := params[3];
	params := 0;
 
	var phase_coords := GetCoordsInLine( mobile.x, mobile.y, targ.x, targ.y );
 
	//Get rid of the last coordinate - so the npc doesnt tele on top of the player.
	phase_coords.Erase( phase_coords.Size() );

	PlaySoundEffect( mobile, SFX_TELEPORT );

	foreach coord in ( phase_coords )
		PlayStationaryEffect( mobile.x, mobile.y, mobile.z, GFX_SMOKE, 5, 5, 0 );
		var z_pos := GetStandingHeight( coord.x, coord.y, mobile.z, mobile.realm ).z;
		if( !z_pos )
			z_pos := GetWorldHeight( coord.x, coord.y, mobile.realm );
		endif

		MoveObjectToLocation( mobile, coord.x, coord.y, z_pos, mobile.realm, MOVEOBJECT_NORMAL );
		SleepMS(20);
	endforeach

	return 1;
endprogram
