/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2009-10-10

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":attributes:attributes";
include ":brainAI:npcCommands";
include ":blood:blood";
include ":damage:damage";
include "include/facings";
include "include/client";

program SpellScript_SplitAcid( params )

	var npc := params[1];
	//var targ := params[3];
	params := 0;

	var hp := AP_GetVital( npc, HITS );
	var max_hp := AP_GetVitalMaximumValue( npc, HITS );

	if( hp < max_hp )
		var attack_list := array{};
		foreach mobile in ( ListMobilesNearLocationEX( npc.x, npc.y, npc.z, 10, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN ))
			if(( mobile.ip ) && ( !mobile.concealed ))
				attack_list.Append( mobile );
			endif
			SleepMS(5);
		endforeach

		var num_opponents := attack_list.Size();

		if( num_opponents > 0 )
			var dmg := max_hp-hp;
   
			//Divvy up the damage
			dmg /= num_opponents;
			dmg := CInt( Ceil( dmg ));
   
			if( dmg < 1 )
				dmg := 1;
			endif

			foreach opponent in ( attack_list )

				TurnObjectToward( npc, opponent.x, opponent.y );
				PerformAction( npc, ANIM_FIDGET_2 );
				PlaySoundEffect( npc, 0x17C );
    
				var temp := RandomInt( dmg )+1;
				ApplyDamageEX( opponent, temp );
				MakeBleed( opponent );
				HealDamage( npc, temp );
    
				SleepMS(10);
			endforeach
		endif
	endif

	return 1;
endprogram
