/*
        Created by *Edwards

        2009-02-07

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":attributes:attributes";
include ":brainAI:npcCommands";

program SpellScript_DoppelGanger( params )

	var mobile := params[1];
	//var info := params[2];
	var targ := params[3];
	params := 0;
        
        if( GetObjProperty( mobile, "Copied" ) && targ.serial == GetObjProperty( mobile, "Copied" ))
                return 0;
        endif

	mobile.graphic := targ.graphic;
	mobile.color := targ.color;
	mobile.truecolor := targ.color;

        AI_Speak( mobile, "*The creature shape is changing!*" );

        SetName( mobile, targ.name );

	foreach item in ListEquippedItems( mobile )
		DestroyItem( item );
		SleepMS(2);
	endforeach

        var attrib;
	foreach attrib_name in AP_ListAttributesByType()
                attrib := Cint( AP_GetTrueSkill( targ, attrib_name ));
		AP_SetTrueSkill( mobile, attrib_name, attrib );
		SleepMS(10);
	endforeach

        AP_RefreshVitals( mobile );

	foreach item in ListEquippedItems( targ )
		var clone_item := CreateItemCopyAtLocation( mobile.x, mobile.y, mobile.z, item, mobile.realm );
                clone_item.newbie := 1;
                SetObjProperty( clone_item, "LFucker", "DoppelGanger" );
                SetObjProperty( clone_item, "NoLoot", 1 );
		EquipItem( mobile, clone_item );
		SleepMS(500);
	endforeach

	SetObjProperty( mobile, "Copied", targ.serial );

	return 1;
endprogram
