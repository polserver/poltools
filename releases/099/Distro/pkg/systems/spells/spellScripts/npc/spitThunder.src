/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2010-07-07
*/
use uo;
use os;

include "include/arrays";
include "include/client";
include "include/shapes";
include ":brainAI:npcCommands";
include ":brainAI:npcUtil";
include ":spells:spells";

program SpellScript_SpitThunder( params )

	var mobile := params[1];
	//var info := params[2];
	var targ := params[3];
        //var circle := MS_GetCircle( info.spell_id );
	params := 0;
        
        var coords := array{};

        var npc_cfg := NPC_GetNPCConfig( mobile.npctemplate );
	foreach coord in GetCoordsInSquare( targ.x, targ.y, CInt( npc_cfg.SpellRadius ))
		SleepMS(5);
		if( RandomInt(30) != 1 ) // Don't place a thunder
			continue;
		endif

                var z_pos := CInt( GetWorldHeight( coord.x, coord.y, mobile.realm ));
                coord.+z := z_pos;

                coords.Append( coord );
	endforeach

        var coords_size := coords.Size(),
            i;

        for( i:=0; i<coords_size; i+=1 )
                var coord := coords.RandomEntry();

                RemoveFromArray( coords, coord );

                PlaySoundEffect( mobile, GetRandomThunderSound() );
                PlayStationaryEffect( coord.x, coord.y, coord.z, 0x3be4, 4, 14, 0, mobile.realm );
                
                DmgNearbyMobiles( mobile, coord );
		SleepMS(105);
        endfor
       
	return 1;
endprogram

function GetRandomThunderSound()

        var sound := 0x0029;
        if( RandomInt(2) == 1 )
                sound := 0x0207;
        endif
        
        return sound;
endfunction

function DmgNearbyMobiles( mobile, coord )

	var nearby := ListMobilesNearLocationEX( coord.x, coord.y, coord.z, 0, LISTEX_FLAG_NORMAL, mobile.realm );
	foreach player in ( nearby )
		SleepMS(5);
  
                if( player.IsA( POLCLASS_NPC ))
                	continue;
                endif

		var amount := SPELL_MathDamage( mobile, 10 );
        	SPELL_ApplyDamage( player, amount, DMG_AIR, mobile, DMG_AIR );
	endforeach
        
	return 1;
endfunction
