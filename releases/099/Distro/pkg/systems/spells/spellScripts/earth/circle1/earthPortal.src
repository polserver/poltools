/*
        Maintened by *Edwards

        2008-11-21

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include "include/itemUtil";
include ":areas:managment";
include ":spells:spells";

program SpellScript_EarthPortal( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        //var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	if( !targ.IsRecallRune() )
		SendSysMessage( mobile, "You can only cast that on a recall rune." );
		return 0;
	elseif( !useItem_check( mobile, targ, ITEM_INCONTAINER ))
		return 0;
        elseif( !GetObjProperty( targ, "Destination" ))
		SendSysMessage( mobile, "This rune does not have a destination." );
		return 0;
	endif

	var dest := targ.GetDestination();
	if( !dest )
		SendSysMessage( mobile, "That rune is not marked." );
		return 0;
        elseif( A_IsIn( mobile, AREAS_NO_RECALL ))
		SendSysMessage( mobile, "You cannot gate from here." );
		return 0;
        elseif( A_IsIn( dest, AREAS_NO_MARK ))
		SendSysMessage( mobile, "You cannot gate to this place." );
		return 0;
	elseif( ListMultisInBox( dest.x-3, dest.y-3, dest.z-10, dest.x+3, dest.y+3, dest.z+10, dest.realm ).Size() > 0 && !mobile.cmdlevel )
		SendSysMessage( mobile, "This ability cannot be used to recall too near or in a house!" );
		return 0;
	endif

	SPELL_PlaySpellSFX( info.spell_id, mobile );
	SPELL_PlaySpellGFX( info.spell_id, mobile, mobile );
 
        var gate_type := 0xF6C;
        var duration := RandomInt(20) + 20;

        if( AP_GetSkill( mobile, MAGERY ) > 120 )
                gate_type := 0xDDA;
                duration *= 2;
        endif

        var gate_entrance := CreateItemAtLocation( mobile.x, mobile.y, mobile.z, gate_type, 1, mobile.realm );
            gate_entrance.SetDestination( dest.x, dest.y, dest.z, dest.realm );

        var gate_dest := CreateItemAtLocation( dest.x, dest.y, dest.z, gate_type, 1, dest.realm );
            gate_dest.SetDestination( mobile.x, mobile.y, mobile.z, mobile.realm );

        ReleaseItem( mobile );

        Sleep( duration );
        
        DestroyItem( gate_entrance );
        DestroyItem( gate_dest );
        
	return 1;
endprogram
