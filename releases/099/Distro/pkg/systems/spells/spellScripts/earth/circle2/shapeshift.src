/*
        Maintened by *Edwards

        2008-12-07

        Last Modifications 2010-04-27
             * Cleaned codes for better performances

        Original Idea from ZuluHotel
*/
use uo;
use os;

include ":mounts:mounts";
include ":spells:spells";
include ":timedscripts:timedScripts";

program SpellScript_ShapeShift( params )

	var mobile := params[1];
	var info := params[2];
	params := 0;

        if( GetObjProperty( mobile, "PolymorphMod" ))
                SendSysMessage( mobile, "Already under the influence!" );
		return 0;
        elseif( GetObjProperty( mobile, "LicheMod" ))
                SendSysMessage( mobile, "Already under the influence!" );
		return 0;
        elseif( GetObjProperty( mobile, "WraithMod" ))
                SendSysMessage( mobile, "Already under the influence!" );
		return 0;
        elseif( MP_GetMount( mobile ))
                SendSysMessage( mobile, "You cannot use that spell when you are mounted!" );
		return 0;
	endif
 
        var cfg := ReadConfigFile( ":spells:shapeshift" ),
            menu := CreateMenu( "What form would you like to assume?" ),
            difficulty := SPELL_GetEfficiency( mobile, AP_GetSkill( mobile, MAGERY ));
            
        var x := mobile.x,
            y := mobile.y,
            z := mobile.z,
            realm := mobile.realm;

        var texit := 0,
            i := 1;
            
        while( !texit )
        	var elem := cfg[i];
        	if( !elem )
                	texit := 1;
        	elseif( difficulty >= elem.Difficulty )
                        AddMenuItem( menu, elem.Menu, elem.Name );
                else
                        texit := 1;
                endif
                i += 1;
	endwhile
 
        var choice := SelectMenuItem2( mobile, menu );
 	if( !choice )
		SendSysMessage( mobile, "Cancelled." );
        	return 0;
	endif
 
        var shapeshift := cfg[choice.index];
 	if( !shapeshift )
		SendSysMessage( mobile, "Error with shapeshift variable -->"+shapeshift.errortext );
        	return 0;
	endif
 
        if( mobile.x != x || mobile.y != y || mobile.z != z || mobile.realm != realm )
		SendSysMessage( mobile, "You've lost your concentration." );
        	return 0;
        elseif( MP_GetMount( mobile ))
                SendSysMessage( mobile, "You've lost your concentration." );
		return 0;
	endif
 
	SPELL_PlaySpellSFX( info.spell_id, mobile );
	SPELL_PlaySpellGFX( info.spell_id, mobile, mobile );
 
        mobile.graphic := shapeshift.Graphic;
        mobile.color := 0;
 
	var amount := SPELL_GetAmount( mobile ),
	    duration := SPELL_GetDuration( mobile );

	TS_StartTimer( mobile, "Polymorph", duration, amount, mobile );
 
	return 1;
endprogram
