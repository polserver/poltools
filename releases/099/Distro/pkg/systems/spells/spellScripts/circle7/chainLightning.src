/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";

program SpellScript_ChainLightning( params )

	var mobile := params[1];
	var info := params[2];
	//var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	var area_size := CInt( AP_GetSkill( mobile, MAGERY ) / 15 ) + 1,
	    targs := SPELL_GetValidTargets( mobile, area_size, 1, {mobile}, mobile.party );
 
	foreach vtarg in targs
                SleepMS(5);

                if( vtarg.dead || vtarg.hidden || vtarg.concealed )
			continue;
                elseif( !CheckLineOfSight( mobile, vtarg ))
			continue;
                elseif( vtarg.IsA( POLCLASS_NPC ) && vtarg.master == mobile )
			continue;
                elseif( SPELL_Reflected( mobile, vtarg, circle ) == "Reflected" )
			continue;
                endif
		
                var amount := SPELL_GetResist( mobile, vtarg, circle, SPELL_MathDamage( mobile, circle ));
                    
                var protection := SPELL_GetImmunity( mobile, vtarg, circle );
	        if( protection == "Immuned" )
		         continue;
                elseif( protection == "Cursed" )
                         amount *= 1.50;
	        endif
         
                SPELL_PlaySpellSFX( info.spell_id, mobile );

		PlayLightningBoltEffect( vtarg );
                PlayLightningBoltEffect( vtarg );
                PlayLightningBoltEffect( vtarg );

                SPELL_ApplyDamage( vtarg, amount, "ChainLightning", mobile, DMG_AIR );
	endforeach

	return 1;
endprogram
