/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";

program SpellScript_ManaVampire( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	if( !SPELL_MobileCheck( mobile, targ ))
		return 0;
	elseif( mobile.serial == targ.serial )
		SendSysMessage( mobile, "You should consider therapy!" );
		return 0;
	endif

        SPELL_PlaySpellSFX( info.spell_id, targ );
	SPELL_PlaySpellGFX( info.spell_id, targ, mobile );

        if( SPELL_Reflected( mobile, targ, circle ) == "Reflected" )
                SPELL_PlaySpellSFX( info.spell_id, mobile );
	        SPELL_PlaySpellGFX( info.spell_id, targ, mobile );

                targ := mobile;
        endif
 
        var mobile_magery := SPELL_GetEfficiency( mobile, AP_GetSkill( mobile, MAGERY )),
            targ_resist := SPELL_GetEfficiency( targ, AP_GetSkill( targ, RESISTING_SPELLS ));
            
        if( mobile_magery <= targ_resist )
                PrintTextAbovePrivate( mobile, targ.name+"'s will is too strong!", mobile );
        else
                var amount := SPELL_GetResist( mobile, targ, circle, SPELL_MathDamage( mobile, circle )),
                    targ_vital := AP_GetVital( targ, MANA ),
                    mobile_vital := AP_GetVital( mobile, MANA );
                    
                var protection := SPELL_GetImmunity( mobile, targ, circle );
	        if( protection == "Immuned" )
		        return 0;
                elseif( protection == "Cursed" )
                        amount *= 1.50;
	        endif

		if( amount > targ_vital )
			amount := CInt( targ_vital );
		endif

		AP_SetVital( targ, MANA, targ_vital - amount );
  
                if( mobile_vital + amount > AP_GetVitalMaximumValue( mobile, MANA ))
                        AP_SetVital( mobile, MANA, AP_GetVitalMaximumValue( mobile, MANA ));
                else
			AP_SetVital( mobile, MANA, mobile_vital + amount );
                endif
  
		PrintTextAbovePrivate( targ, "You drained "+amount+" mana out of "+targ.name+"'s aura!", mobile );
        endif
	
	return 1;
endprogram
