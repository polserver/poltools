/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":brainAI:npcUtil";
include ":mounts:mounts";
include ":spawnsystem:groups";
include ":spells:spells";
include ":timedscripts:timedScripts";

program SpellScript_Polymorph( params )

	var mobile := params[1];
	var info := params[2];
	//var targ := params[3];
        //var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	if( GetObjProperty( mobile, "PolymorphMod" ))
                SendSysMessage( mobile, "Already under the influence!" );
		return 0;
        elseif( GetObjProperty( mobile, "LicheMod" ))
                SendSysMessage( mobile, "Already under the influence!" );
		return 0;
        elseif( GetObjProperty( mobile, "WraithMod" ))
                SendSysMessage( mobile, "Already under the influence!" );
		return 0;
        elseif( MP_GetMount( mobile ))
                SendSysMessage( mobile, "You cannot use that spell when you are mounted!" );
		return 0;
	endif

	SPELL_PlaySpellSFX( info.spell_id, mobile );
	SPELL_PlaySpellGFX( info.spell_id, mobile, mobile );

        var magery := CInt( AP_GetSkill( mobile, MAGERY ) / 10 ) + 1,
            chances := RandomInt( magery ),
            group_name := "Elemental-Lord";
            
        if( chances == 12 )
                group_name := "Wisp-Good";
        elseif( chances > 7 )
                group_name := "Daemon";
        endif

	var group_entries := SS_GetEntriesInGroup( group_name ).Keys(),
	    template := group_entries.RandomEntry(),
            graphic := NPC_GetNPCConfig( template ).ObjType,
            amount := CInt( SPELL_GetAmount( mobile ) / 3 ),
	    duration := SPELL_GetDuration( mobile );

        mobile.color := 0;
        mobile.graphic := CInt( graphic );
        
	TS_StartTimer( mobile, "Polymorph", duration, amount, mobile );

	return 1;
endprogram
