/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2010-01-06
*/
use uo;

include ":townRaid:settings";
include ":townRaid:townRaid";

program control_townRaid_Gold( chest )

        var location := GetObjProperty( chest, "Location" ),
            level := GetObjProperty( chest, "Level" ),
            level_elem := TRaid_GetCfgElem( level, ":townRaid:levels" ),
            level_delay := CInt( level_elem.PenaltyDelay ),
            level_penalty := CInt( level_elem.PenaltyAmount ),
            children := array{};

        while( chest )

                var list_near := ListMobilesNearLocation( chest.x, chest.y, chest.z, 4, chest.realm  ),
                    penalty := 0;
                    
                children := GetObjProperty( chest, "Children" );

                foreach creature in list_near
                        if( !GetObjProperty( creature, "IsRaider" ))
                                continue;
                        endif

                        penalty += 1;
                        
                        creature.Kill();

                        SleepMS(5);
                endforeach

                var gold := CInt( GetObjProperty( chest, "GoldAmount" ));
                if( penalty )
                	gold := CInt( gold - ( level_penalty * penalty ));
                        if( gold <= 0 )
                                gold := 0;
                                SetObjProperty( chest, "GoldAmount", gold );
                                
                                if( GetObjProperty( chest, "EndsNoGold" ))
                                        EraseObjProperty( chest, "IsActive" );
                                        SendStatusMessage( location, FAIL_MESSAGE );
                                        break;
                                endif
                        endif
                        SetObjProperty( chest, "GoldAmount", gold );
                endif

                SetObjProperty( chest, "NameSpec", gold+" golds" );

		Sleep( level_delay );
	endwhile
 
        KillChildren( children );

        return 1;
endprogram
