/*
        Created by *Edwards

        2010-01-03
        
        Last Modifications 2010-01-06
             * Changed main loop with townRaid chest
*/
use uo;
use polsys;

include ":townRaid:settings";
include ":townRaid:townRaid";

program control_townRaid_Chest( chest )
            
        var active := GetObjProperty( chest, "IsActive" );
        while( !active )
                active := GetObjProperty( chest, "IsActive" );
                Sleep(1);
        endwhile
        
        var location := GetObjProperty( chest, "Location" );
	var raiders := GetObjProperty( chest, "Raiders" );
	var level := GetObjProperty( chest, "Level" );

        SendStatusMessage( location, START_MESSAGE );
        StartGoldCheck( chest );

        var level_elem := TRaid_GetCfgElem( level, ":townRaid:levels" ),
            max_NPCs := CInt( level_elem.MaxNPCs ),
            spawn_Delay := CInt( level_elem.SpawnDelay ),
            cycles := CInt( level_elem.Cycles ),
            cycle_Delay := CInt( level_elem.CycleDelay ),
            creatures := array{};

        Sleep( 120 );

        while( chest && active && cycles )

                var i;
                for( i:=creatures.Size(); i>0; i-=1 )
                        var creature := SystemFindObjectBySerial( creatures[i] );
                        if( !creature )
                                creatures.Erase(i);
                        endif
                        SleepMS(5);
                endfor
                
                var j;
                for( j:=0; j<=max_NPCs; j+=1 )
                        active := GetObjProperty( chest, "IsActive" );
                        if( !chest || !active )
                                break;
                        endif
                        
                        var creature := CreateSpawn( chest, location, raiders );
                        if( creature )
                                creatures.Append( creature );
                                SetObjProperty( chest, "Children", creatures );
                        endif
                        Sleep( spawn_Delay );
                endfor
                
                Sleep( cycle_Delay );
                active := GetObjProperty( chest, "IsActive" );
                
                cycles -= 1;
        endwhile
        
        while( creatures.Size() )
                var i;
                for( i:=creatures.Size(); i>0; i-=1 )
                        var creature := SystemFindObjectBySerial( creatures[i] );
                        if( !creature )
                                creatures.Erase(i);
                        endif
                        SleepMS(5);
                endfor
                Sleep(1);
        endwhile

        if( active )
                SendStatusMessage( location, END_MESSAGE );
                chest.locked := 0;
                
                if( !GetObjProperty( chest, "Gold" ))
                        //Assume .startRaid requested no gold
                        return 0;
                endif
                
                var gold_amount := GetObjProperty( chest, "GoldAmount" );
                if( gold_amount )
                        var created := CreateItemInContainer( chest, 0xeed, gold_amount );
                        if( created.errortext )
                                PrintTextAbove( chest, "Error creating "+gold_amount+" golds in container -->"+created.errortext );
                        endif
                endif
        endif
        
        return 1;
endprogram

function StartGoldCheck( chest )

        var script := start_script( ":townRaid:raidChest/goldLoop", chest );
        if( script.errortext )
                PrintTextAbove( chest, "Error starting script <:townRaid:raidChest/goldLoop> -->"+script.errortext );
                return 0;
        endif
        
        return 1;
endfunction
