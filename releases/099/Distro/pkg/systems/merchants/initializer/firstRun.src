use uo;
use os;
use polsys;
use cfgfile;

include ":security:report";
include ":merchants:storage";

program Initializemerchants()

	if( GetGlobalProperty( "MerchantInitialized" ))
		return 1;
	endif

	SetGlobalProperty( "MerchantInitialized", 1 );

	var cfg_file := ReadConfigFile( ":merchants:init" );
	foreach realm_name in ( Realms().Keys() )
		var cfg_elem := cfg_file[realm_name];
		if( cfg_elem )
			SetupMerchantsInRealm( realm_name, cfg_elem );
		endif

		SleepMS(2);
	endforeach
 
	UnloadConfigFile( ":merchants:unit" );

	return 1;
endprogram

function SetupMerchantsInRealm( realm_name, cfg_elem )

	foreach entry in ( GetConfigStringArray( cfg_elem, "Merchant" ))
		entry := SplitWords( entry );
  
  		var x := CInt( entry[1] ),
		    y := CInt( entry[2] ),
		    z := CInt( GetWorldHeight( x, y, realm_name )),
		    template := CStr( entry[3] ),
		    shared := entry[4];

		var result := CreateNpcFromTemplate( ":merchants:"+template, x, y, z, 0, realm_name );
		if( !result )
			ReportText( "Error: Could not create merchant "+template+" location x:"+x+" y:"+y+" -->"+result.errortext+"", REPORT_SYSLOG );
			continue;
		elseif( shared )
			var area_names := array{CPM_FORSALE, CPM_BUYING, CPM_SHOPPINGCART, CPM_HOLDING};
			foreach area_name in ( area_names )
				SetObjProperty( result, "Shared-"+area_name, shared );
				SleepMS(2);
			endforeach
		endif
		SleepMS(2);
	endforeach

	return 1;
endfunction
