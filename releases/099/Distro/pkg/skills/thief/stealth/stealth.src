/*
        Maintened by *Edwards
        For FantasiaShard.com
        
        Edwards@FantasiaShard.com
        
        2008-10-13
*/
use uo;
use os;
use math;

include "include/client";
include ":attributes:attributes";
include ":mounts:mounts";

program skill_Stealth( mobile )

	if( AP_GetSkill( mobile, HIDING ) < 90 )
		PrintTextAbovePrivate( mobile, "You are not good enough at hiding to stealth.", mobile );
		return 0;
	elseif( !mobile.hidden )
		PrintTextAbovePrivate( mobile, "You must be hidden to use stealth.", mobile );
		return 0;
	elseif( MP_GetMount( mobile ))
		PrintTextAbovePrivate( mobile, "You can't do that while mounted.", mobile );
		return 0;
	endif

	if( SkillCheck( mobile, STEALTH, -1 ) > 0 )
		mobile.stealthsteps := CalcNumSteps( mobile );
		PrintTextAbovePrivate( mobile, "You are now moving stealthily.", mobile );
	else
		if( mobile.hidden )
			mobile.hidden := 0;
			PrintTextAbovePrivate( mobile, "You have revealed yourself.", mobile );
		else
			PrintTextAbovePrivate( mobile, "You fail to move stealthily.", mobile );
		endif
	endif
 
	return 1;
endprogram

function CalcNumSteps( mobile )

        var thief := GetObjProperty( mobile, THIEF );
	var skill := AP_GetSkill( mobile, STEALTH );
 
	var num_steps := ( skill / 7 ) + 1;
 	if( thief )
		num_steps *= ClasseBonus( mobile, THIEF );
	endif

	return CInt( num_steps );
endfunction
