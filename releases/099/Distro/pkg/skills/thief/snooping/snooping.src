/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-10-19

        Last Modifications 2009-08-24
             * Added useItem()
             * Fixed HasThiefGloves() with methods
             * Added random chances to be detected while snooping by others nearby
             * Added ClasseBonus with range
             * Fixed few things for better performances

        Last Modifications 2010-02-26
             * Added support to opencontainer. Player can view but not steal.
             * Removed Auto-CallGuard when failing to snoop
             * Cleaned codes for better performances
             * Added 10 seconds delay
*/
use uo;
use os;

include "include/client";
include "include/facings";
include "include/itemUtil";
include ":areas:managment";
include ":attributes:attributes";
include ":classes:classes";

program skill_Snooping( mobile, gloves )

        if( CInt( GetObjProperty( mobile, "#Snooping" )) > ReadGameClock() )
                SendSysMessage( mobile, "You must wait to perform another action." );
                return 0;
        else
                SetObjProperty( mobile, "#Snooping", ReadGameClock() + 10 );
        endif
        
        EquipItem( mobile, gloves );
       	if( !useItem_check( mobile, gloves, ITEM_EQUIPPED ))
		return 0;
	endif

        SendSysMessage( mobile, "Select the player you would like to snoop." );
	var targ := Target( mobile );
	if( !targ.IsA( POLCLASS_MOBILE ) || targ.IsA( POLCLASS_NPC ))
		SendSysMessage( mobile, "That is not a player." );
		return 0;
        elseif( mobile.serial == targ.serial )
		SendSysMessage( mobile, "You should consider therapy." );
		return 0;
        elseif( targ.cmdlevel >= 1 )
		SendSysMessage( mobile, "You can't snoop a staff member." );
		return 0;
        elseif( A_IsIn( mobile, AREAS_NO_STEALING ))
		SendSysMessage( mobile, "You cannot snoop here." );
		return 0;
        elseif( !IsBehind( targ, mobile.x, mobile.y ))
                SendSysMessage( mobile, "It is not wise to snoop "+targ.name+" from here." );
		return 0;
        elseif( Distance( mobile, targ ) != 1 )
		SendSysMessage( mobile, "You must consider your position." );
		return 0;
	endif
 
        TurnObjectToward( mobile, targ.x, targ.y );
        
        PrintTextAbovePrivate( mobile, "*You carefully try to open the backpack*", mobile );
        Sleep(1);

        var difficulty := CInt(( AP_GetStat( targ, INTELLIGENCE ) / 2 ) + ( AP_GetStat( targ, DEXTERITY ) / 2 ));
	difficulty += RandomInt( 30 );

	if( SkillCheck( mobile, SNOOPING, difficulty ) > 0 )
                TurnObjectToward( mobile, targ.x, targ.y );
                SendViewContainer( mobile, targ.backpack );
        else
                SendSysMessage( mobile, "You were unable to snoop "+targ.name+"." );
        endif

        NotifyNoticers( mobile, targ );

        return 0;
endprogram

function NotifyNoticers( mobile, targ )

	var skill := AP_GetSkill( mobile, SNOOPING );
	// Ex: 100 -> 5, 60 -> 9, 23.7 -> 13
	var range := 20 - CInt( skill / 12 );

        if( GetObjProperty( mobile, THIEF ))
                range /= ClasseBonus( mobile, THIEF );
        endif

	foreach player in ( ListMobilesInLineOfSight( mobile, CInt( range )))
                if( player.IsA( POLCLASS_NPC ))
                	continue;
                endif

                if( RandomInt(4) == 2 )
                	if( player.serial == targ.serial )
                                SendSysMessage( player, "You notice "+mobile.name+" attempting to snoop your backpack!" );
                	else
				SendSysMessage( player, "You notice "+mobile.name+" attempting to snoop "+targ.name+"!" );
                	endif
                endif

		SleepMS(2);
	endforeach

	return 1;
endfunction
