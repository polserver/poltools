/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-10-14
        
        Last Modifications 2009-01-18
             * Added some lazy randomless with PrintForensicMessageAbove()

        Last Modifications 2009-08-20
             * No more CheckSkill for cmdlevel
             * Modified script for better performances.
*/
use uo;
use os;

include ":attributes:attributes";
include "include/facings";
include "include/itemUtil";

program skill_forensic( mobile )

	SendSysMessage( mobile, "What would you like to evaluate?" );
	var targ := Target( mobile );
	if( !targ )
                SendSysMessage( mobile, "Cancelled." );
		return 0;
	elseif( !targ.IsA( POLCLASS_CORPSE ))
		SendSysMessage( mobile, "You can't find anything useful on that." );
		return 0;
	elseif( !useItem_check( mobile, targ, DEFAULT_ITEM ))
		return 0;
	endif

	TurnObjectToward( mobile, targ.x, targ.y );
	PrintTextAbove( mobile, "Let me see..." );
	PerformAction( mobile, 0x20 );
	SleepMS( 1500 );
	PrintTextAbove( mobile, "Hmmmm..." );
	SleepMS( 1000 );

	SkillCheck( mobile, FORENSIC_EVALUATION, -1 );
 

	PrintForensicMessageAbove( mobile );
 
	var killer,
	    looter;
 
	var Last_Damage := GetObjProperty( targ, "LastDamage" );
	if( Last_Damage && Last_Damage[1] != targ.ownerserial )
		killer := SystemFindObjectBySerial( Last_Damage[1] );
	endif
                
	var Last_Loot := GetObjProperty( targ, "LastLoot" );
	if( Last_Loot && Last_Loot[1] != targ.ownerserial )
		looter := SystemFindObjectBySerial( Last_Loot[1] );
	endif
                
	Sleep( 2 );

	if( killer )
		PrintTextAbove( mobile, "I think it was killed by "+killer.name+"." );
		Sleep( 2 );
	endif

	if( looter )
		PrintTextAbove( mobile, "I see that it was looted by "+looter.name+"." );
		Sleep( 2 );
	endif
  
	if( !killer && !looter )
		PrintTextAbove( mobile, "Finally, I have no clues..." );
	endif

        return 1;
endprogram

function PrintForensicMessageAbove( speaker )

        var thinking := { "OH!",
		          "I think I got it...",
		          "Arf, my bad...",
                          "I know!"
	                };

	PrintTextAbove( speaker, thinking[RandomInt( thinking.Size() )+1] );

	return 1;
endfunction
