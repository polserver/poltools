/*
        Maintened by *Edwards
        
        2008-10-12
        
        Last Modifications 2008-12-12
             * Now the player must accept being resurected.

        Last Modifications 2009-08-24
             * Added useItem()
*/
use uo;
use os;
use util;
use math;

include "include/itemUtil";
include ":attributes:attributes";
include ":classes:classes";
include ":death:resurrection";
include ":equipment:protections";
include ":timedscripts:timedScripts";

program UseBandage( mobile, bandage )

       	if( !useItem_check( mobile, bandage, ITEM_INCONTAINER ))
		return 0;
	endif

	SendSysMessage( mobile, "Whom would you like to heal?" );
	var targ := Target( mobile, TGTOPT_CHECK_LOS+TGTOPT_HELPFUL );
        if( !targ )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        elseif( Distance( mobile, targ ) > 1 )
 		SendSysMessage( mobile, "The patient is too far away." );
		return 0;
        elseif( !targ.IsA( POLCLASS_MOBILE ))
                SendSysMessage( mobile, "You should consider therapy!" );
                return 0;
        elseif( targ.IsA( POLCLASS_NPC ) || targ.IsA( POLCLASS_CORPSE ))
		var veterinary := start_script( ":veterinary:veterinary", array{mobile, bandage, targ} );
                if( veterinary.errortext )
                         SysLog( "Error starting <:veterinary:veterinary> -->"+veterinary.errortext );
                endif
		return 1;
        endif
        
        if( targ.dead )
		ResurrectGhost( mobile, targ, bandage );
	elseif( targ.poisoned )
		CurePatient( mobile, targ, bandage );
	else
                HealPatient( mobile, targ, bandage );
        endif
        
	return 1;
endprogram

function ResurrectGhost( mobile, targ, bandage )

	var heal_skill := CInt( AP_GetSkill( mobile, HEALING ));
	var anat_skill := CInt( AP_GetSkill( mobile, ANATOMY ));
 
	if( heal_skill < 90 || anat_skill < 90 )
		SendSysMessage( mobile, "You don't have enought skills to ressurect someone." );
	 	return 0;
	elseif( targ.multi )
                SendSysMessage( mobile, "You cannot resurrect in a house." );
                return 0;
        elseif( !SubtractAmount( bandage, 5 ))
                SendSysMessage( mobile, "You don't have enough bandages." );
                return 0;
        endif

        SendSysMessage( mobile, "You try to bring "+targ.name+" back to life." );
	SendSysMessage( targ, mobile.name+" is trying to bring you back to life." );

        var vital := AP_GetVital( mobile, HITS ),
            delay := 15;
        
 	while( Cint( delay ))
		Sleep( 1 );
		delay -= 1;
		if( Distance( mobile, targ ) > 2 || mobile.dead || !targ.dead || AP_GetStat( mobile, STRENGTH ) < vital )
                        SendSysMessage( mobile, "You have lost your concentration." );
                        SendSysMessage( targ, mobile.name+" couldn't resurrect you." );
			return 0;
		endif
	endwhile
 
        if( !SkillCheck( mobile, HEALING, 90 ))
		SendSysMessage( mobile, "You couldn't resurrect your patient." );
                SendSysMessage( targ, mobile.name+" couldn't resurrect you." );
                return 0;
        elseif( !ResurrectionGump( targ ))
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        endif
                
	SendSysMessage( mobile, "You bring "+targ.name+" back to life!" );
	SendSysMessage( targ, mobile.name+" brough you back to life!" );
 
	CreateItemInBackpack( mobile, 0x0e20, 5 );

	return 1;
endfunction

function CurePatient( mobile, targ, bandage )

        if( !SubtractAmount( bandage, 2 ))
                SendSysMessage( mobile, "You don't have enough bandages." );
                return 0;
        endif
        
        if( mobile.serial != targ.serial )
                SendSysMessage( mobile, "You start to cure "+targ.name+"." );
	 	SendSysMessage( targ, mobile.name+" starts to cure you." );
	else
		SendSysMessage( mobile, "You start to cure yourself." );
	endif

        var vital := AP_GetVital( targ, HITS ),
            delay := 10;
        
 	while( Cint( delay ))
		Sleep( 1 );
		delay -= 1;
		if( mobile.dead || targ.dead || !targ.poisoned || Distance( mobile, targ ) > 2 )
                        SendSysMessage( mobile, "You have lost your concentration." );
			return 0;
                elseif( AP_GetVital( targ, HITS ) < vital )
                        if( !SubtractAmount( bandage, 2 ))
                                SendSysMessage( mobile, "You don't have enough bandages." );
                                if( mobile.serial != targ.serial )
           		                   SendSysMessage( mobile, "You couldn't cure your patient." );
                                           SendSysMessage( targ, mobile.name+" couldn't cure you." );
                                else
                                           SendSysMessage( mobile, "You couldn't cure yourself." );
                                endif
		                return 0;
                        else
			        SendSysMessage( mobile, "Yours fingers slip..." );
                                CreateItemInBackpack( mobile, 0x0e20, 1 );
			        vital := AP_GetVital( targ, HITS );
	                endif
		endif
	endwhile
 
        var current_poison := CInt( GetObjProperty( targ, "#PoisonedLevel" ));
        if( !current_poison )
                SendSysMessage( mobile, "That is not poisoned." );
                return 0;
        endif
        
        var bonus := CInt( GetMobileProtection( mobile, "HealingIncrease" ));
        if( bonus )
                bonus := CInt( bonus * 5 );
        endif
        
        var difficulty := CInt(( current_poison * 25 ) + 15 ),
            healing := AP_GetSkill( mobile, HEALING );

        if( SkillCheck( mobile, HEALING, CInt( difficulty )) > 0 || healing >= CInt( difficulty - bonus ))

                SendSysMessage( mobile, "You cured level "+current_poison+" poison." );
                TS_LowerDuration( targ, "DefaultPoison", -1 );
                
                if( mobile.serial != targ.serial )
                        SendSysMessage( targ, mobile.name+" has cured you!" );
                else
                        var heal := CInt( HL_GetAmount( mobile ) * 0.25 );
		        SendSysMessage( mobile, "You heal "+heal+" damages." );
          
                        HealDamage( mobile, heal );
                endif
        else
                if( mobile.serial != targ.serial )
                        SendSysMessage( mobile, "You couldn't cure your patient." );
                        SendSysMessage( targ, mobile.name+" couldn't cure you." );
                else
                        SendSysMessage( mobile, "You couldn't cure yourself!" );
	        endif
        endif
        
	CreateItemInBackpack( mobile, 0x0e20, 2 );

	return 1;
endfunction

function HealPatient( mobile, targ, bandage )

        var max_vital := AP_GetStat( targ, STRENGTH ),
            vital := AP_GetVital( targ, HITS );
            
        if( CInt( vital ) >= CInt( max_vital ))
       	        SendSysMessage( mobile, "This patient don't have a bruise!" );
                return 0;
        elseif( !SubtractAmount( bandage, 1 ))
                SendSysMessage( mobile, "You don't have enough bandages." );
                return 0;
        endif
        
        if( mobile.serial != targ.serial )
                SendSysMessage( mobile, "You start to heal "+targ.name+"." );
	 	SendSysMessage( targ, mobile.name+" starts to heal you." );
	else
		SendSysMessage( mobile, "You start to heal yourself." );
	endif
            
        var delay := 7;
 	while( Cint( delay ))
		Sleep( 1 );
		delay -= 1;
		if( mobile.dead || targ.dead || targ.poisoned || Distance( mobile, targ ) > 2 )
                        SendSysMessage( mobile, "You have lost your concentration." );
			return 0;
                elseif( CInt( vital ) >= CInt( max_vital ))
                 	SendSysMessage( mobile, "This patient don't have a bruise!" );
                        return 0;
                elseif( AP_GetVital( targ, HITS ) < vital )
                        if( !SubtractAmount( bandage, 1 ))
                                SendSysMessage( mobile, "You don't have enough bandages." );
                                if( mobile.serial != targ.serial )
           		                   SendSysMessage( mobile, "You couldn't heal your patient." );
                                           SendSysMessage( targ, mobile.name+" couldn't heal you." );
                                else
                                           SendSysMessage( mobile, "You couldn't heal yourself." );
                                endif
		                return 0;
                        else
			        SendSysMessage( mobile, "Yours fingers slip..." );
                                CreateItemInBackpack( mobile, 0x0e20, 1 );
			        vital := AP_GetVital( targ, HITS );
	                endif
		endif
	endwhile
        
        var difficulty := CInt( AP_GetStat( mobile, STRENGTH ) - AP_GetVital( mobile, HITS ));
        if( difficulty > 65 )
                difficulty := 65;
        endif

        if( SkillCheck( mobile, HEALING, difficulty ) > 0 )
        
                var heal := HL_GetAmount( mobile );
		SendSysMessage( mobile, "You heal "+heal+" damages." );
  
                if( mobile.serial != targ.serial )
                        HealDamage( targ, heal );
		        SendSysMessage( targ, mobile.name+" heals "+heal+" damages." );
                else
                        HealDamage( mobile, heal );
                endif
	else
                if( mobile.serial != targ.serial )
		        SendSysMessage( mobile, "You couldn't heal your patient." );
                        SendSysMessage( targ, mobile.name+" couldn't heal you." );
                else
                        SendSysMessage( targ, "You couldn't heal yourself." );
                endif
        endif
        
        CreateItemInBackpack( mobile, 0x0e20, 1 );
	
	return 1;
endfunction

function HL_GetAmount( mobile )

	var heal_skill := CInt( AP_GetSkill( mobile, HEALING ) / 10 );
	var anat_skill := CInt( AP_GetSkill( mobile, ANATOMY ) / 15 );

        var heal := RandomDiceRoll( CInt( heal_skill )+"d4+"+CInt( heal_skill )) + RandomDiceRoll( "1d8+2" ) + CInt( anat_skill );

        var bonus := CInt( GetMobileProtection( mobile, "HealingIncrease" ));
	if( bonus )
		heal *= CDbl(( bonus * 0.1 ) + 1 );
	endif

        if( GetObjProperty( mobile, WARRIOR ))
                heal *= ClasseBonus( mobile, WARRIOR );
        endif
        
        return CInt( heal );
endfunction
