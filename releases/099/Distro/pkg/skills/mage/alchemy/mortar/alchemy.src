/*
        Created by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2009-11-24

        Last Modifications 2009-12-04
             * Added loop messages
             * Removed deprecated var counter and now using only amt for while loop
*/
use uo;
use os;
use util;
use cfgfile;

include "include/itemUtil";
include ":alchemy:drinkPotion";
include ":crafting:crafting";

var item_config := ReadConfigFile( ":alchemy:itemdesc" );
var craft_config := ReadConfigFile( ":alchemy:alchemy" );

program use_MapmakersPen( who, tool )

	if( !useItem_check( who, tool, ITEM_INCONTAINER ))
		return 0;
	endif

	var rinfo := sendCraftGump( who, ALCHEMY );
	if( rinfo == 0 )
		return 0;
	endif

	var the_objtype := rinfo[1];
	var amt := rinfo[2];

	makeAlchemyMap( who, tool, the_objtype, amt );

	return 1;
endprogram

function makeAlchemyMap( who, tool, the_objtype, amt )

	if( !the_objtype || !amt )
		SendSysMessage( who, "Cancelled." );
		return 0;
	endif

 	var craft_elem := craft_config[the_objtype],
 	    skill_elem := menu_config[craft_skill],
            x := who.x,
	    y := who.y;

        SendSysMessage( who, "You start "+skill_elem.EndMessage+"...", 3, 89 );

	while( amt )
		if( !tool )
			break;
		endif

                if( amt != -1 )
                	SendSysMessage( who, "Looping ["+amt+" more to go].", 3, 89 );
                else
                	SendSysMessage( who, "Looping.", 3, 89 );
                endif
                
                Sleep( CRAFT_DELAY );

                Craft_PlayEffects( who, skill_elem );

                if( who.x != x || who.y != y )
                        SendSysMessage( who, "Cancelled." );
			break;
                endif

                var parts_list := Craft_CheckParts( who, craft_elem );
                if( !parts_list )
                	break;
                elseif( Craft_SkillCheck( who, craft_elem, CRAFT_ADV_ALL ) > 0 )
                	if( !Craft_ConsumeParts( who, parts_list ))
                		break;
			endif
   
			var the_item := Craft_CreateItem( who, the_objtype, craft_elem ),
                            strength := GetPotionStrength( the_item ),
                            power := CInt( strength * ClasseBonus( who, MAGE ));
                            
                        Craft_ExpCheck( who, craft_elem, the_item );
                        SetObjProperty( the_item, "ByTrueMage", power );
		else
                        parts_list.Erase(3854); //Do not destroy empty bottles
                  	if( !Craft_ConsumeParts( who, parts_list ))
                		break;
			endif
			SendSysMessage( who, "You lost some materials." );
		endif

                CheckToolWear( who, tool, craft_skill );

                if( amt != -1 )
                	amt -= 1;
                endif
	endwhile

        SendSysMessage( who, "You stop "+skill_elem.EndMessage+"...", 3, 89 );

	return 1;
endfunction
