/*
        Created by *Edwards

        2010-01-03
*/
use uo;
use os;
use util;

include "include/itemUtil";
include "include/shapes";
include ":alchemy:drinkPotion";

Program PoisonFlask( parms )

	var potion := parms[1],
	    who := parms[2],
            thecenter := potion;

	while( thecenter.container )
		thecenter := thecenter.container;
	endwhile

	SetScriptController( who );

       	var power := CInt( GetPotionStrength( potion )),
 	    range := power + 2,
 	    duration := ( 2 * power ) + 2;

	var location := struct;
	location.+x := thecenter.x;
	location.+y := thecenter.y;
	location.+z := thecenter.z;
	location.+objtype := potion.objtype;

        SubtractAmount( potion, 1 );

	var rect := GetCoordsInSquare( location.x, location.y, range );
	var fields := dictionary;
	for( duration; duration > 0; duration -= 1 )
		MakeGasRect( who, location, rect, fields );
		Sleep(5);
	endfor

        return 1;
endprogram

function MakeGasRect( who, location, rect, byref fields )

	foreach coord in rect
                if( !CheckLosBetween( coord.x, coord.y, GetWorldHeight( coord.x, coord.y ), location.x, location.y, location.z, who.realm ))
                        continue;
                endif
                
		if( !fields[coord.x+":"+coord.y] )
			if( RandomInt(2) )
				var gas := CreateItemAtLocation( coord.x, coord.y, GetWorldHeight( coord.x, coord.y ), 0x3920, 1, who.realm );

				gas.SetOwner( who.serial );
				gas.SetDuration( RandomInt(5)+1 );

				fields[coord.x+":"+coord.y] := gas;
			endif
		endif
		SleepMS(5);
	endforeach

        return 1;
endfunction
