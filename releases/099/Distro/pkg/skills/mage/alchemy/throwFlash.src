/*
        Created by *Edwards

        2010-01-03
*/
use uo;
use os;

include ":alchemy:drinkPotion";
include "include/itemUtil";

program potion_TrowFlash( who, potion )

       	if( !useItem_check( who, potion, ITEM_INCONTAINER ))
		return 0;
	endif

	SendSysMessage( who, "Select a target." );

	if( GetObjProperty( potion, "#bomb" ) < ReadGameClock() )
		start_script( ":alchemy:explosionTimer", {potion, who, ":alchemy:flashBomb"} );
	endif

	SetObjProperty( potion, "#bomb", ReadGameClock() + 3 );
	ReleaseItem( potion ); // allows bomb to be destroyed in pack

	var throw_to := TargetCoordinates( who );
	if( !throw_to )
		return 0;
	elseif( !potion )
		return 0;
	elseif( !CheckLosAt( who, throw_to.x, throw_to.y, throw_to.z ))
		MoveObjectToLocation( potion, who.x, who.y, who.z, who.realm, MOVEOBJECT_FORCELOCATION );
		SendSysMessage( who, "You can't see that!" );
		return 0;
	elseif( !potion )
		return 0;
	endif
 
        PrintTextAbove( who, "*Throws a potion!*" );
        PerformAction( who, 0x9 );
	SleepMS(300);
 
	Detach();
	PlayMovingEffectXYZ( who.x, who.y, who.z+15, throw_to.x, throw_to.y, throw_to.z, potion.graphic, 8, 0, 0, who.realm );
	if( !potion )
		return 0;
	endif
 
	MoveObjectToLocation( potion, throw_to.x, throw_to.y, throw_to.z, who.realm, MOVEOBJECT_FORCELOCATION );

        return 1;
endprogram
