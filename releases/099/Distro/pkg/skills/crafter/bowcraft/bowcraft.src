/*
        Created by *Edwards

        2009-11-05

        Last Modifications 2009-12-04
             * Added loop messages
             * Removed deprecated var counter and now using only amt for while loop
*/
use uo;
use os;
use cfgfile;

include "include/itemUtil";
include ":crafting:crafting";

var item_config := ReadConfigFile( ":*:itemdesc" );
var craft_config := ReadConfigFile( ":bowcraft:bowcraft" );

program use_CarpentryTool( parms )

        var who := parms[1],
            tool := parms[2],
            targ := parms[3];

	if( !useItem_check( who, tool, ITEM_EQUIPPED ))
		return 0;
	endif

	var rinfo := sendCraftGump( who, BOWCRAFT_FLETCHING );
	if( rinfo == 0 )
		return 0;
	endif

	var the_objtype := rinfo[1],
	    amt := rinfo[2];

        makeBowcraftItem( who, tool, targ, the_objtype, amt );

	return 1;
endprogram

function makeBowcraftItem( who, tool, targ, the_objtype, amt )

	if( !the_objtype || !amt )
		SendSysMessage( who, "Cancelled." );
		return 0;
	endif

 	var craft_elem := craft_config[the_objtype],
 	    skill_elem := menu_config[craft_skill],
            material := CInt( craft_elem.Material ),
            useOn_obj := targ.objtype,
            mat_diff := targ.GetRessourceDifficulty(),
            x := who.x,
	    y := who.y;

        SendSysMessage( who, "You start "+skill_elem.EndMessage+"...", 3, 89 );
        
	while( amt )
		if( !tool )
			break;
		endif

                if( amt != -1 )
                	SendSysMessage( who, "Looping ["+amt+" more to go].", 3, 89 );
                else
                	SendSysMessage( who, "Looping.", 3, 89 );
                endif
                
                Sleep( CRAFT_DELAY );

                Craft_PlayEffects( who, skill_elem );

                if( who.x != x || who.y != y )
			SendSysMessage( who, "Cancelled." );
			break;
                elseif( material > targ.amount )
			SendSysMessage( who, "You do not have the required materials to make that." );
                	break;
                elseif( !SubtractAmount( targ, material ))
			SendSysMessage( who, "You do not have the required materials to make that." );
                	break;
                elseif( Craft_SkillCheck( who, craft_elem, CRAFT_ADV_ALL, mat_diff ) > 0 )
			var the_item := Craft_CreateItem( who, the_objtype, craft_elem, useOn_obj );
                        Craft_ExpCheck( who, craft_elem, the_item );
		else
			SendSysMessage( who, "You lost some materials." );
		endif

                CheckToolWear( who, tool, craft_skill );

                if( amt != -1 )
                	amt -= 1;
                endif
	endwhile

        SendSysMessage( who, "You stop "+skill_elem.EndMessage+"...", 3, 89 );

	return 1;
endfunction
