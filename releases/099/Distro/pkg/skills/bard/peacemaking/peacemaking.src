/*
        Maintened by *Edwards

        2008-10-19
        
        Last Modifications 2009-01-18
             * Verifications of the codes

        Last Modifications 2009-07-30
             * Revised code for better performances.
*/
use uo;
use os;

include ":attributes:attributes";
include ":brainAI:npcUtil";
include ":classes:classes";
include ":musicianship:bard";
include ":brainAI:npcNerves";
include ":brainai:npcCommands";

program skill_Peacemaking( mobile )

        var instrument := CheckInstrument( mobile );
        if( !instrument )
                SendSysMessage( mobile, "You do not have any instruments to play!" );
                return 0;
        endif

        var range := GetBardRange( mobile ),
            affected := array;

        foreach character in ListMobilesNearLocation( mobile.x, mobile.y, mobile.z, range, mobile.realm )
                if( mobile.serial != character.serial && ( character.warmode || GetNerveProcess( character, "Combat" ).pid ))
                        if( character.cmdlevel >= 1 )
                                  continue;
                        elseif( !MainCheckTarget( mobile, character, range ))
                                continue;
                        endif
                        
                        affected.Append({ character, GetMobileDifficulty( mobile, character )});
                endif
                SleepMS(5);
        endforeach

        if( Len( affected ) < 1 )
                SendSysMessage( mobile, "There is nothing to calm down in your range." );
		return 0;
        endif
        
        var sfx_success := instrument.SfxSuccess(),
            sfx_failure := instrument.SfxFaillure();
 
        var delay := ( RandomInt( 5 ) + 1 ) * ClasseBonus( mobile, BARD );

        var success := 0;
        foreach element in affected
		var character := element[1],
		    difficulty := element[2];

                if( SkillCheck( mobile, MUSICIANSHIP, difficulty, 0, ADV_DISABLE ) > 0 )
		        if( SkillCheck( mobile, PEACEMAKING, -1 ) > 0 )
				success += 1;
				PrintTextAbove( character, "dazzled by the beautiful music!" );
    
                                if( character.IsA( POLCLASS_NPC ))
                                        SetObjProperty( character, "#Peacemaking", ReadGameClock() + CInt( delay ));
                                        AI_WarMode( character, 0 );
                                else
                                        character.warmode := 0;
                                endif
			endif
		endif
                SleepMS(5);
	endforeach
 
        if( success > 0 )
		PlaySoundEffect( mobile, sfx_success );
	else
		PlaySoundEffect( mobile, sfx_failure );
		PrintTextAbovePrivate( mobile, "*you play a musical instrument... poorly.*", mobile );
	endif

	return 1;
endprogram
