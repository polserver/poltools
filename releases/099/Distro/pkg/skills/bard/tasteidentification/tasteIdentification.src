/*
        Maintened by *Edwards

        2009-01-17
        
        Last Modifications 2009-08-13
             * Added poison check
             * Added the results
             
        To-Do
             * Need to identify if poisonned with skill.
*/
use uo;
use cfgfile;

include ":attributes:attributes";
include "include/itemUtil";
include ":timedscripts:timedScripts";
include ":alchemy:drinkPotion";

program skill_TasteID( mobile )

	SendSysMessage( mobile, "What would you like to taste?" );

	var targ := Target( mobile );
	if( !targ )
                SendSysMessage( mobile, "Cancelled." );
		return 0;
	elseif( targ.IsA( POLCLASS_MOBILE ))
		SendSysMessage( mobile, "You feel that such an action would be inappropriate." );
		return 0;
	elseif( !targ.IsA( POLCLASS_ITEM ))
		SendSysMessage( mobile, "That's not something you can taste." );
		return 0;
	elseif( !useItem_check( mobile, targ, ITEM_INCONTAINER ))
		return 0;
	endif
	
	var tasted := GetObjProperty( targ, "TasteIDed" );
	if( !tasted )
		tasted := array{};
	endif
 
        var item_config := ReadConfigFile( ":alchemy:itemdesc" ),
            item_script := item_config[targ.objtype].script;
	
	if( targ.IsKeg() )
		var amt	:= CInt( GetObjProperty( targ, "Amount" ));
		if( !amt )
			SendSysMessage( mobile, "There seems to be nothing in the keg." );
                        EraseObjProperty( targ, "TasteIDed" );
			return 0;
		endif
		
		if( mobile.serial in tasted )
			SendSysMessage( mobile, "You are already familiar with this keg's contents." );
			return 0;
		endif
		
		if( SkillCheck( mobile, TASTE_IDENTIFICATION, -1 ) > 0 )
			SendSysMessage( mobile, "You were able to identify the contents." );
			SendSysMessage( mobile, "It appears to be a "+targ.desc+"." );
		else
			SendSysMessage( mobile, "You are unable to identify the contents." );
			return 0;
		endif
	elseif( targ.IsPotion() )
		if( mobile.serial in tasted )
			SendSysMessage( mobile, "You already know what kind of potion that is." );
			return 0;
		endif

		if( SkillCheck( mobile, TASTE_IDENTIFICATION, -1 ) > 0 )
			SendSysMessage( mobile, "The potion tastes familiar to you. A slight taste tells you the properties of the potion." );
			SendSysMessage( mobile, "It appears to be a "+targ.desc+"." );
		else
			if( item_script == ":alchemy:poison" )
				SendSysMessage( mobile, "You do not recognize the potion, but it appears to be toxic!" );

                                var power := CInt( GetPotionStrength( targ )),
 	                            duration :=  CInt( power * 120 );
    
                                TS_StartTimer( mobile, "DefaultPoison", duration, power );
			else
				SendSysMessage( mobile, "You are unable to recognize the potion." );
			endif
			
			return 0;
		endif
	elseif( targ.IsFood() || targ.IsDrink() )
		if( mobile.serial in tasted )
                        SendSysMessage( mobile, "You already know what that is." );
			return 0;
		endif

		if( SkillCheck( mobile, TASTE_IDENTIFICATION, -1 ) > 0 )
			SendSysMessage( mobile, "It appears to be a "+targ.desc+"." );
		else
			SendSysMessage( mobile, "You cannot discern anything about this substance." );
			return 0;
		endif
	endif
	
	tasted.Append( mobile.serial );
	SetObjProperty( targ, "TasteIDed", tasted );

	return 1;
endprogram
