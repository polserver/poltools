/*
        Maintened by *Edwards

        2008-10-12
        
        Last Modifications 2009-01-18
             * Fixed a lot of sysmessage
             
        Last Modifications 2009-08-22
             * Created method scripts for campire and burned wood

        Last Modifications 2009-08-23
             * Added classe bonus with duration of campfire
*/
include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";
include ":classes:classes";
include "include/itemUtil";

program UseWood( who, wood )

	if( !useItem_check( who, wood, ITEM_WILD ))
		return 0;
	endif

	var camptimer := GetObjProperty( who, "#camped" );
	if( camptimer && camptimer > ReadGameClock() )
		SendSysMessage( who, "You must wait a few moments to camp again." );
		return 0;
	endif

	if( SkillCheck( who, CAMPING, -1 ) > 0 )
		SetObjProperty( who, "#camped", ReadGameClock() + 10 );
  
		var x := wood.x,
		    y := wood.y,
		    z := wood.z,
		    wx := who.x,
		    wy := who.y,
		    realm := wood.realm;
  
                SubtractAmount( wood, 1 );
                
		var campfire := CreateItemAtLocation( x, y, z, 0xDE3, 1, realm );
                if( campfire.errortext )
			SendSysMessage( who, "Error creating <> -->"+campfire.errortext );
			return 0;
                endif
                
                var duration := 30;
                if( GetObjProperty( who, RANGER ))
                        duration *= ClasseBonus( who, RANGER );
                endif
                
                campfire.SetOwner( who );
		campfire.SetDuration( duration );

                SetObjProperty( campfire, "#camped", ReadGameClock() + 30 );
  
		var secured := 10;
		while( secured )
			if( who.x != wx || who.y != wy || who.warmode || who.poisoned )
				SendSysMessage( who, "You abandon your preparations to camp." );
				return 0;
			endif
   
     			secured -= 1;
			Sleep(1);
		endwhile
  
		SendSysMessage( who, "Your campfire is now secure" );
                SetObjProperty( campfire, "Secured", 1 );
	else
		PrintTextAbovePrivate( wood, "You couldn't manage to start the fire.", who );
	endif

        return 1;
endprogram
