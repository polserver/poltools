/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2008-10-14

        Last Modifications 2008-12-13
             * Fixed a check distance between of mobile & targ

        Last Modifications 2009-01-18
             * GM Poseidon added with bonus

        Last Modifications 2009-08-23
             * Added useItem()

        Last Modifications 2009-11-24
             * Added check with LOS

        Last Modifications 2010-02-16
             * Added check if mobile is in noDamage area will get a penalty for amount harvested

        Last Modifications 2010-03-14
             * Added SpecialEffects()

        Last Modifications 2010-03-19
             * Added Autoloop to fishing
*/
use uo;
use os;
use util;

include "include/client";
include "include/facings";
include "include/itemUtil";
include "include/objType";
include ":attributes:attributes";
include ":autoloop:autoloop";
include ":classes:classes";
include ":crafting:crafting";
include ":loot:lootParser";

const NO_DAMAGE_BONUS := 1.50;
const HARVEST_BONUS := 2.5;

program skill_Fishing( mobile, pole )

        EquipItem( mobile, pole );

        if( !useItem_check( mobile, pole, ITEM_EQUIPPED ))
		return 0;
	endif

        SendSysMessage( mobile, "Select a fishing spot." );
        var targ := TargetCoordinates( mobile );
        if( !targ )
                SendSysMessage( mobile, "Cancelled." );
        elseif( !useItem_check( mobile, pole ))
		return 0;
        elseif( !IsWater( targ ))
                SendSysMessage( mobile, "You must target water." );
                return 0;
        elseif( !CheckLosAt( mobile, targ.x, targ.y, targ.z ))
                SendSysMessage( mobile, "You cannot see that." );
                return 0;
        endif
        
        var x := mobile.x,
            y := mobile.y;

        SendAutoLoop( mobile );
        SendSysMessage( mobile, "You start fishing...", 3, 89 );

	while( AutoLoop( mobile ))

                PlayFishingAnimation( mobile, targ );
                
        	var delay := RandomInt( 3 ) + 2;
        	while( delay )
			Sleep( 1 );
			delay -= 1;
			if( x != mobile.x || y != mobile.y || !pole )
				SendSysMessage( mobile, "You must be patient while fishing." );
                        	return 0;
			endif
		endwhile
  
                if( CheckSoSTreasure( mobile, targ ))
                	return 1;
                endif

        	var difficulty := CInt( GetHarvestDifficulty( "fish", targ.x, targ.y, targ.objtype ));
        	if( !difficulty )
                	SendSysMessage( mobile, "You can't seem to get any fish here." );
                	return 0;
        	endif

        	if( SkillCheck( mobile, FISHING, -1 ) > 0 )

                	var harvest := HarvestResource( "fish", targ.x, targ.y, 1, 1 );
                	if( !harvest )
                        	SendSysMessage( mobile, "There's no more fish left there." );
                        	return 0;
                	endif

                	var fish_amount := CInt( GetFishAmount( mobile, pole ) * HARVEST_BONUS );
                	var dagger := GetEquipmentByLayer( mobile, LAYER_HAND1 ).graphic == 3921;

                	if( SpecialEffects( mobile, pole, fish_amount, dagger ))
                        	continue;
                	endif

                	if( dagger )
                        	PlaySoundEffect( mobile, 0x0057 );

                                var i;
                                For( i:=0; i<fish_amount; i+=1 )
                        	 	CreateItemInBackpack( mobile, 0x097a, 1 );
                                 	SleepMS(5);
                                endfor
                                
                        	SendSysMessage( mobile, "You carve the fish and put "+fish_amount+" steak(s) in your backpack." );
                	else
                        	var create := CreateItemInBackpack( mobile, 0x09cc, fish_amount );
                        	if( !create )
                                	SendSysMessage( mobile, "They don't seem to be biting." );
                        	endif
                         
                        	SendSysMessage( mobile, "You put "+fish_amount+" fish in your backpack." );
                	endif
        	else
                	SendSysMessage( mobile, "They don't seem to be biting." );
        	endif

        	CheckToolWear( mobile, pole, FISHING );
	endwhile

        EndAutoLoop( mobile );
        SendSysMessage( mobile, "You stop fishing...", 3, 89 );

        return 1;
endprogram

function CheckSoSTreasure( mobile, targ )

        foreach item in ListItemsNearLocation( targ.x, targ.y, targ.z, 6, targ.realm )
                if( item.IsSOSTile() )
                        if( FetchBottle( mobile, item ));
                                return 1;
                        endif
                endif
                SleepMS(5);
        endforeach
        
        return 0;
endfunction

function PlayFishingAnimation( mobile, targ )

        TurnObjectToward( mobile, targ.x, targ.y );
        PerformAction( mobile, 0x9 );

        SleepMS( RandomInt( 1000 ));
        
        PlaySoundEffect( mobile, 0x0240 );
        PlayStationaryEffect( targ.x, targ.y, targ.z, 13613, 10, 10, 1 );
        
        Sleep(2);
        
        return 1;
endfunction

function GetFishAmount( mobile, pole )

        var skill := AP_GetSkill( mobile, FISHING ),
            max_amount := CInt( RandomDiceRoll( "1d"+CInt( skill / 25 )+"+3" ) * ClasseBonus( mobile, RANGER ));

        var bonus := GetObjProperty( pole, "FishingBonus" );
	if( bonus )
		max_amount *= bonus;
	endif

        if( !A_IsIn( mobile, AREAS_NO_DAMAGE ))
                max_amount *= NO_DAMAGE_BONUS;
        endif

        if( max_amount < 1 )
		max_amount := 1;
	endif

        return CInt( max_amount );
endfunction

function SpecialEffects( mobile, pole, fish_amount, dagger )

        var bonus := ( CInt( AP_GetSkill( mobile, FISHING ) / 30 ) + 1 ) * ClasseBonus( mobile, RANGER );
        if( bonus < 5 )
                bonus := 5;
        endif
        
        var chance := RandomInt( 95 ) - bonus;

        var pole_bonus := GetObjProperty( pole, "FishingBonus" );
	if( pole_bonus )
		chance += 20;
	elseif( !GetObjProperty( mobile, CRAFTER ))
                chance -= 20;
        endif

        var ret := 0;
        case( CInt( chance ))
		15:	CreateItemInBackpack( mobile, 0xdca, 1 );
			SendSysMessage( mobile, "You just found a special fishing net!" );

                        ret := 1;
                        break;
   
                40:
                        CreateItemInContainer( mobile.backpack, RandomFish(), 1 );
                        SendSysMessage( mobile, "You caught a magical fish!" );
                        ret := 1;
			break;

                80:	CreateMagicFish( mobile, fish_amount, dagger );
			SendSysMessage( mobile, "You find some seaweed!" );

                        ret := 1;
			break;

		98:	CreateItemInBackpack( mobile, 0xa360, 1 );
			SendSysMessage( mobile, "You find a tattered old map!" );

                        ret := 1;
			break;

                100:	CreateItemInBackpack( mobile, CreateShell( mobile ), 1 );
			SendSysMessage( mobile, "You find something valuable!" );

                        ret := 1;
			break;
	endcase

	return ret;
endfunction

function CreateMagicFish( mobile, fish_amount, dagger )

	if( dagger )
		PlaySoundEffect( mobile, 0x0057 );

		CreateItemInBackpack( mobile, 0x097a, CInt( fish_amount ));
		SendSysMessage( mobile, "You carve the fish and put "+CInt( fish_amount )+" steak(s) in your backpack." );
	else
                var magic_fish := array{0xA350, 0xA351, 0xA352, 0xA353, 0xA354, 0xA355, 0xA356, 0xA357};

                CreateItemInBackpack( mobile, magic_fish.RandomEntry(), 1 );
	endif

        return 1;
endfunction

function CreateShell( mobile )

        case( RandomInt( 5 ))
                0: return 0x80ae;
                1: return 0x80af;
                2: return 0x80b0;
                3:
                4: if( AP_GetSkill( mobile, FISHING ) > 100 )
	                   if( RandomInt(3) == 2 )
		                   return 0x80b6;
	                   else
		                   return 0x80b5;
	                   endif
                   else
   	                   return 0x80b1;
                   endif
        endcase

        return 0;
endfunction

function FetchBottle( mobile, tile )
        
        var msg_serial := GetObjProperty( tile, "SOSMsgSerial" ),
            sos_message := 0;

        foreach item in EnumerateItemsInContainer( mobile.backpack )
                if( item.serial == msg_serial )
                        sos_message := item;
                        break;
                endif
                SleepMS(5);
        endforeach

        if( sos_message )
                SendSysMessage( mobile, "You catch a sunken treasure!" );
                
                SkillCheck( mobile, FISHING, -1, 400 );
	        CreateSOSChest( mobile );
	        DestroyItem( sos_message );
         
                return 1;
        endif

        return 0;
endfunction

function CreateSOSChest( mobile )

        var chest := CreateItemAtLocation( mobile.x, mobile.y, mobile.z, 0xE40, 1, mobile.realm );
        if( chest.errortext )
                SendSysMessage( mobile, "Error creating treasure chest -->"+chest.errortext );
                return 0;
        endif
        
        var level := RandomInt( CInt( GetObjProperty( mobile, RANGER )));
        if( !level )
                level := 1;
        elseif( level >= 6 )
                level := 6;
        endif
        
        Loot_Generate( chest, level, 1 );

        return 1;
endfunction

function RandomFish()

        case( RandomInt( 7 ))
                0: return 0xA350;
                1: return 0xA351;
                2: return 0xA352;
                3: return 0xA353;
                4: return 0xA354;
                5: return 0xA355;
                6: return 0xA356;
                7: return 0xA357;
        endcase

        return 0;
endfunction
