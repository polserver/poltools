/*
        Created by *Edwards

        2008-11-05
*/
use uo;
use npc;

include "include/security";
include ":animaltaming:taming";
include ":brainAI:npcNerves";
include ":brainAI:npcUtil";
include ":timedscripts:timedScripts";

program textcmd_tame( mobile )

        if( !AuthorizationCode( mobile ))
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        endif

        var targ := Target( mobile, TGTOPT_NOCHECK_LOS );
        if( !targ )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        elseif( !targ.IsA( POLCLASS_NPC ))
                SendSysMessage( mobile, "You must select a NPC." );
                return 0;
        endif

        var npc_cfg := NPC_GetNPCConfig( targ.npctemplate ),
            requiredTaming := CInt( npc_cfg.TameSkill );

        if( !requiredTaming && mobile.cmdlevel < 3 )
               SendSysMessage( mobile, "Your privileges are not allowing you to tame the creature." );
               return 0;
        endif
        
        TS_LowerDuration( targ, "Tame", -1 );
        Sleep(2);

        tameCreature( targ, mobile );

        return 1;
endprogram
