/*
        Created by *Edwards
        For FantasiaShard.com
        
        Edwards@FantasiaShard.com
        
        2010-04-15
*/
use cfgfile;
use uo;

include "include/string";
include ":attributes:attributes";

var physical_cfg := ReadConfigFile( ":animallore:physical" );

program skill_AnimalLore( who )

	SendSysMessage( who, "Whom shall I examine?" );
	var subject := Target( who );
	if( !subject )
		return 0;
	elseif( !subject.IsA( POLCLASS_MOBILE ))
		SendSysMessage( who, "You know nothing about that." );
		return 0;
	endif
 
	var MOB_NOTAPPROVED := array{ 0x190, 0x191, 0x25D, 0x25E };
	if( subject.graphic in MOB_NOTAPPROVED )
		SendSysMessage( who, "You know nothing about this creature" );
                return 0;
        endif

        var range := 10 * ClasseBonus( who, RANGER );
	if( Distance( who, subject ) > range )
		SendSysMessage( who, "The subject is too far away." );
		return 0;
        endif
        
	if( SkillCheck( who, ANIMAL_LORE, -1 ) > 0 )
		PrintTextAbovePrivate( subject, "That creature looks "+HandleSubStrength( subject )+".", who );

		if( subject.master )
                	HandleFreeTime( who, subject );
                endif
	else
		PrintTextAbovePrivate( subject, "You can not quite get a sense of that creature's physical characteristics.", who );
		return 0;
	endif
 
	return 1;
endprogram

function HandleSubStrength( subject )

	var sub_muscle := AP_GetStat( subject, "Strength" );
	var strength_elem := physical_cfg["Strength"];
	var phrase_list := GetConfigStringArray( strength_elem, "Str" );
        var result := "";

	foreach entry in ( phrase_list )
		// SplitWords the entry, using a tab as the delimiter.
		var p_array := SplitWords( entry, "\t" );
		if( sub_muscle < CDbl( p_array[1] ))
			result := p_array[2];
                        if( !result )
                        	result :=  SplitWords( entry )[2];
                        endif
			return result;
		endif
		SleepMS(2);
	endforeach

	return error{ "errortext":="Could not find strength phrase for serial "+subject.serial };
endfunction

function HandleFreeTime( who, subject )

        var free_time := GetObjProperty( subject, "FreeTime" );
	if( free_time )
		if(( ReadGameClock() + 1800 ) > free_time )
			PrintTextAbovePrivate( subject, "It looks very hungry!", who );
		elseif(( ReadGameClock() + 3600 ) > free_time )
			PrintTextAbovePrivate( subject, "It looks pretty hungry.", who );
		else
			PrintTextAbovePrivate( subject, "It looks well fed.", who );
		endif
	endif
 
        return 1;
endfunction
