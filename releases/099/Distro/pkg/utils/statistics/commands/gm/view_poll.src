/*
        Created by *Edwards

        2009-07-26
*/
use uo;
use os;
use cfgfile;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":statistics:account";
include ":statistics:poll_questions";
include "include/yesNo";

program textcmd_viewpoll( mobile )

        var targ := Target( mobile, TGTOPT_NOCHECK_LOS );
        if( !targ )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        elseif( !targ.IsA( POLCLASS_MOBILE ) || targ.IsA( POLCLASS_NPC ))
                SendSysMessage( mobile, "You must select a player." );
                return 0;
        endif

        var i;
        for( i:=1; i<=poll_questions.Size(); i+=1 )

                var poll := poll_questions[i],
                    question := poll[1],
                    answers := poll[2];

                if( !DisplayQuestion( mobile, targ, question, answers, i ))
                	return 0;
                endif

		SleepMS(5);
        endfor

        return 1;
endprogram

function DisplayQuestion( mobile, targ, question, answers, iter )

	var poll_answers := GetAccountProperty( targ.acctname, "FS_poll" );
        if( !poll_answers )
                SendSysMessage( mobile, "Couldn't find any answers." );
                return 0;
        endif

        var gump := GFCreateGump();

	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 300, 400 );

        GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 270, 25 );
	GFTextMid( gump, 15, 17, 270, 1720, "Fantasia Shard Official Poll" );

        //BackgroundAreas
	GFResizePic( gump, 15, 45, GFGetCfgConst( "Defaults", "ForeGround" ), 270, 345 );
        GFTextMid( gump, 15, 60, 270, 1720, "Question "+ CStr( iter+"/"+poll_questions.Size() ));

        GFResizePic( gump, 30, 90, GFGetCfgConst( "Backgrounds", "GOLDTRIM" ), 240, 70 );
        GFHTMLArea( gump, 45, 100, 220, 50, question, 0, 1 );

        var y_pos := 180;

        var i;
        for( i:=1; i<=answers.Size(); i+=1 )

                if( poll_answers[iter] == answers[i] )
                        GFTextLine( gump, 50, y_pos, 1720, answers[i] );
                else
                        GFTextLine( gump, 50, y_pos, 2100, answers[i] );
                endif

                GFAddButton( gump, 30, y_pos, 2117, 2118, GF_CLOSE_BTN, i );

                y_pos += 20;

        	SleepMS(5);
        endfor

        var input := GFSendGump( mobile, gump );
        if( !input )
        	return 0;
        endif

	return 1;
endfunction
