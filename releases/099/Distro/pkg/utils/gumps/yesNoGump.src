/*
        Maintened by *Edwards
        
        2011-01-22
*/
use uo;
use os;

include ":gumps:gumps";
include ":gumps:gumps_ex";

program Gump_YesNo( params )

        var mobile := params[1],
            prompt := params[2],
            yes_text := params[3],
            no_text := params[4],
            delay := params[5];

	SetObjProperty( mobile, "#CloseYesNoGumpPID", GetPid() );

	var yn_gump := GFCreateGump( 140, 100 );
	GFClosable( yn_gump, 0 );

	GFResizePic( yn_gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 400, 205 );
	GFResizePic( yn_gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 370, 175 );
 
        GFAddAlphaRegion( yn_gump, 0, 0, 400, 205 );

	var y_pos := 20;
	foreach line in ( GFWordWrap( prompt, 420 ))
		GFTextLine( yn_gump, 30, y_pos, 1153, line );
		y_pos += 20;
		SleepMS(2);
	endforeach

	y_pos := 100;
	GFAddButton( yn_gump, 30, ( y_pos+3 ), 2117, 2118, GF_CLOSE_BTN, 0xA1 );
	foreach line in ( GFWordWrap( yes_text, 355 ));
		GFTextLine( yn_gump, 50, y_pos, 1153, line );
		y_pos += 20;
		SleepMS(2);
	endforeach

	y_pos := 140;
	GFAddButton( yn_gump, 30, ( y_pos+3 ), 2117, 2118, GF_CLOSE_BTN, 0xA2 );
	foreach line in ( GFWordWrap( no_text, 355 ))
		GFTextLine(yn_gump, 50, y_pos, 1153, line);
		y_pos += 20;
		SleepMS(2);
	endforeach

        if( delay )
                var close_gump := GetObjProperty( mobile, "#CloseYesNoGumpPID" );
                GFCloseGumpTimed( mobile, close_gump, CInt( delay ), 0 );
        endif

	var input := GFSendGump( mobile, yn_gump );
        if( input[0xA1] )
                return 1;
        endif

        return 0;
endprogram
