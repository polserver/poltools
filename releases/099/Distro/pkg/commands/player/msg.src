/*
        Maintened by *Edwards
        
        Last Modifications 2009-01-03
             * Removed spymode and replaced by NotifyStaffOnline()
             
        Last Modifications 2010-03-22
             * Changed gump interface to custom.

        Last Modifications 2010-05-29
             * Added code to msg bots

*/
use uo;
use os;
use util;

include "include/security";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":datafile:datafile";
include ":security:speechReport";

program command_Message( who, text )

	if( GetProcess( CInt( GetObjProperty( who, "#CmdMSGPid" ))))
		return 0;
	else
		SetObjProperty( who, "#CmdMSGPid", GetPid() );
	endif

	if( CommandIssued( who, text ))
		return 1;
	else
		MessageGump( who );
	endif
endprogram

function MessageGump( who )

	var gump := GFCreateGump();

	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 310, 380 );
 
	GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 280, 25 );
	GFResizePic( gump, 15, 45, GFGetCfgConst( "Defaults", "ForeGround" ), 280, 320 );

	GFTextLine( gump, 30, 15, 1720, "Player Name" );
	GFTextLine( gump, 255, 15, 1720, "Send" );
 
        var names_list := GetNamesList( who ),
	    num_players := names_list.Size(),
	    counter := 0,
	    y_pos := 80;
     
	GFTextLine( gump, 30, 50, 1720, num_players+" players online" );

	GFPage( gump, 1 );
	foreach name in ( names_list.Keys() )
                if( name == who.name )
			continue;
                endif
                
		GFTextLine( gump, 30, y_pos, 2100, name );
		GFAddButton( gump, 261, y_pos+3, 2117, 2118, GF_CLOSE_BTN, _name_iter );

		y_pos += 20;
		counter += 1;

		if( counter >= 13 && _name_iter < num_players )
			GFAddButton( gump, 280, 320, 0xA92, 0xA93, GF_PAGE_BTN, gump.cur_page+1 );
			GFPage( gump, gump.cur_page+1 );
			GFAddButton( gump, 280, 80, 0xA90, 0xA91, GF_PAGE_BTN, gump.cur_page-1 );
			counter := 0;
			y_pos := 80;
		endif
		SleepMS(2);
	endforeach

	var input := GFSendGump( who, gump );
	input := input[0];

	if( !input )
		SendSysMessage( who, "Cancelled." );
		return 0;
	else
		names_list := names_list.Keys();
		SendMessage( who, names_list[input] );
	endif
endfunction

function GetNamesList( who )

	var the_list := dictionary; // Used to store names in ABC order
	foreach person in EnumerateOnlineCharacters();
		var msg_settings := GetMessageSettings( person );
		if( msg_settings.disable && !who.cmdlevel )
			//If messages are off, and 'who' is a player, dont add the name.
			//If 'who' has a command level, then he/she can see any name.
			continue;
		elseif( !msg_settings.visible && person.concealed > who.cmdlevel );
			//If the person is not in visible mode, and concealed is
			//higher than who's command level, don't add that person.
			continue;
                elseif( who.serial == person.serial )
			continue;
		else
			the_list.Insert( person.name, person.serial );
		endif

		SleepMS(2);
	endforeach
 
        /* Code for online count cheat */
        var bots := GetGlobalProperty( "#Bots" );
        foreach bot in bots
                the_list.Insert( bot.name, _bot_iter );
                SleepMS(2);
        endforeach

	return the_list;
endfunction

function SendMessage( who, name_text, is_reply:=0 )

	var can_msg := 0;
	if( is_reply )
		can_msg := 1;
	elseif( name_text in ( GetNamesList( who ).Keys() ))
		can_msg := 1;
	endif

	if( !can_msg )
		SendSysMessage( who, name_text+" is not online." );
		return 0;
	endif

	var message_txt := RequestInput( who, who.backpack, "What message would you like to send to "+name_text+"? (hit escape to cancel)" );
	if( !message_txt )
		SendSysMessage( who, "Cancelled." );
		return 0;
	endif

	var sndr_settings := GetMessageSettings( who );
	sndr_settings["last"] := name_text;
	SetObjProperty(who, ".msg", sndr_settings);

	foreach player in EnumerateOnlineCharacters()
		if( player.name == name_text )
			SendSysMessage( player, "Message from "+who.name+" : "+message_txt );

			var rcvr_settings := GetMessageSettings( player );
			rcvr_settings["reply"] := who.name;
			SetObjProperty( player, ".msg", rcvr_settings );

			if( sndr_settings.Exists( "disabled" ))
				SendSysMessage( who, "Reminder: You have messages off." );
				SendSysMessage( player, "Note: "+who.name+" has messages off. Use .msg reply." );
			endif
   
                        NotifyStaffOnline( who, "[MSG SpyMode] To:"+name_text+": "+message_txt );
                        Speech_ReportText( who, message_txt, ITEM_REPORT_SYSLOG );
                endif

		SleepMS(5);
	endforeach
 
	SendSysMessage( who, "You sent a message to ["+name_text+"]: "+message_txt );
endfunction

function CommandIssued( who, text )

	var msg_settings := GetMessageSettings( who );
        if( text == "help" )
                SendSysMessage( who, "off : to toggle off your name on the message list." );
                SendSysMessage( who, "on : to toggle on your name on the message list." );
                SendSysMessage( who, "reply : to send a message to the last person who contacted you." );
                SendSysMessage( who, "last : to send a message to the last person you contacted." );
                return 1;
	elseif( text == "off" )
		msg_settings["disabled"] := 1;
		SetObjProperty( who, ".msg", msg_settings );
		SendSysMessage( who, "Your name has been removed from the .msg list." );
		return 1;
	elseif( text == "on" )
		msg_settings.Erase( "disabled" );
		SetObjProperty( who, ".msg", msg_settings );
		SendSysMessage( who, "Your name has been put back on from the .msg list." );
		return 1;
	elseif( text == "reply" )
		SendMessage( who, msg_settings["reply"], 1 );
		return 1;
	elseif( text == "last" )
		SendMessage( who, msg_settings["last"], 1 );
		return 1;
	elseif( text )
		SendMessage( who, text );
		return 1;
	endif

	if( who.cmdlevel <= 1 )
		return 0;
	elseif( text == "visible on" )
		msg_settings["visible"] := 1;
		msg_settings.Erase( "visible" );
		SendSysMessage( who, "Players can now message you while concealed." );
		SetObjProperty( who, ".msg", msg_settings );
		return 1;
	elseif( text == "visible off" )
		msg_settings.Erase( "visible" );
		SendSysMessage( who, "Players can no longer message you while concealed." );
		SetObjProperty( who, ".msg", msg_settings );
		return 1;
	else
		return 0;
	endif
endfunction

function GetMessageSettings( mobile )

	var msg_settings := GetObjProperty( mobile, ".msg" );
	if( Lower( TypeOf( msg_settings )) != "dictionary" )
		msg_settings := dictionary;
	endif

	return msg_settings;
endfunction
