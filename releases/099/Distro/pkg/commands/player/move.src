/*
        Maintened by *Edwards
        For FantasiaShard.com

        Edwards@FantasiaShard.com

        2010-03-22
        
        Original idea from ZuluHotel
*/
use uo;
use os;

include "include/itemUtil";
include ":containers:storageAreas";

program textcmd_move( mobile, text )

        SendSysMessage( mobile, "Move all of what?" );
        var targ := Target( mobile );
        if( !targ )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        elseif( !useItem_check( mobile, targ, ITEM_INCONTAINER ))
                return 0;
        endif

        SendSysMessage( mobile, "Move "+targ.desc+" where?" );
        var move_to := Target( mobile );
        if( !move_to )
                SendSysMessage( mobile, "Cancelled." );
                return 0;
        elseif( !move_to.IsA( POLCLASS_CONTAINER ))
                SendSysMessage( mobile, "You cannot move "+targ.desc+" there." );
                return 0;
        elseif( targ.container.serial == move_to.serial )
                SendSysMessage( mobile, "You should consider therapy." );
                return 0;
        endif
        
        if( mobile.backpack.serial != move_to.serial )
                if( !useItem_check( mobile, move_to, ITEM_INCONTAINER ))
                        return 0;
                endif
        endif
        
        if( move_to.serial == mobile.serial )
		move_to := mobile.backpack;
        endif

	var targ_objtype := targ.objtype,
	    targ_color := targ.color,
	    targ_name := targ.name,
            num_moved := 0;

        //Allow system to move a lot of items really fast...
        SetObjProperty( mobile, "#FastLooting", 1 );
        
        var loop := CInt( text );
        if( !text )
                loop := CInt( 100 );
        endif
        
	foreach item in CP_ListRootItemsInContainer( targ.container );
 
                SleepMS(10);
                if( item.objtype != targ_objtype )
                        continue;
                elseif( item.color != targ_color )
                        continue;
                elseif( item.name != targ_name )
                        continue;
                elseif( item.newbie )
                        continue;
                elseif( num_moved >= loop )
                        break;
                endif
                
                if( !MoveItemToContainer( item, move_to ))
                        SendSysMessage( mobile, "That container is full!" );
                        return 0;
                else
                        num_moved += 1;
                endif
	endforeach
 
        EraseObjProperty( mobile, "#FastLooting" );

	SendSysMessage( mobile, num_moved+" item(s) moved." );

        return 1;
endprogram
