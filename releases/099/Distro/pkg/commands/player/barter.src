/*
        Created by *Edwards
        
        2010-07-02
*/
use uo;
use os;
use util;

include ":timeutils:gameTime";

const BARTER_DELAY := 11;

program textcommand_Barter( who, text )

        if( GetObjProperty( who, "BarterBAN" ))
                SendSysMessage( who, "You have been banned from barter." );
                return 0;
        endif
        
        var last_barter := GetObjProperty( who, "#BarterMSG" );
        if( who.cmdlevel )
                last_barter := 0;
        endif
        
        if( CInt( last_barter ) > ReadGameClock() )
                var delay_left := CInt( last_barter - ReadGameClock() ),
                    delay_string := TIME_GetTimeDesc( delay_left, 1 );
                    
                SendSysMessage( who, "You have already used this in the past "+BARTER_DELAY+" seconds, please wait "+delay_string+".", 3, 48 );
                return 0;
        else
                SetObjProperty( who, "#BarterMSG", ReadGameClock()+BARTER_DELAY );
        endif
        
        if( !text )
                SendSysMessage( who, "Options are: .barter <message> <help> <on> or <off>" );
		return 0;
        else
                case( Lower( text ))
                "help":     SendSysMessage( who, "off : to toggle off your name on the barter list.", 3, 48 );
                            SendSysMessage( who, "on : to toggle on your name on the barter list.", 3, 48 );
                            return 0;
                "off":      SendSysMessage( who, "You toggle off your name on the barter list.", 3, 48 );
                            SetObjProperty( who, "NoBarter", 1 );
                            return 0;
                "on":       SendSysMessage( who, "You toggle on your name on the barter list.", 3, 48 );
                            EraseObjProperty( who, "NoBarter" );
                            return 0;
                endcase
        endif
        
        if( GetObjProperty( who, "NoBarter" ))
                SendSysMessage( who, "Reminder: you turned barter off.", 3, 48 );
                SendSysMessage( who, "Cancelled.", 3, 42 );
                return 0;
        endif
        
        if( text["www"] || text["http"] || text[".com"] )
                SendSysMessage( who, "Your message is not accepted." );
                return 0;
        endif
        
	foreach player in EnumerateOnlineCharacters()
		if( !GetObjProperty( player, "NoBarter" ))
			SendSysMessage( player, "[Barter] "+who.name+" : "+text, 3, 48 );
                endif

		SleepMS(5);
	endforeach
        
        return 1;
endprogram
