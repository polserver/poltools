/* $Id: SOSMessage.src 87 2006-05-23 19:04:43Z sroyalty $
 *
 */

use uo;
use os;

include ":coords:coords";
include ":itemutils:canAccess";
include ":gumps:include/gumps";

program use_SOSMessage( who, message )

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	if( !can_access(who, message) )
		return 0;
	endif
	if( message.movable == 0 )
		SendSysMessage(who, "You cannot use that.");
		return 0;
	endif
	if( !(message in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessage(who, "That item is not in your backpack.");
		return 0;
	endif

	detach();
        var SOS_Gump := SOS_LoadGump(who, message);
        GFSendGump(who, SOS_Gump);

endprogram


function SOS_LoadGump(who, message)

	var tox := GetObjProperty(message, "tileX");
	var toy := GetObjProperty(message, "tileY");
	var msgindex := GetObjProperty(message, "msgnum");

	if( tox.errortext || toy.errortext)
		syslog("bad message "+tox+" "+toy);
		DestroyItem(message);
		SendSysMessage(who, "The message is garbled beyond your ability to read!");
		return 0;
	endif
	var coords := ToSextant(tox, toy);
	var msgLineOne, msgLineTwo, msgLineThree;
	case (msgindex)
		0: 	msgLineOne   := "Help, our ship is sinking!  We are";
			msgLineTwo   := "at coordinates " + coords + "!  Please";
			msgLineThree := "hurry, we are slipping away fast!";
		1: 	msgLineOne   := "We are under attack by a gigantic";
			msgLineTwo   := "sea serpent!  We are at coordinates";
			msgLineThree :=  + coords + "!  Please help us!";
		2: 	msgLineOne   := "A savage beast has holed our ship.";
			msgLineTwo   := "We are at location " + coords + "!";
			msgLineThree := "We will hang on as long as possible.";
		3: 	msgLineOne   := "We have encountered a terrible storm";
			msgLineTwo   := "at coordinates " + coords + "!";
			msgLineThree := "The ship may capsize soon, hurry!";
	endcase

        var gump := GFCreateGump();
	GFResizePic(gump, 0, 0, 3500, 300, 200);
        GFAddButton(gump, 110, 140, 1148, 1149, GF_CLOSE_BTN, 1);
	GFTextLine(gump, 20, 30, 0, msgLineOne);
	GFTextLine(gump, 20, 50, 0, msgLineTwo);
	GFTextLine(gump, 20, 70, 0, msgLineThree);

	return gump;

endfunction