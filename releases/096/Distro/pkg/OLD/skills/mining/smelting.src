use uo;
use os;

include "include/client";
include ":itemutils:objtype";
include ":itemutils:canAccess";
include ":attributes:attributes";

program smelt_that_ore (who, ore)
  EraseObjProperty(who, "#IsMeditating");
  EraseObjProperty(who, "#HealTimer");
  if(!can_access(who, ore))
    return;
  endif
  if(ore.movable == 0)
    SendSysMessage(who, "You cannot smelt that.");
    return;
  endif
  case(ore.objtype)
    0x6300:  SmeltOre(who, ore, 0x6309,  15);
    0x6301:  SmeltOre(who, ore, 0x630a,  65);
    0x6302:  SmeltOre(who, ore, 0x630b,  70);
    0x6303:  SmeltOre(who, ore, 0x630c,  75);
    0x6304:  SmeltOre(who, ore, 0x630d,  80);
    0x6305:  SmeltOre(who, ore, 0x630e,  85);
    0x6306:  SmeltOre(who, ore, 0x630f,  90);
    0x6307:  SmeltOre(who, ore, 0x6310,  95);
    0x6308:  SmeltOre(who, ore, 0x6311,  99);
    default: SendSysMessage(who,"You cannot smelt that.");
  endcase
endprogram

function SmeltOre(who, ore, ingot, difficulty)
  if(!ReserveItem(ore))
    return;
  endif
  if(!Accessible(who, ore))
    SendSysMessage(who,"You can't reach that.");
    return;
  endif
  var forge := Target(who);
  if((forge.objtype == 0xfb1) || ((forge.objtype >= 0x197a) && (forge.objtype <= 0x19a9)))
    if(!CheckLineOfSight(who, forge))
      SendSysMessage(who,"You can't see that.");
      return;
    endif
    if(Distance(who, forge) > 2)
      SendSysMessage(who,"That is too far away.");
      return;
    endif
    if(Distance(who, ore) > 2)
      SendSysMessage(who,"That is too far away.");
      return;
    endif
    if( SkillCheck(who, MINING, difficulty) > 0 )
      var ingots := CreateItemInBackpack(who, ingot, (GetAmount(ore) * 2));
      ReserveItem(ingots);
      if(ingots)
        if(DestroyItem(ore))
          PlaySoundEffect(forge, 0x22);
          sleep(2);
          SendSysMessage(who,"You create some ingots and place them in your pack.");
	      return;
        else
          DestroyItem(ingots);
        endif
      endif
    else
      SubtractAmount(ore, 1);
	  SendSysMessage(who, "You destroy some ore.");
      return;
    endif
  else
    SendSysMessage(who, "That is not a forge.");
  endif
endfunction
