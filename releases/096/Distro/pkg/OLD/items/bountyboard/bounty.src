// $Id$

use uo;
use os;

include ":bountyboard:bounty";
include ":datafile:datafile";
include ":containers:storageAreas";
include ":gumps:gumps";
include "include/client";


program Assign_Bounty(parms)
	var mobile := parms[1];
	var data := parms[2];
	var bank := CP_GetStorageContainerForMobile(mobile, CP_DEFAULT_AREA, 1);
	foreach id in data
		var holder := FindMyGold(bank);
		var mygold := Cint(holder[1]);
		var goldpiles := holder[2];
		if (mygold)
			var mob := SystemFindObjectBySerial(id, SYSFIND_SEARCH_OFFLINE_MOBILES);
			var bounty := GetBountyAmount(mobile, mygold, mob.name);
			if ((bounty) && (bounty <= mygold))
				AddBounty(mob, bounty);
				foreach thing in goldpiles
					if (thing.amount < bounty)
						bounty := bounty - thing.amount;
						DestroyItem(thing);
					elseif (thing.amount >= bounty)
						SubtractAmount(thing, bounty);
						break;
					endif
					if (bounty <= 0)
						break;
					endif
				endforeach
			elseif (bounty > mygold)
				SendSysMessage(mobile, "You do not have that much gold.", 3, 40);
			else
				SendSysMessage(mobile, "You have declined to place a bounty on " + mob.killer.name + "'s head.");
			endif
		else
			SendSysMessage(mobile, "You have no gold in your bank from which to place a bounty.", 3, 40);
		endif
	endforeach
endprogram


function GetBountyAmount(mobile, mygold, name)

	var gump := GFCreateGump();
	GFResizePic(gump, 50,  50, 5120, 500, 200);
	GFResizePic(gump, 60,  60, 5054, 480, 180);
	GFTextLine(gump, 247,  68, 0, "Bounty Menu");
	GFTextLine(gump, 250,  70, 0x35, "Bounty Menu");
	GFTextLine(gump, 80, 120, 0, "Your bank box contains " + mygold + " gold coins from which you");
	GFTextLine(gump, 80, 140, 0, "may place a bounty on " + name + "'s head.");
	GFTextLine(gump, 80, 160, 0, "Simply enter the bounty amount you wish to place below.");
	GFResizePic(gump, 90, 185, 2620, 120, 30);
	var amt := GFTextEntry(gump, 100, 190, 100, 20, 900, "0");
	GFAddButton(gump, 380, 190, 2121, 2120, GF_CLOSE_BTN, 9998);
	GFAddButton(gump, 440, 190, 2130, 2129, GF_CLOSE_BTN, 1);
	var ret := GFSendGump(mobile, gump);
	if (ret[0] == 1)
		amt := Cint(SplitWords(ret[amt.keyid])[2]);
	else
		SendSysMessage(mobile, "Cancelled.", 3, 40);
		amt := 0;
	endif

	return amt;

endfunction


function FindMyGold(box)

	var amt := 0;
	var piles := array{};

	foreach item in EnumerateItemsInContainer(box)
		if (item.objtype == 0xeed)
			amt := amt + item.amount;
			piles.append(item);
		endif
	endforeach
	var holder := array;
	holder.append(amt);
	holder.append(piles);
	print(holder);

	return holder;

endfunction

