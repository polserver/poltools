/* $Id$
 *
 */

use uo;
use polsys;
use os;

include ":loot:lootParser";

program townChestControl(chest)

   	sleep(15); // Wait for initializers to finish. Helps with any lag.

	var level := CInt(GetObjProperty(chest, "level"));
	var cycle := CInt(GetObjProperty(chest, "duration"));

	if(!level || level.errortext)
		level := GetObjProperty(chest, "level");
		cycle := GetObjProperty(chest, "duration");
  	endif

	if(!level || level.errortext)
		Syslog("TownChest Spawn " + chest.serial + " at: " + chest.x + ", " + chest.y + " is improperly configured");
		return 0;
	endif
	if(!cycle || cycle.errortext)
		Syslog("TownChest Spawn " + chest.serial + " at: " + chest.x + ", " + chest.y + " is improperly configured");
		return 0;
	endif

	// We need to see what TYPE of chest this is. Possible types are
	// Town - Standard weapons/armor.
	// Food - Random storable food.
	// Special - Special items, like dried flowers, etc.
	// Books - Ohhh la la!
	// Rare - Same as special really. Recommend spawning very few of these :)
	// Cloth - Clothing. Maybe for Tailor shops?
	var chest_type := GetObjProperty(chest, "ChestType");
	if (!chest_type || chest_type.errortext)
		chest_type := "Town";
	endif
	CreateLoot(chest, chest_type, level);

	while(chest)
		if(!chest.locked)
			Respawn(chest, level, cycle, chest_type);
		endif
		sleep(cycle);
	endwhile
endprogram


function CreateLoot(chest, chest_type, level)

	// Our naming convention for loot groups is based off ChestTypes you can pass to it.
	// Town Chests named "TownChest1, TownChest2 etc. Type prefixes the Chest<number>.
	var loot_group := "TownChest1";
	case( level )
		1: loot_group := chest_type+"Chest1"; break;
		2: loot_group := chest_type+"Chest2"; break;
		3: loot_group := chest_type+"Chest3"; break;
		4: loot_group := chest_type+"Chest4"; break;
		5: loot_group := chest_type+"Chest5"; break;
	endcase

	Loot_Generate(chest, loot_group);

endfunction

function Respawn(chest, level, cycle, chest_type)
	var ret := GetItemDescriptor(chest.objtype);
	ret.CProps.insert("level", CInt(level));
	ret.CProps.insert("duration", CInt(cycle));
	ret.CProps.insert("ChestType", chest_type);

	var info := struct { x:=chest.x, y:=chest.y, z:=chest.z, descriptor:=ret, realm:=chest.realm };

	DestroyItem(chest);

	Start_Script(":containers:townchest/respawn", info);
endfunction
