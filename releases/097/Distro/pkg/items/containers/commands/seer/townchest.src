/* $Id$
 *
 */

use polsys;
use uo;
use os;
use datafile;

include ":datafile:datafile";
include ":gumps:gumps";
include ":gumps:gumpprompt";
include ":gumps:gumps_ex";
include ":gumps:yesno";
include ":containers:include/townchestGumps";

var tc_data_gumps := DFOpenDataFile(":commands:TownChest", DF_CREATE);

program command_TownChest(mobile, text)


	if (text)
		TC_RefreshGump("Index", "TownChest");
	endif

	var gump := TC_RetrieveGump("Index", "TownChest");
	if  ( !gump )
		gump := TC_Build_Gump();
		TC_SaveGump("Index", gump, "TownChest");
	endif

	var input := GFSendGump(mobile, gump);

	if( input[0] == 100)
		// Get the details for everything
		var level;
		var cycle := CInt(GFExtractData(input, 20))*60;
		var type;
		var chest_objtype;

		foreach key in (input.keys)
			if (key >= 101 && key <= 199)
				level := (key-100);
			elseif (key >= 201 && key <= 299)
				type := TCExtractType(key);
			elseif (key >= 50000)
				chest_objtype := key;
			endif
		endforeach
		// Now we can start to work on placeing it and all.
		var chest_details := GetItemDescriptor(chest_objtype);
		chest_details.CProps.insert("level", CInt(level));
		chest_details.CProps.insert("duration", CInt(cycle));
		chest_details.CProps.insert("ChestType", type);

		var targ := TargetCoordinates(mobile);

		if (!targ || targ.errortext)
			SendSysMessage(mobile, "You decide to place this somewhere else?");
			return 0;
		endif

		var chest := CreateItemAtLocation(targ.x, targ.y, targ.z, chest_details, 1, mobile.realm);
		if( TypeOf(chest) == "ItemRef" )
			if( !YesNo(mobile, "Place chest here?") )
				DestroyItem(chest);
			endif
		endif


	endif

	return 1;

endprogram
