/* $Id$
 *    Author: Unreal
 *    Created: Sunday, May 21 2006
 *    Version: 0.01
 *    Status: Unfinished, Considering removing this and just putting it in the guild manager...hmm
 */

use uo;
use guilds;

include ":guilds:report";
include ":guilds:settings";
include ":guilds:events";
include ":guilds:validate";
include ":guilds:ranks";
include ":gumps:yesno";
include ":gumps:gumps";
include ":gumps:gumps_ex";

CONST GCGUMP_X := 125;
CONST GCGUMP_Y := 200;
CONST TXT_CLR_FG := 0;
CONST TXT_CLR_BG := 2100;

var background := GFGetCfgConst("Defaults", "BackGround");
var foreground := GFGetCfgConst("BackGrounds", "WHITE_PAPER");
var settings := Guild_GetSettingsCfgElem("Settings");

program Guild_Create(who)

	var gump := GFCreateGump(GCGUMP_X, GCGUMP_Y);

	GFResizePic(gump, 0, 0, background, 500, 300);
	GFResizePic(gump, 25, 20, foreground, 450, 25);
	GFTextLine(gump, 200, 20, TXT_CLR_FG, "Guild Creation");

	GFTextLine(gump, 25, 60, TXT_CLR_BG, "As you are not a member of any guild, you can create your own by");
	GFTextLine(gump, 25, 80, TXT_CLR_BG, "providing a unique guild name and paying the standard guild registration");
	GFTextLine(gump, 25, 100, TXT_CLR_BG, "fee.");

	var regist_fee := CInt(settings.RegistrationFee);

	GFTextLine(gump, 25, 140, TXT_CLR_BG, "Registration Fee:");
	GFResizePic(gump, 150, 140, foreground, 200, 25);
	GFTextLine(gump, 160, 140, TXT_CLR_FG, regist_fee);

	GFTextLine(gump, 25, 165, TXT_CLR_BG, "Guild Name:");
	GFResizePic(gump, 150, 165, foreground, 200, 25);
	GFTextEntry(gump, 160, 165, 200, 20, TXT_CLR_FG, "", 3);

	GFTextLine(gump, 25, 190, TXT_CLR_BG, "Guild Abbreviation:");
	GFResizePic(gump, 150, 190, foreground, 200, 25);
	GFTextEntry(gump, 160, 190, 200, 20, TXT_CLR_FG, "", 4);

	GFAddButton(gump, 25, 250, 2128, 2129, GF_CLOSE_BTN, 1); // Okay
	GFAddButton(gump, 100, 250, 2119, 2120, GF_CLOSE_BTN, 2); // Cancel

	var guild_invites := GetObjProperty(who, "Guild_Invites");

	if ( !guild_invites )
		SetObjProperty(who, "Guild_Invites", "ON");
		guild_invites := GetObjProperty(who, "Guild_Invites");
	endif

	GFTextLine(gump, 325, 250, TXT_CLR_BG, "Guild Invites:");
	case ( guild_invites )
		"ON":
			GFTextLine(gump, 405, 250, TXT_CLR_BG+1, "Allow");
			break;
		"OFF":
			GFTextLine(gump, 405, 250, TXT_CLR_BG+1, "Ignore");
			break;
		default:
			GFTextLine(gump, 405, 250, TXT_CLR_BG+1, "Allow");
			break;
	endcase
	GFAddButton(gump, 305, 253, 2117, 2118, GF_CLOSE_BTN, 10);

	var result := GFSendGump(who, gump);

	if ( result[1] == 1 ) // clicked Okay
		var i, x;
		x := Len(result[3]);
		i := result[3];
		var guild_name := i[4, (x-3)];

		if ( !guild_name )
			SendSysMessage(who, "You must provide a guild name.");
			return 0;
		endif

		x := Len(result[4]);
		i := result[4];
		var guild_abbr := i[4, (x-3)];

		if ( !guild_abbr )
			SendSysMessage(who, "You must provide a guild abbreviation.");
			return 0;
		endif

		AddGuild(who, guild_abbr, guild_name);
		return 1;
	elseif ( result[2] == 1 ) // clicked Cancel
		SendSysMessage(who, "Aborted.");
		return 0;
	elseif ( result[10] == 1 )
		case ( guild_invites )
			"ON":
				if ( SetObjProperty(who, "Guild_Invites", "OFF") )
					SendSysMessage(who, "Guild invites will now be ignored.");
					return 1;
				endif
				break;
			"OFF":
				if ( SetObjProperty(who, "Guild_Invites", "ON") )
					SendSysMessage(who, "Guild invites will not be ignored.");
					return 1;
				endif
				break;
		endcase
		return 0;
	else
		SendSysMessage(who, "Aborted.");
		return 0;
	endif

endprogram

function AddGuild(guild_gm, guild_abbr, guild_name)

	var regist_fee := CInt(settings.RegistrationFee);
	if ( regist_fee == 0 || !regist_fee )
		SendSysMessage(guild_gm, "Aborted. No registration fee is set or not found.");
		return 0;
	endif

	if ( guild_gm.gold < regist_fee )
		SendSysMessage(guild_gm, "Aborted. You do not have enough gold to cover the fee.");
		return 0;
	endif

	if ( Len(guild_name) > CInt(settings.MaxNameLen) )
		SendSysMessage(guild_gm, "Aborted. Guild name is too long.");
		return 0;
	elseif ( !ValidStr(guild_name) )
		SendSysMessage(guild_gm, "Aborted. Guild name contains illegal characters.");
		return 0;
	elseif ( !ValidName(guild_name) )
		SendSysMessage(guild_gm, "Aborted. Guild name already exists.");
		return 0;
	endif

	if ( Len(guild_abbr) > CInt(settings.MaxAbbrLen) )
		SendSysMessage(guild_gm, "Aborted. Guild abbreviation is too long.");
		return 0;
	elseif ( !ValidStr(guild_abbr) )
		SendSysMessage(guild_gm, "Aborted. Guild abbreviation contains illegal characters.");
		return 0;
	elseif ( !ValidName(guild_abbr) )
		SendSysMessage(guild_gm, "Aborted. Guild abbreviation already exists.");
		return 0;
	endif

	if ( !YesNo(guild_gm, "Are you sure you want to create a new guild?", "Yes", "No") )
		SendSysMessage(guild_gm, "Aborted.");
		return 0;
	else
		if ( !guild_gm.SpendGold(regist_fee) )
			SendSysMessage(guild_gm, "Aborted. You do not have enough gold to cover the fee.");
			return 0;
		endif
	endif

	var guild := CreateGuild();

	if ( guild )
		var guild_id := guild.guildid;
		var null_space := "NONE";
		guild.SetProp("Name", guild_name);
		guild.SetProp("Abbr",  guild_abbr);
		guild.SetProp("Master", guild_gm.serial);
		guild.SetProp("Charter", null_space);
		guild.SetProp("Website", null_space);
		guild.SetProp("Faction", "Neutral");
		guild.AddMember(guild_gm);
		SetObjProperty(guild_gm, "Guild_ID", guild_id); // Should I just remove this? There is who.guildid....
		SetGuildRank(guild_gm, 5);
		SetObjProperty(guild_gm, "Fealty", guild_gm.serial);
		SendSysMessage(guild_gm, "Guild was successfully created.");
		return 1;
	elseif ( guild.errortext )
		SendSysMessage(guild_gm, "Aborted. Guild creation failed.");
		SysLog("Error creating guild -> ["+guild.errortext+"] + CharSerial -> ["+guild_gm.serial+"]");
		return 0;
	endif

endfunction