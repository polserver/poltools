/* $Id: SOSBottle.src 87 2006-05-23 19:04:43Z sroyalty $
 *
 */

use uo;
use cfgfile;
use os;
use util;

include ":itemutils:canAccess";

program use_MessageBottle( who, bottle )

	if( !can_access(who, bottle) )
		return 0;
	endif
	if( !(bottle in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessage(who, "That item is not in your backpack.");
		return 0;
	endif
	if( bottle.movable == 0 )
		SendSysMessage(who, "You cannot use that.");
		return 0;
	endif
	if( !ReserveItem(bottle) )
		return 0;
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	var cfg := ReadConfigFile("sosarea");
	if (!cfg)
		Syslog("Could not read sosarea.cfg");
		SendSysMessage(who, "Error with SOSArea Config file. Please contact an admin!");
		return 0;
	endif

	var maxi := GetConfigMaxIntKey(cfg);
	var elem := cfg[RandomInt(maxi)+1];
	var X := CInt(elem.x)+RandomInt(CInt(elem.range)*2) - CInt(elem.range);
	var Y := CInt(elem.y)+RandomInt(CInt(elem.range)*2) - CInt(elem.range);
	UnloadConfigFile("sosarea");
	SetObjProperty(bottle, "tileX", X);
	SetObjProperty(bottle, "tileY", Y);
	var message := CreateItemInContainer(who.backpack, 0xa363);
	if( !message )
		DestroyItem(bottle);
		SendSysMessage(who, "You cannot make out the writing on the message!");
		return 0;
	endif
	SetObjProperty(message, "tileX", X);
	SetObjProperty(message, "tileY", Y);
	SetObjProperty(message, "msgnum", RandomInt(4));
	SendSysMessage(who, "You have extracted a message from the bottle!");
	DestroyItem(bottle);

endprogram