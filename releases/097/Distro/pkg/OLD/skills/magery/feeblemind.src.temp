use uo;
use os;
use util;
use cfgfile;

include "include/statMod";
include ":attributes:attributes";
include "include/sound";
include "include/tileEffects";
include ":magery:reflect";
include "include/magic";


program spell_feeblemind(parms)
  var caster := parms.caster;
  var castOn := parms.castOn;
  var spellid := parms.spellid;
  var spellcfg := ReadConfigFile(":*:spells");
  var circle := spellcfg[spellid].circle;

  SetObjProperty(castOn, "LastHit", {caster.name,caster.serial, "feeblemind" });
  if (spell_reflect(castOn))
	castOn := caster;
  endif
  PlaySoundEffect(castOn, SFX_SPELL_FEEBLEMIND);
  PlayObjectCenteredEffect( castOn, FX_CURSE_EFFECT, 7, 7 );
  if (!CanMod(castOn, "int"))
	SendSysMessage(caster,"Already under the influence!");
	return;
  endif
  var magery := AP_GetSkill(caster, MAGERY);
  var modAmount := Resisted(circle,caster,castOn,(GetModAmount( magery )));
  var duration := GetModDuration( magery );
  DoTempMod(castOn, "int", 0 - modAmount, duration);
endprogram