/* $Id$
 *
 */

use uo;
use os;
use util;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcSounds";

program BrainNerve(params)
	var npc		:= params[1];
	var nerve_name	:= params[2];
	//var settings	:= params[3];
	//var scripts	:= params[4];
	params := 0; // Not needed anymore.

	// Shut down appropriate nerves.
	foreach nerve in (LoadNerves(npc))
		if ( nerve.flags & NF_CONTINUE_ON_SLEEP )
			continue;
		elseif ( _nerve_iter != nerve_name )
			StopNerve(npc, _nerve_iter);
		endif
		SleepMS(2);
	endforeach

	while ( npc )
		if ( npc.graphic == 223 )
			// Detected the sheep has no wool.
			// See if there is a timer already on the sheep to regrow
			if ( !CInt(GetObjproperty(npc, "#RegrowWool")) )
				// No timer set yet. 
				// Sheep can regrow wool after 5 minutes has passed.
				SetObjProperty(npc, "#RegrowWool", POLCore().systime+300);
			elseif ( CInt(GetObjProperty(npc, "#RegrowWool")) < POLCore().systime )
				// Timer has passed, give the sheep wool again.
				npc.graphic := 207;
				EraseObjProperty(npc, "#RegrowWool");
			endif
		endif

		Sleep( 30+RandomInt(10) );

		var my_idle_sound := NPC_GetIdleSound(npc);
		if ( my_idle_sound )
			PlaySoundEffect(npc, my_idle_sound);
		endif

		Sleep(20+RandomInt(10));

		npc.facing := RandomInt(8);
		AI_Wander(npc, NOWAKE);

		AI_ClearThoughts(npc, CLR_NERVE);
	endwhile
endprogram
