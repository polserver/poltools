/* $Id$
 *
 */

use uo;
use util;
use cfgfile;

include ":begging:begging";
include ":brainAI:npcUtil";
include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";

CONST MINKARMA	:= 4000;
CONST MINFAME	:= 4000;
CONST UOBJ_GOLDCOIN :=0xEED;

program skill_Begging( who )

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	// Human and Elf both.
	var MOB_APPROVED  := { 400, 401, 605, 606 };
	SendSysMessage( who, "Select the person you wish to beg to." );
	var beggee := Target( who );
	if ( !beggee )
		SendSysMessage( who, "Changed your mind?" );
		return 0;
  	endif
  	if( beggee == who )
  		SendSysMessage(who, "You might want to consider therapy.");
  		return 0;
	endif
	if( (!beggee.isa(POLCLASS_NPC)) )
		SendSysMessage(who, "You would better odds begging from a starving rat.");
		return 0;
	endif
	if( !(beggee.graphic in MOB_APPROVED) && beggee.isa(POLCLASS_NPC) )
		SendSysMessage(who, "I think they would rather eat you for dinner.");
		return 0;
	endif
	if( beggee.isA(POLCLASS_ITEM) )
		SendSysMessage(who, "I think you need to talk to a therapist.");
		return 0;
	endif
	if( TypeOf(beggee.acctname) == "String" )
		SendSysMessage(who, "Perhaps talking to them would be a better idea.");
		return 0;
	endif

	PrintTextAbove(who, "*looks at " + beggee.name + "*" );

	PrintBeggingMessage( who, Pleade);
	sleep(1);

	var karma := GetObjProperty(who, "Karma");
	if( !karma )
		karma := 0;
		SetObjProperty(who, "Karma", karma);
	endif
	if( karma < 0 )
		PrintBeggingMessage( beggee, NegKarma );
		return 0;
	endif

	var bTemplate := NPC_GetNPCConfig(beggee.npctemplate);
	var wealth := Lower(bTemplate.wealth);
	if( !wealth )
		PrintBeggingMessage(beggee, Broke );
		return 0;
	endif
	var lb := CInt(GetObjProperty(beggee, "begpurse"));
	if( lb < 10 || lb.errortext )
		PrintBeggingMessage(beggee, Broke );
		return 0;
	endif

	if( SkillCheck(who, BEGGING, -1) > 0 )
		var gold;
		case (wealth)
			"upper":	gold := RandomDiceRoll("4d5");
			"middle":	gold := RandomDiceRoll("3d4");
			"poor":		gold := RandomDiceRoll("2d3");
			"broke":
			default:	PrintBeggingMessage( beggee, Broke );
					return 0;
			"none":		PrintBeggingMessage( beggee , None );
					return 0;
		endcase
		if( (GetObjProperty(who, "Karma") > MINKARMA) and (GetObjProperty(who, "Fame") > MINFAME) )
			gold := CInt(gold*1.3);
		endif
		if ( lb < gold )
			gold := lb;
		endif
		lb := Cint(lb - gold);
		SetObjProperty(beggee, "begpurse", lb);
		PrintTextAbove( beggee, "*throws a few coins to " + who.name + "*" );
		if( !CreateItemInBackpack(who, UOBJ_GOLDCOIN, gold) )
			SendSysMessage( who, "You miss and the coins bounce into cracks in the ground!" );
		else
			var coin := "coins";
			if (gold == 1)
				coin := "coin";
			endif
			SendSysMessage( who, "You quickly stuff " + gold + " " + coin + " in your pack!" );
		endif
		if (GetObjProperty(who, "Karma") > 10)
			SendSysMessage(who, "You lose a little karma.");
			SetObjProperty(who, "Karma", GetObjProperty(who, "Karma")-10);
		endif
		return 1;
	else
		PrintBeggingMessage( beggee, Failed );
		return 0;
	endif


endprogram