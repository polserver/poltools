// $Id$

use uo;
use os;

include ":magery:spells";
include ":magery:fields";
include ":attributes:attributes";
include "include/facings";

program SpellScript_PoisonField(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;
	
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);
	
	// Duration: (3 + (Magery * 0.4)) seconds
	var duration := CInt(3 + AP_GetSkill(mobile, MAGERY) * 0.4);
	
	// Level is the same as Poison spell
	var poison_level := MS_GetPoisonLevel(mobile, targ);
	var area_size := GetConfigInt(info.spell_config, "AreaSize");
	var prop_struct := struct{ "PoisonLevel" := poison_level, "SummonedField" := 1 };
	if ( mobile.facing == 1 || mobile.facing == 2 || mobile.facing == 5 || mobile.facing == 6 )
		CreateField(targ, FIELD_SHAPE_HORIZONTAL, "PoisonField_NS", area_size, duration, prop_struct, mobile, 1);
	else
		CreateField(targ, FIELD_SHAPE_VERTICAL, "PoisonField_EW", area_size, duration, prop_struct, mobile, 1);
	endif
	
	return 1;
endprogram
