/*
 * $Id$
 *
 */

use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";

program SpellScript_GreaterHeal(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;
	
	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif
	
	// Amount healed: 40% Magery + (1 to 10)
	var points := CInt(AP_GetSkill(mobile, MAGERY) * 0.4) + RandomDiceRoll("1d10");
	
	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);

	var healed := AP_GetVital(targ, HITS);
	HealDamage(targ, points);
	healed := AP_GetVital(targ, HITS) - healed;
	
	SendSysMessage(mobile, "You healed "+healed+" hit points of damage.");
	
	if ( mobile.serial != targ.serial )
		SendSysMessage(targ, "You have had "+healed+" hit points of damage healed.");
	endif
	
	return 1;
endprogram
