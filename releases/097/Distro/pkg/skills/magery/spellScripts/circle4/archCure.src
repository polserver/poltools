// $Id$

use uo;
use os;

include ":magery:spells";
include ":attributes:attributes";
include ":timedScripts:poisonUtil";

program SpellScript_ArchCure(params)
	var mobile := params[1];
	var info := params[2];
	params := 0;

	MS_PlaySpellSFX(info.spell_id, mobile);

	// TODO: Get non-hostile blues instead of people just in party
	// Get all targets in area that are in party.
	var area_size := GetConfigInt(info.spell_config, "AreaSize");
	var includes := GetObjProperty(mobile, "#Party");
	if ( includes == error )
			includes := array{mobile.serial};
	else
			includes.Append(mobile.serial);
	endif

	var friendlies := MS_GetFriendlies(mobile, area_size);
	if ( friendlies != error )
			includes += friendlies;
	endif
	
	var targs := MS_GetValidTargets(mobile, area_size, 1, "", includes);
	foreach vtarg in targs
		// Get poison level
		var poison_names := TS_GetTimerNames(vtarg, "P");
		var poison_level := 1;
		
		foreach timer_name in ( poison_names )
			var timer_info := TS_GetTimerInfo(vtarg, timer_name);
			if ( timer_info.level > poison_level )
					poison_level := timer_info.level;
			endif
		endforeach
	
		//% Chance to cure: (Magery * 1.42) + (110 - (Poison level * 33)) (87% chance for level 5 poison at GM magery)
		var chance := CInt(AP_GetSkill(mobile, MAGERY) * 1.42) + (110 - (poison_level * 33));
		if ( chance >= RandomInt(100) + 1 )
			// NOTE: There is still a 1 in 100 chance that this wont work..
			TS_CurePoison(vtarg, 10000, "P", poison_level);
		endif
		
		MS_PlaySpellGFX(info.spell_id, vtarg, mobile);
	endforeach

	return 1;
endprogram
