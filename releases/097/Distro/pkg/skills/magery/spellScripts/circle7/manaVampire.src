use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";

program SpellScript_ManaVampire(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	if ( mobile.serial == targ.serial )
		SendSysMessage(mobile, "You cannot mana vampire yourself!");
		return 0;
	endif
	

	// Mana drained = caster's eval - target's resist
	var mana_drained := AP_GetSkill(mobile, EVALUATING_INTELLIGENCE) - AP_GetSkill(targ, RESISTING_SPELLS);
	var targ_mana := AP_GetVital(targ, MANA);

	// Can not drain more mana than the target has
	if ( mana_drained > targ_mana )
		mana_drained := targ_mana;
	endif
	
	AP_ConsumeVital(targ, MANA, mana_drained);
	
	// Can not go above vital cap on mobile
	var mobile_mana := AP_GetVital(mobile, MANA);
	var max_mana := AP_GetVitalMaximumValue(mobile, MANA);

	if ( mana_drained + mobile_mana > max_mana )
		mana_drained := max_mana - mobile_mana;
	endif
	
	// You never know...
	if ( mana_drained < 0 )
		mana_drained := 0;
	endif
	
	AP_SetVital(mobile, MANA, mobile_mana + mana_drained);

	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);	
	
	return 1;
endprogram