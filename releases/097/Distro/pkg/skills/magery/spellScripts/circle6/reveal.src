use uo;
use os;

include "include/client";
include "include/sounds";
include ":magery:spells";
include ":magery:spellSounds";
include ":attributes:attributes";

program SpellScript_Reveal(params)
	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
	params := 0;

	if ( !MS_MobileCheck(mobile, targ) )
		return 0;
	endif

	MS_PlaySpellSFX(info.spell_id, targ);
	MS_PlaySpellGFX(info.spell_id, targ, mobile);

	var area_size := CInt(GetConfigInt(info.spell_config, "AreaSize"));
	var targs := ListMobilesNearLocationEx(targ.x, targ.y, targ.z, area_size, LISTEX_FLAG_HIDDEN, targ.realm);

	foreach targ in targs
		if ( GetObjProperty(targ, "Invisibile") )
			targ.hidden := 0;
		else
			// I just made these up. uo.stratics and uo.com didn't give any good info.
			var caster_skill := AP_GetSkill(mobile, MAGERY) + AP_GetSkill(mobile, DETECTING_HIDDEN);
			var hider_skill := AP_GetSkill(targ, HIDING) + AP_GetSkill(targ, STEALTH);
			if ( caster_skill >= hider_skill )
				targ.hidden := 0;
			endif
		endif
	endforeach
	
	return 1;
endprogram
