/* $Id
 *
 */
use uo;
use os;
use cfgfile;

include ":containers:containers";
include ":gumps:yesno";
include ":keys:key";

const UOBJECT_DOORS_START	:= 0x0675;
const UOBJECT_DOORS_END		:= 0x06f4;
const CANCEL_ID			:= 9999;

program place_deed(who, deed)

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	if( (!deed.movable) || !ReserveItem(deed) )
		SendSysMessage(who, "You cannot use that");
		return 0;
	elseif( !(deed in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessage(who, "That item is not in your backpack.");
		return 0;
	endif
	
	var item_cfg := ReadConfigFile(":carpentry:itemdesc");
	var element  := FindConfigElem(item_cfg, deed.objtype);

	if(!element)
		SendSysMessage(who, "You can't make anything out of this deed.");
		return 0;
	endif

	var items := SplitWords(element.ObjList);
	if( !len(items) )
		SendSysMessage(who, "You can't make anything out of this deed.");
		return 0;
	endif

	SendSysMessage(who, "Where do you want to create the item?");
	var start := TargetCoordinates(who);
	if( !start )
		SendSysMessage(who, "Cancelled");
		return 0;
	endif
	if( !CheckLosAt(who, CInt(start.x), CInt(start.y), CInt(start.z)) )
		SendSysMessage(who, "You can't see that.");
		return 0;
	endif

	var in_house := element.InHouse;
	if((in_house) && (who.cmdlevel < 2))
		if(!who.multi)
			SendSysMessage(who, "You have to be in a house to place this item.");
			return 0;
		endif
		if(GetObjProperty(who.multi, "ownerserial") != who.serial)
			var house := who.multi;
			var sign;
			foreach thing in (house.components)
				if((thing.objtype == 0x0bd2) || (thing.objtype == 0x0bd0))
					sign := thing;
					break;
				endif
			endforeach
			var co_owners := GetObjProperty(house, "coownlist");
			if(!(who.serial in co_owners))
				SendSysMessage(who, "You can only place this item if you own or co-own the house!");
				return 0;
			endif
		endif
	endif

	var movable	:= element.ObjMovable;
	var created	:= {};
	var the_serials	:= {};
// Need a better check than setting a cprop for exceptional. Named based
// would be best.
	var is_except  := GetObjProperty(deed, "Exceptional");

	var i, failed;
	var x_mods := SplitWords(element.ObjXMod);
	var y_mods := SplitWords(element.ObjYMod);
	var z_mods := SplitWords(element.ObjZMod);

	for(i := 1; i <= len(items); i := i + 1)
		var obj   := CInt( items[i]);
		var x_mod := CInt(x_mods[i]);
		var y_mod := CInt(y_mods[i]);
		var z_mod := CInt(z_mods[i]);

		var the_item := CreateItemAtLocation(start.x + x_mod, start.y + y_mod, start.z + z_mod, obj, 1);
		if(!the_item || the_item.errortext)
			foreach thing in created
				DestroyItem(thing);
			endforeach
			DestroyItem(the_item);
			failed := 1;
			break;
		endif

		the_item.decayAt := 0;

		if((the_item.multi).serial != (who.multi).serial)
			foreach thing in created
				DestroyItem(thing);
			endforeach
			DestroyItem(the_item);
			failed := 2;
			break;
		endif
		the_item.color   := deed.color;
		if(is_except)
			SetObjProperty(the_item, "Exceptional", 1);
		endif
		if(!movable)
			the_item.movable := 0;
		else
			the_item.movable := 1;
		endif

		if(KP_IsLockable(the_item))
			var lockid := CreateLockID();
			the_item.locked := 1;
			var thekey := CreateItemInBackpack(who, 0x100E);
			SetObjProperty(thekey,  "LockId", lockid);
			SetObjProperty(the_item, "LockId", lockid);
			SendSysMessage(who, "You will find the key in your backpack.");
			created.append(thekey);
			the_serials.append(thekey.serial);
		endif
		created.append(the_item);
		the_serials.append(the_item.serial);
		sleepms(2);
	endfor

	if(failed == 1)
		SendSysMessage(who, "Could not build that there");
		return 0;
	elseif(failed == 2)
		SendSysMessage(who, "You may only place this in a house you own");
		return 0;
	endif

	var deed_objtype:= deed.objtype;
	var deed_color	:= deed.color;
	var deed_name	:= deed.desc;
	if(YesNo(who, "Placed well?"))
		if(DestroyItem(deed))
			foreach part in created
				if(part.objtype != 0x100E)
					SetObjProperty(part, "BaseName", deed_name);
					SetObjProperty(part, "DeedOwnerSerial", who.serial);
					SetObjProperty(part, "ItemsCreatedSerials", the_serials);
					SetObjProperty(part, "FromDeed", deed_objtype);
					SetObjProperty(part, "DeedColor", deed_color);
					SetObjProperty(part, "furniture", 1);
				endif
			endforeach
		endif
	else
		foreach part in created
			DestroyItem(part);
		endforeach
		SendSysMessage(who, "Cancelled.");
	endif

	return 1;
	
endprogram