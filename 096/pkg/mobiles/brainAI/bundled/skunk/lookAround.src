use uo;
use os;
use util;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcWatchList";

program BrainNerve(params)
	var npc		:= params[1];
	//var nerve_name	:= params[2];
	var settings	:= params[3];
	var scripts	:= params[4];
	params := 0; // Not needed anymore.
	
	while ( npc )
		if ( !npc.opponent )
			if ( NPC_LoadWatchList(npc).size() < 1 )
				// No need to look around any longer. We're all alone :(
				return 0;
			else
				FindOpponent(npc, settings, scripts);
			endif
		endif
		
		AI_ClearThoughts(npc, CLR_NERVE);
		Sleep(5);
	endwhile
endprogram

function FindOpponent(npc, byref settings, byref scripts)
	var closest := 0;
	foreach mobile in ListMobilesInLineOfSight(npc, CInt(settings["AreaSize"]))
		if ( NPC_ShouldWatch(npc, mobile, settings, scripts) )
			if ( !closest )
				closest := mobile;
			elseif ( Distance(npc, mobile) < Distance(npc, closest) )
				closest := mobile;
			endif
		endif
		SleepMS(2);
	endforeach
	
	if ( closest )
		AI_Attack(npc, closest);
		return closest;
	endif
	
	return 0;
endfunction