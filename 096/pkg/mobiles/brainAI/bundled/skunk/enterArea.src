use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:npcWatchList";

program BrainNerve(params)
	var npc		:= params[1];
	//var nerve_name	:= params[2];
	var event	:= params[3];
	var settings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.
	
	while ( npc )
		if ( event )
			case ( event.type )
				SYSEVENT_ENTEREDAREA:
					EnteredArea(npc, event, settings, scripts);
					break;
				default:
					break;
			endcase
			
			event := 0;
		endif
		
		SleepMS(5);
		event := Wait_For_Event(600);
	endwhile
	
	return 1;
endprogram

function EnteredArea(npc, byref event, byref settings, byref scripts)
	var source := event.source;
	
	if ( NPC_ShouldWatch(npc, source, settings, scripts) )
		if ( NPC_AddToWatchList(npc, source) )
			if ( NPC_LoadWatchList(npc).Size() == 1 ) 
				// First time someone approached
				AI_Turn(npc, source, NETURN_AWAY);
				AI_Speak(npc, "*stands up on its front legs in a defensive manner*");
			endif
			
			AI_WakeUp(npc);
			
			if ( scripts["LookAround"] )
				if ( !GetNerveProcess(npc, "LookAround") )
					AI_WakeUp(npc);
				endif
			endif
		endif
	endif
		
	return 1;
endfunction
