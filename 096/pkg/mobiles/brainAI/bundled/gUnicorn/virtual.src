use uo;
use os;
use math;
use util;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include "include/client";
include "include/facings";
include "include/shapes";

CONST FIREWORK_RANGE	:= 4;

program BrainNerve(params)
	var npc		:= params[1];
	//var nerve_name:= params[2];
	var event	:= params[3];
	//var settings	:= params[4];
	//var scripts	:= params[5];
	params := 0; // Not needed anymore.

	while ( npc )
		if ( event )
			if ( event.source )
				SparkleEffect(npc);
			else
				SomethingLame(npc);
			endif
		endif
		
		event := Wait_For_Event(900000);
	endwhile
endprogram

function SomethingLame(npc)
	var effect := RandomInt(7);
	case ( effect )
		0: 1: 5:
			SaySomething(npc);
			break;
		2: 4:
			RainbowStuff(npc);
			break;
		3: 6:
			FartFireworks(npc);
			break;
		default:
			break;
	endcase

	return 1;
endfunction

function RainbowStuff(npc)
	var orig_color := npc.color;
	var speech_color := npc.speech_color;
	
	npc.speech_color := 32;
	npc.color := 32;
	AI_Speak(npc, "RED!");
	SleepMS(600);

	npc.speech_color := 44;
	npc.color := 44;
	AI_Speak(npc, "ORANGE!");
	SleepMS(600);

	npc.speech_color := 53;
	npc.color := 53;
	AI_Speak(npc, "YELLOW!");
	SleepMS(600);

	npc.speech_color := 462;
	npc.color := 462;
	AI_Speak(npc, "GREEN!");
	SleepMS(600);

	npc.speech_color := 3;
	npc.color := 3;
	AI_Speak(npc, "BLUE!");
	SleepMS(600);

	npc.speech_color := 818;
	npc.color := 818;
	AI_Speak(npc, "INDIGO!");
	SleepMS(600);

	npc.speech_color := 223;
	npc.color := 223;
	AI_Speak(npc, "VIOLET!");
	SleepMS(700);

	npc.speech_color := speech_color;
	npc.color := orig_color;
	AI_Speak(npc, "UNICORN POWER!");
	SparkleEffect(npc);
	return 1;
endfunction

function FartFireworks(npc)
	npc.frozen := 1;
	AI_Speak(npc, "Oh .. god.. what did Rowan put in there?!");
	Sleep(1);
	AI_Speak(npc, "*lifts its tail*");
	Sleep(1);
	AI_Speak(npc, "*nnngggffffffff*");
	
	var behind := Abs((npc.facing+4)%8);
	var coords := GetPointsByFacing(npc.x, npc.y, behind, FIREWORK_RANGE+2);
	var coord_a := coords[1]; // First coordinate
	var coord_z := coords[coords.Size()]; // Last coordinate
	
	PlaySoundEffect(npc, 351);
	SleepMS(30);
	PlayMovingEffectXYZ(coord_a.x, coord_a.y, npc.z, coord_z.x, coord_z.y, npc.z+5, GFX_WAVY_FIREBALL, 10, 10, 0, npc.realm);
	SleepMS(400);
	PlayStationaryEffect(coord_z.x, coord_z.y, npc.z+5, GFX_EXPLODE_3, 10, 10, 0, npc.realm);
	foreach coordinate in ( GetCoordsInSquare(coord_z.x, coord_z.y, FIREWORK_RANGE-2) )
		PlayStationaryEffect(coordinate.x, coordinate.y, npc.z+5, RandomSparkle(), 15, 15, 0, npc.realm);
		SleepMS(2);
	endforeach
	
	npc.frozen := 0;
	return 1;
endfunction

function SparkleEffect(npc)
	PlayObjectCenteredEffect(npc, GFX_BLUE_SPARKLES, 10, 20);
	SleepMS(30);
	PlayObjectCenteredEffect(npc, GFX_BLUE_SPARKLE_SWIRL, 10, 20);
	SleepMS(30);
	PlayObjectCenteredEffect(npc, GFX_BLUE_SPARKLE_BORDER, 10, 20);
	SleepMS(30);
	PlayObjectCenteredEffect(npc, GFX_BLUE_SPARKLE_GROUND, 10, 20);

	return 1;
endfunction

function SaySomething(npc)
	var speech_entries := array
	{
		"Most of my scripted personality is based off of a guy named Rowan in IRC!",
		"Chicks dig my phallical unit!",
		"On a scale of 1-10 for my flamboyance, I am Richard Simmons",
		"Im so sparkly!",
		"Unicorn power!",
		"I love the worj!"
	};
	
	var text := speech_entries[RandomInt(speech_entries.Size())+1];
	
	AI_Speak(npc, text, SPEAK_TEXTTYPE_DEFAULT, SPEAK_DOEVENT_ENABLE, WAKEUP);
	
	return 1;
endfunction

function RandomSparkle()
	var sparkles := array{
	GFX_MAGICAL_SPARKLES_EW,
	GFX_MAGICAL_SPARKLES_NS,
	GFX_BLUE_SPARKLES,
	GFX_RED_SPARKLES,
	GFX_BLUE_SPARKLE_SWIRL,
	GFX_BLUE_SPARKLE_BORDER,
	GFX_BLUE_SPARKLE_GROUND
	};
	
	return sparkles[RandomInt(sparkles.Size())+1];
endfunction