/* $Id$
 *
 */
use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";

program BrainNerve(params)
	var npc		:= params[1];
	var nerve_name	:= params[2];
	var event	:= params[3];
	var settings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.
	
	var opponent := event.source;

	while ( npc )
		var dist := Distance(npc, opponent);
		if ( DoneFighting(npc, opponent, dist) )
			if ( scripts.Exists("EndFight") )
				params := array{opponent, settings, scripts};
				AI_StartNerve(npc, "EndFight", scripts["EndFight"].script, params);
			else
				AI_WarMode(npc, 0);
			endif
			AI_EndNerve(npc, nerve_name);
		else
			AI_Move(npc, opponent, NEMOVE_AWAY, NEMOVE_RUN, WAKEUP, 100);
		endif
		
		sleepms(400);
		While ( Events_Waiting() )
			CheckForEvent(npc, event);
		endwhile
	endwhile
endprogram

function DoneFighting(npc, opponent, byref dist)
	if ( opponent.dead )
		return 1;
	elseif ( !opponent )
		return 1;
	elseif ( opponent.hidden )
		return 1;
	elseif ( opponent.concealed > npc.cmdlevel )
		return 1;
	elseif ( dist > 20 )
		return 1;
	elseif ( dist > 10 && !CheckLineOfSight(npc, opponent) )
		return 1;
	endif

	return 0;
endfunction

// Need more stuff here, should have events checked for peacemaking
// and provocation.
function CheckForEvent(npc, byref event)
	// Temp added for stopping ecompile spam
	npc := npc;
	event := wait_for_event(0);
	if ( event )
		var source := event.source;
		case ( event.type )
			SYSEVENT_ENGAGED: source := source; // Added temp for stopping ecompile spam
				break;
			SYSEVENT_DAMAGED:
				break;
			default:
				break;
		endcase
		event := 0;
	endif

	return 1;
endfunction
