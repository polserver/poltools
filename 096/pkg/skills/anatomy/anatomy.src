/* $Id: anatomy.src 834 2005-11-02 14:09:50Z austin $
 *
 */
use uo;

include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";


program skill_Anatomy(who)

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	SendSysMessage(who, "Whom shall I examine?");
	var subject := Target(who);
	// target checks begin here
	if(!subject)
		return 0;
	elseif(subject.serial == who.serial)
		PrintTextAbovePrivate(who, "You know yourself quite well enough already.", who);
		return 0;
	elseif(subject.graphic == 0x2006)
		SendSysMessage(who, "Looks pretty dead to you.");
		return 0;
	elseif(!subject.isA(POLCLASS_MOBILE))
		SendSysMessage(who, "That cannot be inspected.");
		return 0;
	endif

	// variables for sys messages
  	var streng := getStreng(AP_GetStat(subject, "Strength"), subject.graphic, subject.gender);
	var dext := getDext(AP_GetStat(subject, "Dexterity"), subject.graphic, subject.gender);
	var endur_percentage := CInt(CInt(AP_GetVital(subject, "Stamina"))/CDbl(AP_GetVitalMaximumValue(subject, "Stamina"))*100);
	var endur := getEnd(endur_percentage);

	// adjust succeed and fail msgs here
	var strMsg, strFail;
	if(subject.graphic != 0x190 && subject.graphic != 0x191)
		strMsg := "That creature";
		strFail := "that creature's physical characteristics.";
	else
		strMsg := subject.name;
		if(upper(subject.name[len(subject.name)]) != "S" || lower(subject.name[len(subject.name)]) != "s" )
			strFail := subject.name + "'s physical characteristics.";
		else
			strFail := subject.name + "' physical characteristics.";
		endif
	endif

	if( SkillCheck(who, ANATOMY, -1) > 0 )
		PrintTextAbovePrivate(subject, strMsg + " looks " + streng + " and " + dext + ".", who);
		// player's anatomy skill equal/greater than 65 can get endurance info
		var skill := AP_GetSkill(who, ANATOMY);
		if(skill >= 65)
			PrintTextAbovePrivate(subject, strMsg + " is at " + endur + " endurance.", who);
		endif
		return 1;
	else
		PrintTextAbovePrivate(subject, "You can not quite get a sense of " + strFail, who);
		return 0;
	endif

endprogram


function getStreng(subject, graphic, gender)

	var strSuper, strType, strGender;
	if(graphic != 0x190 && graphic != 0x191)
		strSuper := "supernaturally";
		strType := "creatures";
		strGender := "it";
	else
		strSuper := "superhumanly";
		strType := "people";
		if(gender == 0)
			strGender := "he";
		elseif(gender == 1)
			strGender := "she";
		endif
	endif
	if(subject >= 100)
		return strSuper + " strong";
	elseif(subject >= 99)
		return "like one of the strongest " + strType + " you have ever seen";
	elseif(subject >= 89)
		return "strong as an ox";
	elseif(subject >= 79)
		return "extraordinarily strong";
	elseif(subject >= 69)
		return "extremely strong";
	elseif(subject >= 59)
		return "very strong";
	elseif(subject >= 49)
		return "somewhat strong";
	elseif(subject >= 39)
		return "to be of normal strength";
	elseif(subject >= 29)
		return "somewhat weak";
	elseif(subject >= 19)
		return "rather feeble";
	else
		return "like " + strGender + " would have trouble lifting small objects";
	endif

endfunction


function getDext(subject, graphic, gender)

	var strSuper, strType, strGender;
	if(graphic != 0x190 && graphic != 0x191)
		strSuper := "supernaturally";
		strType := "creatures";
		strGender := "it";
	else
		strSuper := "superhumanly";
		strType := "people";
		if(gender == 0)
			strGender := "he";
		elseif(gender == 1)
			strGender := "she";
		endif
	endif
	if(subject >= 100)
		return "also looks " + strSuper + " agile";
	elseif(subject >= 99)
		return "like one of the fastest " + strType + " you have ever seen";
	elseif(subject >= 89)
		return "moves like quicksilver";
	elseif(subject >= 79)
		return "extraordinarily agile";
	elseif(subject >= 69)
		return "extremely agile";
	elseif(subject >= 59)
		return "very agile";
	elseif(subject >= 49)
		return "somewhat agile";
	elseif(subject >= 39)
		return "moderately dextrous";
	elseif(subject >= 29)
		return "somewhat uncoordinated";
	elseif(subject >= 19)
		return "very clumsy";
	else
		return "like " + strGender + " can barely manage to stay standing";
	endif

endfunction


function getEnd(subject)

	if(subject >= 100)
		return "one-hundred percent";
	elseif(subject >= 90)
		return "ninety percent";
	elseif(subject >= 80)
		return "eighty percent";
	elseif(subject >= 70)
		return "seventy percent";
	elseif(subject >= 60)
		return "sixty percent";
	elseif(subject >= 50)
		return "fifty percent";
	elseif(subject >= 40)
		return "forty percent";
	elseif(subject >= 30)
		return "thirty percent";
	elseif(subject >= 20)
		return "twenty percent";
	elseif(subject >= 10)
		return "low";
	else
		return "very low";
	endif

endfunction
