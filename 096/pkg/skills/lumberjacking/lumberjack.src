/*
 * $Id$
 *
 */
use uo;
use os;
use math;

include ":attributes:attributes";
include ":datafile:datafile";
include ":lumberjacking:settings";
include ":lumberjacking:lumberJack";
include "include/client";
include "include/sounds";
include "include/facings";

program skill_LumberJack(who, tool)
	SendSysMessage(who, "Select a tree to chop.");
	var targ := TargetCoordinates(who);
	
	if ( !targ )
		SendSysMessage(who, "Cancelled.");
		return 0;
	elseif ( !LJ_IsTree(targ.objtype) )
		SendSysMessage(who, "That is not a tree.");
		return 0;
	endif
	
	var orig_x := who.x;
	var orig_y := who.y;
	
	var num_loops := RandomDiceRoll("1d5");
	for ( num_loops; num_loops>=0; num_loops:=num_loops-1 )
		if ( !ChopTree(who, tool, targ, orig_x, orig_y) )
			break;
		endif
		// Wear down tool function call goes here.
		sleep(3);
	endfor

	SendSysMessage(who, "You stop lumberjacking.");
endprogram

function ChopTree(who, tool, targ, orig_x, orig_y)
	var i:=1;//RandomDiceRoll("3d3");
	for ( i; i>=0; i:=i-1 )
		if ( !CanChop(who, tool, targ, orig_x, orig_y) )
			return 0;
		endif

		PerformAction(who, ANIM_ATTACK_2HAND_WIDE);
		PlaySoundEffect(who, SFX_HIT_10);
		sleepms(1200);
	endfor

	var wood_type := LJ_GetWoodType(who.realm, targ.x, targ.y);
	var wood_amount := LJ_GetWoodAmount(who.realm, targ.x, targ.y);

	var max_wood := CInt(LJ_GetSettingsCfgElem("Settings").MaxWood);
	// The less wood there is, the more difficult it becomes.
	
	var difficulty := CInt(105 - (100 * wood_amount / max_wood));
	
	if ( who.cmdlevel )
		SendSysMessage(who, "MaxWood="+max_wood+"    CurWood="+wood_amount);
		SendSysMessage(who, "Difficulty ->"+difficulty);
	endif
	wood_amount := wood_amount - 1;
	LJ_SetWoodAmount(who.realm, targ.x, targ.y, wood_amount);

	if ( SkillCheck(who, LUMBERJACKING, difficulty) > 0 )
		var item := CreateItemInBackPack(who, wood_type, 1);
		if ( item.errortext )
			SendSysMessage(who, "Unable to create "+wood_type+" ->"+item.errortext);
			return 0;
		else
			SendSysMessage(who, "You place the log in your pack.");
			return 1;
		endif
	endif

	SendSysMessage(who, "You are unable to produce a useable log from this tree.");
	return 0;
endfunction

function CanChop(who, axe, targ, orig_x, orig_y)
	if ( who.hidden )
		SendSysMessage(who, "You stop chopping to remain hidden.");
		return 0;
	elseif ( CoordinateDistance(who.x, who.y, orig_x, orig_y) > 3 )
		SendSysMessage(who, "You turn your attention away from lumberjacking.");
		return 0;
	elseif ( !IsFacing(who, orig_x, orig_y) > 3 )
		SendSysMessage(who, "You turn your attention away from lumberjacking.");
		return 0;
	elseif ( !(axe in ListEquippedItems(who)) )
		SendSysMessage(who, "You put the axe away and stop chopping.");
		return 0;
	elseif ( LJ_GetWoodAmount(who.realm, targ.x, targ.y) <= 0 )
		SendSysMessage(who, "That tree has been chopped down.");
		return 0;
	elseif ( GetEquipmentByLayer(who, LAYER_MOUNT) )
		SendSysMessage(who, "You can't chop while mounted.");
		return 0;
	else
		return 1;
	endif
endfunction