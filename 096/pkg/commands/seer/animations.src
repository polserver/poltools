//Example: .animation (anim number) -> Used to perform an animation.

use uo;

include ":gumps:gumps";
include ":gumps:gumps_ex";

var anim_list := array
{
	"0 Walk (UnArmed)",
	"1 Walk (Armed)",
	"2 Run (UnArmed)",
	"3 Run (Armed)",
	"4 Nothing",
	"5 Look Around",
	"6 Fidget",
	"7 Warmode (1H Weapon)",
	"8 Warmode (2H Weapon)",
	"9 Attack (1H Swing)",	
	"10 Attack (1H Jab)",
	"11 Attack (1H Overhead)",
	"12 Attack (2H Overhead)",
	"13 Attack (2H Swing)",
	"14 Attack (Spear Jab)",
	"15 Walk warmode",
	"16 Directed Spellcast",
	"17 Area Spellcast",
	"18 Attack (Bow)",
	"19 Attack (Crossbow)",
	"20 Get hit",
	"21 Fall backwards",
	"22 Fall fowards",
	"23 Mounted walk",
	"24 Mounted run",
	"25 Mounted",
	"26 Mounted attack", 
	"27 Mounted bow attack",
	"28 Mounted crossbow attack",
	"29 Slap horse",
	"30 Dodge",
	"31 Punch",
	"32 Bow",
	"33 Salut",
	"34 Eat"	
};

program action(who, number)
	if ( !number )
		var gump := GFCreateGump();
		
		GFResizePic(gump, 0, 0, GFCfgConst("Defaults", "BackGround"), 500, 470);
		GFResizePic(gump, 15, 15, GFCfgConst("Defaults", "ForeGround"), 470, 440);
		
		var x_pos := 25;
		var y_pos := 25;
		foreach anim in ( anim_list )
			GFAddButton(gump, x_pos, y_pos+3, 2117, 2118, GF_CLOSE_BTN, _anim_iter);
			GFTextLine(gump, x_pos+20, y_pos, 2100, anim);
			y_pos := y_pos+20;
			
			if ( _anim_iter % 21 == 0 )
				y_pos := 25;
				x_pos := x_pos+250;
			endif
		endforeach
		
		number := GFSendGump(who, gump);
		number := number[0];
		
		if ( !number )
			SendSysMessage(who, "Cancelled");
			return 0;
		endif
		
		SendSysMessage(who, "Anim Name:"+anim_list[number]);
		number := number-1; // Remember, arrays have no 0 index.
		SendSysMessage(who, "Anim Numb:"+Hex(number));
	endif
	
	PerformAction(who, Cint(number));
	
	return 1;
endprogram
