/* $Id$
 *
 */

use uo;
use os;
use file;

include ":datafile:datafile";
include ":gumps:gumps";
include ":gumps:gumps_ex";

CONST ONOFF_BTN	:= 0xE000;

program MessageOfTheDay(mobile)

	var data_file := DFOpenDataFile(":motd:settings", DF_CREATE);
	var data_elem := DFFindElement(data_file, mobile.acctname, DF_CREATE);
	var off       := CInt(data_elem.GetProp("noshow"));

	var text_lines := ReadFile("motd.txt");
	var text := "";
	foreach line in text_lines
		text := text+line+"\n";
		SleepMS(5);
	endforeach

	if ( !text )
		text := "There is no MOTD for today.";
	endif
	
	var gump := GFCreateGump();
	GFResizePic(gump, 0, 0, GFCfgConst("Defaults", "BackGround"), 360, 480);

	GFResizePic(gump, 15, 12, GFCfgConst("Defaults", "ForeGround"), 330, 30);
	GFTextLine(gump, 110, 17, 2100, "Messages of the Day");

	GFResizePic(gump, 15, 45, GFCfgConst("Backgrounds", "GOLDTRIM"), 330, 390);
	GFHTMLArea(gump, 22, 53, 322, 375, text, 0, 1);

	GFResizePic(gump, 15, 438,GFCfgConst("Defaults", "ForeGround"), 330, 30);
	GFCheckBox(gump, 20, 443, 0x768, 0x767, (off == 0), ONOFF_BTN);
	GFTextLine(gump, 45, 443, 2100, "Open on login");

	data_elem.SetProp("FirstLine", text_lines[1]);

	var input := GFSendGump(mobile, gump);

	if ( input[ONOFF_BTN] )
		data_elem.EraseProp("noshow");
	else
		data_elem.SetProp("noshow", 1);
	endif

endprogram