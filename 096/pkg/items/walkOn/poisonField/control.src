/*
 * $Id$
 *
 */

use uo;
use os;
use util;

include ":timedscripts:timedScripts";

program controlScript(field)
	SetObjProperty(field, "#PID", GetPid());

	var creator := field.GetOwner();
	if ( creator.IsA(POLCLASS_MOBILE) )
		SetScriptController(creator);
	endif

	var poison_level := CInt(GetObjProperty(field, "PoisonLevel"));
	if ( !poison_level )
		poison_level := RandomDiceRoll("1d5");
	endif

	while ( field )
		PoisonNearbyMobiles(field, poison_level, creator);

		if ( ReadGameClock() % 7 == 0 )
			// Play once every 7 seconds.
			PlaySoundEffect(field, 0x475);
		endif

		var event := Wait_For_Event(1);
		if ( event )
			//Someone just walked onto the tile.
			PoisonMobile(event, poison_level, creator);
		endif
		
		if ( field.IsExpired() )
			DestroyItem(field);
			return 1;
		endif
	endwhile
endprogram

function PoisonNearbyMobiles(field, poison_level, creator)
	var nearby := ListMobilesNearLocationEX(field.x, field.y, field.z, 0, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN, field.realm);
	foreach mobile in ( nearby )
		PoisonMobile(mobile, poison_level, creator);

		sleepms(2);
	endforeach

	return (nearby.size());
endfunction

function PoisonMobile(mobile, poison_level, creator)
	TS_StartTimer(mobile, "DefaultPoison", 10, poison_level, creator);

	return 1;
endfunction
