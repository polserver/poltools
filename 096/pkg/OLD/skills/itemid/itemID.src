// $Id: itemID.src 834 2005-11-02 14:09:50Z austin $

use uo;
use os;
use basic;
use cfgfile;

include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";
include "include/client";

program skill_ItemID( who )

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	var the_item;
	var ncharges;
	SendSysMessage( who, "Select an object to identify." );
	the_item := Target(who, TGTOPT_CHECK_LOS);
	if( !the_item )
		SendSysMessage(who, "Targetting cancelled.");
		return 0;
	endif
	if( the_item.isA(POLCLASS_MOBILE) )
		SendSysMessage(who, "That's not an item!");
		return 0;
	endif

	if( SkillCheck(who, ITEM_IDENTIFICATION, -1) > 0 )
		var name := "";
		var itsname := the_item.name;
		if( !itsname )
			var cfgfile := ReadConfigFile(":*:itemdesc");
			var elem := FindConfigElem(cfgfile, the_item.objtype);
			itsname := elem.desc;
		endif
		if( GetObjProperty(the_item, "silvermod") )
			name :=  "silver" + name;
		endif
		if( GetObjProperty(the_item, "durability") )
			name := name + " " + GetObjProperty(the_item, "durability");
		endif
		if( GetObjProperty(the_item, "accuracy") )
			name := name + " " + GetObjProperty(the_item, "accuracy");
		endif
		itsname["a "] := "";
		itsname["an "] := "";
		itsname["magic "] := "";
		name := name + " " + itsname;
		if( GetObjProperty(the_item, "damagemod") )
			name := name + " " + GetObjProperty(the_item, "damagemod");
		endif
		name.erase(1);
		if( (!GetObjProperty(the_item, "IDed")) and (name != "") )
			SetName(the_item, name);
		endif
		var msg := "It appears to be";
		itsname := the_item.desc;
		if( itsname[1] == " " )
			if( Cint(itsname[2]) == 0 )
				case(Cstr(itsname[2]))
					"a": msg := msg + " an";
					"e": msg := msg + " an";
					"i": msg := msg + " an";
					"o": msg := msg + " an";
					"u": msg := msg + " an";
				endcase
			endif
		else
			msg := msg + " ";
			if( Cint(itsname[1]) == 0 )
				case(Cstr(itsname[1]))
					"a": msg := msg + " an ";
					"e": msg := msg + " an ";
					"i": msg := msg + " an ";
					"o": msg := msg + " an ";
					"u": msg := msg + " an ";
				endcase
			endif
		endif
		msg := msg + itsname;
		SendSysMessage( who, msg);
		SetObjProperty( the_item, "IDed", 1 );
		if( GetObjProperty(the_item, "charges") )
			ncharges := CStr(GetObjProperty(the_item, "charges") );
			if( ncharges )
				SendSysMessage(who, "It has " + ncharges + " charges.");
			endif
		endif
		if( GetObjProperty(the_item, "type") == "cursed" )
			if( SkillCheck(who, ITEM_IDENTIFICATION, 65) > 0 )
				SendSysMessage(who, "* This item is cursed! *");
			endif
		endif
		return 1;
	else
		SendSysMessage(who, "I'm not sure . . .");
		return 0;
	endif


endprogram