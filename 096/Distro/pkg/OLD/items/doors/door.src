use uo;
use os;
use cfgfile;

include "doors";


program use_door(chr, door)
  if(GetObjProperty(door, "#inuse"))
    return;
  endif
  SetObjProperty(door, "#inuse", 1);
  var doordesc := ReadConfigFile("itemdesc");
  var dooroptions := ReadConfigFile("door");
  var configuration := FindConfigElem(dooroptions, "doors");
  var refresh := GetConfigString(configuration, "DoorsRefresh");
  var flowoption := lower(GetConfigString(configuration, "OpenFromInside"));
  var closelocked := lower(GetConfigString(configuration, "CloseLocked"));
  if((chr.multi) && (!GetObjProperty(door, "Flow")) && (flowoption != "no"))
    run_script_to_completion(":doors:setFlow", chr.multi);
  endif
  var hs := GetObjProperty(door, "houseserial");
  if(!hs)
    hs := GetObjProperty(door, "house_serial");
  endif
  if(hs)
    var house := SystemFindObjectBySerial(hs);
    var ss := GetObjProperty(house, "signserial");
    var sign := SystemFindObjectBySerial(ss);
    if(!sign)
      var item := house.components;
      var signarray := array(0xbd2, 0xbd0, 0xbd1);
      foreach itemt in item
        if(itemt.objtype in signarray)
          SetObjProperty(house, "signserial", itemt.serial);
          sign := itemt;
          break;
        endif
      endforeach
    endif
    var friend := isfriend(chr, house);
    if(friend == 1)
      SendSysMessage(chr, "Your house and its contents have been refreshed.");
      SetObjProperty(sign,"decay", (ReadGameClock()+ 864000));
    endif
  endif
  if(IsLocked(chr, door, flowoption, closelocked))
    PrintTextAbovePrivate(door, "That is locked.", chr);
  else
    if(GetObjProperty( door, "WhenOpened" ))
      start_script(":doors:closeDoor", door);
    else
      detach();
      start_script(":doors:openDoor", door);
      if (refresh["yes"])
        var param := array;
        param[0] := chr;
        param[1] := door;
        start_script(":housing:refresh",param);
      endif
      foreach potentialdoor in ListObjectsInBox((door.x-1),(door.y-1),(door.z),(door.x+1),(door.y+1),(door.z),door.realm)
        if(!potentialdoor.hp)
          var doortype := doordesc[potentialdoor.objtype].doortype;
          if((doortype) && (door != potentialdoor))
            if((!potentialdoor.locked) && (!GetObjProperty(potentialdoor, "WhenOpened")))
              start_script(":doors:openDoor", potentialdoor);
            endif
           endif
        endif
      endforeach
    endif
  endif
  EraseObjProperty(door, "#inuse");
endprogram

function isfriend(who, house)
  var friend := 0;
  if (GetObjProperty(house, "owneracct") == who.acctname)
    friend := 1;
  endif
  var friendinfo := GetObjProperty(house, "friendlist");
  var coownerinfo := GetObjProperty(house,"coownlist");
  if(who.serial in friendinfo)
    friend := 1;
  elseif(who.serial in coownerinfo)
    friend := 1;
  endif
  return friend;
endfunction
