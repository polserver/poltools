// $Id$

use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:commonFunctions";
include ":attributes:attributes";
include "include/shapes";
include "include/client";
include "include/sounds";
include "include/facings";
include ":damage:damage";

program BrainNerve(params)
	var npc		:= params[1];
	var nerve_name	:= params[2];
	var event	:= params[3];
	var settings	:= params[4];
	var scripts	:= params[5];
	params := 0; // Not needed anymore.

	//Store it here so it does not have to do the math over and over.
	var flee_level := AP_GetVitalMaximumValue(npc, "Hits") * (CDbl(settings["FleeLevel"])/100.0);

	var opponent := event.source;
	
	AI_SetOpponent(npc, opponent);

	while ( npc )
		var dist := Distance(npc, opponent);
		if ( ShouldFlee(npc, flee_level) )
			AI_WarMode(npc, 0);
			AI_Move(npc, opponent, NEMOVE_AWAY, NEMOVE_RUN, WAKEUP, 100);
		elseif ( DoneFighting(npc, opponent, dist) )
			if ( scripts.Exists("EndFight") )
				params := array{opponent, settings, scripts};
				AI_StartNerve(npc, "EndFight", scripts["EndFight"].script, params);
			else
				AI_WarMode(npc, 0);
			endif
			AI_EndNerve(npc, nerve_name);
		elseif ( dist > 1 )
			AI_Move(npc, opponent, NEMOVE_TOWARD, NEMOVE_RUN, WAKEUP, dist+1);
		endif
		
		var next_attack := CInt(GetObjProperty(npc, "#NextAttack"));
		if ( next_attack < ReadGameClock() )
			SpecialAttacks(npc, opponent, dist);
		endif

		SleepMS(400);
		AI_ClearThoughts(npc, CLR_NERVE);
	endwhile
endprogram

function DoneFighting(npc, opponent, byref dist)
	if ( opponent.dead )
		AI_Speak(npc, "BURNINNATED!");
		return 1;
	elseif ( !opponent )
		return 1;
	elseif ( opponent.hidden )
		AI_Speak(npc, "NO HIDING FOR YOU!");
		SpecialAttacks(npc, opponent, dist);
		return 1;
	elseif ( opponent.concealed > npc.cmdlevel )
		return 1;
	elseif ( dist > 20 )
		AI_Speak(npc, "*snorts*");
		return 1;
	elseif ( dist > 10 && !CheckLineOfSight(npc, opponent) )
		return 1;
	endif

	return 0;
endfunction

function ShouldFlee(npc, byref flee_level)
	if ( AP_GetVital(npc, "Hits") < flee_level )
		return 1;
	endif

	return 0;
endfunction

function SpecialAttacks(npc, opponent, byref dist)
	AI_Turn(npc, opponent, NETURN_TOWARD);
	npc.frozen := 1;
	if ( dist <= 4 )
		BlowFire(npc, opponent);
	elseif ( dist <= 10 )
		SpitFireBall(npc, opponent);
	endif
	npc.frozen := 0;
	
	SetObjProperty(npc, "#NextAttack", ReadGameClock()+15);
	
	return 1;
endfunction

function BlowFire(npc, opponent)
	var facing := GetFacing(npc.x, npc.y, opponent.x, opponent.y);
	var coordinates := GetPointsByFacing(npc.x, npc.y, facing, 4);
	PerformAction(npc, 12);
	SleepMS(1100);
	AI_Speak(npc, "*BURNINNATES*");
	foreach coord in ( coordinates )
		PlayStationaryEffect(coord.x, coord.y, npc.z+15, SelectRandomExplosion(), 15, 15, 0);
		foreach player in ListMobilesNearLocationEX(coord.x, coord.y, coord.z, 0, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN, npc.realm)
			ApplyRawDamageEX(player, RandomDiceRoll("5d15"), DMG_FIRE, npc);
			SleepMS(2);
		endforeach
		SleepMS(2);
	endforeach
	
	return 1;
endfunction

function SpitFireBall(npc, opponent)
	PerformAction(npc, 0x6);
	SleepMS(80);
	PrintTextAbove(npc, "*"+npc.name+" spits a ball of fire*");
	PlayMovingEffect(npc, opponent, GFX_SMALL_FIREBALL, 10, 10, 1);

	var coord_list := GetCoordsInLine(npc.x, npc.y, opponent.x, opponent.y);
	foreach coord in coord_list
		if ( _coord_iter == 1 ) // Dont make one where its standing
			continue;
		endif
		
		var field := CreateItemAtLocation(coord.x, coord.y, npc.z, "FireField_NS", 1);
		field.SetDuration(2);
		SleepMS(105);
	endforeach

	PlaySoundEffect(opponent, 0x208);
	ApplyRawDamageEX(opponent, RandomDiceRoll("3d7"), DMG_FIRE, npc);

	foreach coord in GetCoordsInSquare(opponent.x, opponent.y, 3)
		if ( RandomInt(2) != 1 ) // Don't place a fire field
			continue;
		endif
		PlayStationaryEffect(coord.x, coord.y, npc.z, SelectRandomExplosion(), 10, 10, 0);
		var field := CreateItemAtLocation(coord.x, coord.y, opponent.z, "FireFieldNS", 1);
		field.SetDuration(5);
		SleepMS(5);
	endforeach
	
	return 1;
endfunction

function SelectRandomExplosion()
	case ( RandomInt(3) )
		1: return GFX_EXPLODE_2; break;
		2: return GFX_EXPLODE_3; break;
		default: return GFX_EXPLODE_1; break;
	endcase
endfunction
		