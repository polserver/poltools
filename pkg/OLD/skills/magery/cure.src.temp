use uo;
use cfgfile;

include "include/eventID";
include ":attributes:attributes";
include "include/sound";
include "include/tileEffects";
include ":timedscripts:timedScripts";
include ":timedscripts:poisonUtil";


program cure( parms )
  var caster := parms.caster;
  var castOn := parms.castOn;
  var npccfg := ReadConfigFile("::npcdesc");
  PlaySoundEffect(caster, SFX_SPELL_CURE);
  PlayObjectCenteredEffect( castOn, FX_BLESS_EFFECT, 10,10);
  var potency := CInt(AP_GetSkill(caster, MAGERY) / 33) +1;
  if(castOn.isA(POLCLASS_NPC))
    var element := npccfg[castOn.npctemplate].elemental;
    if(element == "poison")
      var ev := array;
      ev.+ type;
      ev.+ source;
      ev.type := EVID_PEACEMADE;
      SendEvent(castOn, ev);
      ev.source := caster;
      SendEvent(castOn, ev);
      ev.type := EVID_ENGAGED;
      SendEvent(castOn, ev);
      ApplyRawDamage(castOn, potency);
    endif
  endif
  if (TS_GetTimerNames(castOn, "P").size() > 0)
    TS_CurePoison(castOn, 120, "P", potency);
    if (TS_GetTimerNames(castOn, "P").size() > 0)
      SendSysMessage(caster, "You cure some poison.");  
      SendSysMessage(castOn, caster.name + " has partially cured you!");
    else
      SendSysMessage(caster, castOn.name + " cured.");
      SendSysMessage(castOn, caster.name + " has cured you completely!");
    endif
  endif
endprogram
