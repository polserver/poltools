// $Id: peacemaking.src 911 2005-11-05 23:20:56Z austin $

use os;
use uo;

include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";
include "include/OLD/eventID";
include ":itemutils:canAccess";
include ":musicianship:bard";

program skill_Peacemaking(who)

	EraseObjProperty(who, "#IsMeditating");

	if(Cint(GetObjProperty(who, "#LastMusic")) > ReadGameClock())
		SendSysMessage(who, "You must wait a moment before playing again.");
		return 0;
	endif

	SetObjProperty(who, "#LastMusic", Cint(ReadGameClock() + 2));

	var instrument := findInstrument(who);
	if(!instrument)
		return 0;
	endif
	if( instrument.container != who.backpack )
		SendSysMessage( who, "That instrument is not in your backpack.");
		return 0;
	endif
	if(!can_access(who, instrument))
		return 0;
	endif

	SendSysMessage(who, "Target the creature you would like to calm");
	var peacee := Target(who, TGTOPT_CHECK_LOS);

	if( !peacee.isA(POLCLASS_MOBILE || peacee.dead) )
		SendSysMessage(who, "Cancelled");
		return 0;
	endif

	var barding_difficulty := -1;
	if( peacee.npctemplate )
		barding_difficulty := figureBardDifficulty(who, peacee, instrument, PEACEMAKING);
	endif

	if ( !isBardable(who, peacee, PEACEMAKING, barding_difficulty) )
		PrintTextAbovePrivate(peacee, "You have no chance to calm that creature!", who);
		return 0;
	endif

	if( bardSkillCheck(who, instrument, PEACEMAKING, barding_difficulty) )
		calmDown( who, peacee );
	else
		return 0;
	endif

	return 1;

endprogram


function calmDown( who, peacee )

	var peace_length_single := CInt(AP_GetSkill(who, PEACEMAKING) / 5);
	var peace_length_ranged := CInt(AP_GetSkill(who, PEACEMAKING) / 20);

	var ev := struct;
	ev.+ type;
	ev.type := EVID_PEACEMADE;
	ev.+ source;
	ev.source := who;

	if( who == peacee )
		foreach thetarg in ListMobilesNearLocation(who.x, who.y, who.z, 8, who.realm)
			if( GetObjProperty(thetarg, "#Provoked") )
				EraseObjProperty(thetarg, "#Provoked");
			endif
			if( thetarg.npctemplate )
				ev.+ length;
				ev.length := peace_length_ranged;
				SendEvent(thetarg, ev);
			elseif( thetarg.acctname )
				SetObjProperty(thetarg, "#Peaced", ReadGameClock()+5);
				thetarg.SetWarMode(0);
			endif
		endforeach
	elseif( peacee.npctemplate )
		ev.+ length;
		ev.length := peace_length_single;
		SendEvent(peacee, ev);
	endif
	SetObjProperty(who, "#LastMusic", Cint(ReadGameClock() + 5));

	return 1;

endfunction