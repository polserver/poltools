// $Id: camping.src 905 2005-11-05 08:20:50Z muaddiblsd $

include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":attributes:skillCheck";
include ":itemutils:objtype";
include ":itemutils:canAccess";

program UseWood( who, wood )

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");
	if( !can_access(who, wood) )
		return 0;
	endif
	if( wood.movable == 0 )
		SendSysMessage(who, "You cannot light that.");
		return 0;
	endif
	var camptimer := GetObjProperty(who, "CampTimer");
	if( camptimer )
		if( camptimer > ReadGameClock() )
			SendSysMessage(who, "You must wait a few moments to camp again.");
			return 0;
		endif
	endif
	SetObjProperty(who, "CampTimer", ReadGameClock() + 5);
	if( !Accessible(who, wood) )
		PrintTextAbovePrivate( wood, "You can't reach that!", who );
		return 0;
	endif
	if( wood.container )
		PrintTextAbovePrivate( wood, "Put that on the ground first!", who );
		return 0;
	endif

	if( SkillCheck(who, CAMPING, -1) > 0 )
		var x := wood.x;
		var y := wood.y;
		var z := wood.z;
		Detach();
		var campfire := CreateItemAtLocation( x, y, z, UOBJ_CAMPFIRE, 1, who.realm );
		campfire.movable := 0;
		SubtractAmount(wood,1);
		sleep(60);
		DestroyItem(campfire);
		campfire := CreateItemAtLocation(x,y,z,UOBJ_ASHES,1, who.realm);
		campfire.movable := 0;
		sleep(60);
		DestroyItem(campfire);
	else
		PrintTextAbovePrivate( wood, "You couldn't manage to start the fire.", who );
	endif

endprogram
