/* $Id$
 *
 */
use uo;
use cfgfile;
use util;

include ":itemutils:canAccess";

program use_StatueDeed(who, deed)

	if( !can_access(who, deed) )
		return 0;
	endif
	if( !(deed in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessage(who, "That item is not in your backpack.");
		return 0;
	endif
	if( !ReserveItem(deed) )
		return 0;
	endif

	var statue_config := ReadConfigFile(":ministatues:miniStatues");
	var elem_names := GetConfigStringKeys(statue_config);
	var statue_elem := FindConfigElem( statue_config, elem_names[(RandomInt(elem_names.size())+1)]);

	var mini_statue := CreateItemAtLocation(who.x, who.y, who.z, statue_elem.Graphic, 1, who.realm);

	if( ReleaseItem(deed) )
		if( DestroyItem(deed) )
			SetName(mini_statue, statue_elem.Name);
			mini_statue.graphic := statue_elem.Graphic;
			mini_statue.usescript := ":ministatues:useStatue";
			MoveItemToContainer(mini_statue, who.backpack);
			SendSysMessage(who, "You have redeemed your ticket for a mini statue.");
		else
			DestroyItem(mini_statue);
		endif
	endif
	return 1;

endprogram
