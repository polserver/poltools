/*
 * $Id: createRect.src 663 2005-10-27 02:43:27Z muaddiblsd $
 *
 */

use uo;
use os;

include ":gumps:requestgump";

program textcmd_Create(who, params)
	var objtype, zheight;
	if ( !params )
		objtype := RequestGump(who, "Item objtype/name to create");
		zheight := RequestGump(who, "Z height", "", who.z);
	else
		params := SplitWords(params);
		objtype := params[1];
		zheight := params[2];
	endif

	if ( GetObjTypeByName(objtype) )
		objtype := CInt(GetObjTypeByName(objtype));
	else
		objtype := CInt(objtype);
	endif
	zheight := CInt(zheight);

	if ( !objtype )
		SendSysMessage(who, "Syntax: .createrect <objtype> (zheight)");
		return 0;
	elseif ( !zheight )
		zheight := who.z;
	endif

	SendSysMessage( who, "Target the top-left corner" );
	var topleft := TargetCoordinates(who);
	if ( !topleft )
		return 0;
	endif
	SendSysMessage( who, "Target the bottom-left corner" );
	var botright := TargetCoordinates(who);
	if ( !botright )
		return 0;
	endif

	if ( topleft.x <= botright.x and topleft.y <= botright.y )
		var x, y;
		for( x := topleft.x; x <= botright.x; x := x + 1 )
			for( y := topleft.y; y <= botright.y; y := y + 1 )
				var item := CreateItemAtLocation(x, y, zheight, objtype, 1, who.realm);
				item.decayat := 0;
				sleepms(2);
			endfor
		endfor
		sleepms(2);
	endif

	SendSysMessage(who, "Done.");

	return 1;
endprogram
