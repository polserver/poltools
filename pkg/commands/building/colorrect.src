/*
 * $Id: colorRect.src 663 2005-10-27 02:43:27Z muaddiblsd $
 *
 */

use uo;
use os;

program textcmd_ColorRect(who, text)
	
	text := SplitWords(text);
	
	var color := text[1];
	var zheight := CInt(text[2]);
	var objtype := CInt(text[3]);
	
	if ( !color )
		SendSysMessage(who, "Syntax: .colorrect <color #> (zheight) (objtype)");
		return;
	elseif ( !zheight )
		zheight := who.z;
	endif
	
	color := CInt(color);

	SendSysMessage(who, "Target the top-left corner");
	var topleft := TargetCoordinates( who );
	if ( !topleft )
		return 0;
	endif
	SendSysMessage(who, "Target the bottom-left corner");
	var botright := TargetCoordinates( who );
	if ( !botright )
		return 0;
	endif
	
	if ( (topleft.x <= botright.x) && (topleft.y <= botright.y) )
		var x, y;
		for( x := topleft.x; x <= botright.x; x := x + 1 )
			for( y := topleft.y; y <= botright.y; y := y + 1 )
				foreach item in ListItemsNearLocation(x, y, zheight, 0, who.realm)
					if ( item.objtype == objtype || !objtype )
						item.color := color;
					endif
					sleepms(2);
				endforeach
				sleepms(2);
			endfor
			sleepms(2);
		endfor
	endif
endprogram
