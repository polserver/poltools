use uo;
use os;
use util;
include "include/attributes";

program exploder(parms)
  var potion := parms[1];
  if(GetObjProperty(potion,"running"))
    return;
  endif
  SetObjProperty(potion,"running", 1);
  var who := parms[2];
  if(GetObjProperty(potion,"pause"))
    sleepms(250);
  endif
  var thecenter := potion;
  while(thecenter.container)
    thecenter := thecenter.container;
  endwhile
  SetScriptController(who);
  var dmg := 3;
  PlayStationaryEffect( potion.x, potion.y, potion.z, 0x36b0, 7, 10, 1);
  PlaySoundEffect(potion, 0x208);
  potion.graphic := 1;
  foreach critter in ListMobilesNearLocationEx( potion.x, potion.y, potion.z, 1, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN );
    PlayObjectCenteredEffect( critter, 0x36b0, 7, 0x10 );
    PlaySoundEffect( critter, 0x208);
    case(potion.objtype)
      0xdc08: dmg := (RandomInt(4) + 1 );
      0xdc09: dmg := (RandomInt(4) + 6 );
      0xdc07: dmg := (RandomInt(9) + 11 );
    endcase
    ApplyRawDamage(critter, dmg);
  endforeach
  var potions := {0xdc08, 0xdc09, 0xdc07};
  foreach item in ListItemsNearLocation(potion.x, potion.y, potion.z, 1)
    if((item.objtype in potions) && (item != potion))
      SetObjProperty(item,"pause", 1);
      SetObjProperty(item,"lit", 1);
      var passparms := {item, who};
      start_script(":alchemy:exploder", passparms);
     endif
  endforeach
  DestroyItem(potion);
endprogram
