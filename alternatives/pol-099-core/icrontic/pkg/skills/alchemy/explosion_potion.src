use uo;
use os;
use util;
use cfgfile;
include "include/eventid";

Const MOVING_EFFECT_PURPLE_POTION  := 0x0F0D; 
Const SOUND_EFFECT_EXPLOSION	   := 0x208; 
Const EFFECT_EXPLOSION             := 0x36b0;

program use_explosion( character, potion )

    if (!Accessible( character, potion))
	SendSysmessage(character,"You can't get to that!");
	return;
    endif
    
    set_critical(1);
    if (getobjproperty(potion, "#bomb"))
        sendsysmessage(character, "That is already going to explode!");
        return;
    else
        setobjproperty(potion, "#bomb", 1);
    endif
    set_critical(0);
    
    var tobjtype := potion.objtype;
    character.hidden := 0;
    SendSysmessage (character, "Select a target");
    var parms := {};
    parms[1] := potion;
    parms[2] := character;
    var cast_on := TargetCoordinates( character );
    if (!cast_on)
        return;
    endif
    if (!potion)
        return;
    endif
    
    if (!CheckLosAt( character, cast_on.x, cast_on.y, cast_on.z ))
        SendSysmessage(character, "You can't see that!");
        return;
    endif
    if (!potion)
        return;
    endif
       
    Detach();
    if (!potion)
        return;
    endif
    PlayMovingEffectXYZ( character.x, character.y, character.z+15,
                         cast_on.x, cast_on.y, cast_on.z,
                         MOVING_EFFECT_PURPLE_POTION, 8, 0, 0 );
    if (!potion)
        return;
    endif
    MoveItemtoLocation(potion, cast_on.x, cast_on.y, cast_on.z, MOVEITEM_FORCELOCATION );
    start_script(":alchemy:explosiontimer", parms);
endprogram

