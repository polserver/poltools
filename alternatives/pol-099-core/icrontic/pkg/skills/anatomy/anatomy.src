use uo;
use os;

include "include/client";
include "include/attributes";

var npccfgfile := ReadConfigFile("::npcdesc");

program anatomy( who )
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  var descr;
  var percenthealth;
  SendSysMessage( who, "Select a target." );
  var subject := Target(who);
  if (!subject.isA(POLCLASS_MOBILE))
	SendSysMessage(who, "Looks pretty dead to you.");
	return;
  endif
  if (subject.isA(POLCLASS_NPC))
    var whatisit := CStr(npccfgfile[subject.npctemplate].script);
    if((whatisit == "playerMerchant") or (whatisit == "merchant") or (whatisit == "banker") or (whatisit == "townHealer")
         or (whatisit == "townhealer") or (whatisit == "stablemaster") or (whatisit == "thief") or (whatisit == "bucsTownHealer")
         or (whatisit == "bucsBanker") or (whatisit == "townGuard") or (whatisit == "auctioneer") or (whatisit == "tamed")or (whatisit == "barber"))
      SendSysMessage(who, "That seems pretty uninteresting to you.");
      return;
    endif
  endif
  if(subject.serial == who.serial)
	SendSysMessage(who, "That person needs therapy.");
	return;
  endif
  var pts := CInt(GetEffectiveSkill(who, SKILLID_ANATOMY) + 1);
  if(pts < 30)
    pts := 30;
  endif
  if(CheckSkill(who,SKILLID_ANATOMY, -1, pts))
    percenthealth := CInt( (100 * GetHp(subject)/GetMaxHp(subject)));
    descr := findhp(percenthealth);
    if (CInt(GetObjProperty(subject, "poison_level")) > 0 )
      descr := descr + ", and is poisoned.";
    endif
    var streng := findstrength(GetStrength(subject));
    var dext := finddex(GetDexterity(subject));
    PrintTextAbovePrivate(subject, "The subject " + dext + " and " + streng, who);
    sleep(1);
    PrintTextAbovePrivate(subject, "The subject appears to be " + descr, who);
    else
        SendSysMessage(who, "You're not sure . . .");
    endif
endprogram

function findhp(str)
  if(str >= 80)
    return "in excellent shape";
  elseif(str >= 50)
    return "in fair health";
  elseif(str >= 30)
    return "in poor health";
  elseif(str >= 10)
    return "in bad shape";
  elseif(str <= 10)
    return "near death";
  endif
endfunction

function findstrength(str)
  if(str >= 98)
    return "seems superhumanly strong.";
  elseif(str >= 88)
    return "seems like one of the strongest people you have ever seen.";
  elseif(str >= 78)
    return "seems strong as an ox.";
  elseif(str >= 68)
    return "seems extraordinarily strong.";
  elseif(str >= 58)
    return "seems extremely strong.";
  elseif(str >= 48)
    return "seems very strong.";
  elseif(str >= 38)
    return "seems somewhat strong.";
  elseif(str >= 28)
    return "seems to be of normal strength.";
  elseif(str >= 18)
    return "seems rather feeble.";
  elseif(str >=  8)
    return "seems like they would have trouble lifting small objects.";
  endif
endfunction

function finddex(str)
  if(str >= 98)
    return "looks superhumanly agile";
  elseif(str >= 88)
    return "looks like one of the fastest people you have ever seen";
  elseif(str >= 78)
    return "moves like quicksilver";
  elseif(str >= 68)
    return "looks extraordinarily agile";
  elseif(str >= 58)
    return "looks extremely agile";
  elseif(str >= 48)
    return "looks very agile";
  elseif(str >= 38)
    return "looks somewhat agile";
  elseif(str >= 28)
    return "looks moderately dextrous";
  elseif(str >= 18)
    return "looks very clumsy";
  elseif(str >=  8)
    return "looks like they barely manage to stay standing";
  endif
endfunction
