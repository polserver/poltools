use uo;
use util;
use os;

include "include/client";
include "include/canAccess";

const FX_POISON_GAS := 0x113a;

program open_trapped_item(who, item)
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(!can_access(who, item))
    return;
  endif
  var traptype := CInt(GetObjProperty(item,"trap_type"));
  var trapstrength := GetObjProperty(item,"trap_strength");
  var trapperserial := CInt(GetObjProperty(item, "trapper"));
  var oldscript := GetObjProperty(item, "oldscript");
  if(!oldscript)
    oldscript := "";
  endif
  var trapper;
  foreach chr in EnumerateOnlineCharacters()
    if(chr.serial == trapperserial)
      trapper := chr;
      break;
    endif
  endforeach
  if(!trapper)
    trapper := SystemFindObjectBySerial(trapperserial, SYSFIND_SEARCH_OFFLINE_MOBILES);
  endif
  if(trapper)
    SetObjProperty(who, "LastHit", {trapper.name, trapper.serial, "trapped chest" });
    SetScriptController(trapper);
  endif
  case(traptype)
    1: PlaySoundEffect(who, SFX_224);
       var dmg :=(RandomInt(20) + CInt(trapstrength));
       SendSysMessage(who, "You set off a needle trap!");
       ApplyDamage(who, dmg);
       item.usescript := oldscript;
       EraseObjProperty(item, "trap_type");
       EraseObjProperty(item, "trap_strength");
       item.usescript := oldscript;
    2: PlaySoundEffect(who, SFX_232);
       PrintTextAbovePrivate(who, "You set off a poison trap!", who);
       PlayObjectCenteredEffect(who, FX_POISON_GAS, 10,10);
       item.usescript := oldscript;
       EraseObjProperty(item, "trap_type");
       EraseObjProperty(item, "trap_strength");
       item.usescript := oldscript;
       var plevel := CInt(GetObjProperty(who, "poison_level"));
       SetObjProperty(who, "PoisonStamp", ReadGameClock());
       SetObjProperty(who, "poison_level",CStr(trapstrength / 20));
       var passparms := { who, who, "poison trap" };
       start_script(":spells:poisonDamage", passparms);
    3: PlaySoundEffect(who, SFX_208);
       SendSysMessage(who, "You set off an explosion trap!");
       PlayObjectCenteredEffect(who, FX_EXPLODE_3, 10,10);
       var dmg :=(RandomInt(20) + CInt(trapstrength));
       ApplyRawDamage(who, dmg);
       item.usescript := oldscript;
       EraseObjProperty(item, "trap_type");
       EraseObjProperty(item, "trap_strength");
       item.usescript := oldscript;
    4: PlaySoundEffect(who, SFX_208);
       SendSysMessage(who, "You set off an explosion trap!");
       PlayObjectCenteredEffect(who, FX_EXPLODE_3, 10,10);
       var dmg :=(RandomInt(20) + trapstrength);
       dmg := Resist(who, dmg);
       ApplyRawDamage(who, dmg);
       item.usescript := oldscript;
       EraseObjProperty(item, "trap_type");
       EraseObjProperty(item, "trap_strength");
       item.usescript := oldscript;
    default: SendSysMessage(who, "You narrowly avoid the trap!");
  endcase
endprogram

function Resist(cast_on, amt)
  if(cast_on.dead)
    return 0;
  endif
  if(CheckSkill(cast_on, SKILLID_MAGICRESISTANCE, amt, (amt * 2)))
	SendSysMessage(cast_on, "You feel yourself resisting magical energy!");
	PlaySoundEffect(cast_on, SFX_SPELL_WEAKEN);
	PlayObjectCenteredEffect(cast_on, FX_CURSE_EFFECT, 5,5);
	amt := amt / 2;
	return amt;
  else
	return amt;
  endif
endfunction  
