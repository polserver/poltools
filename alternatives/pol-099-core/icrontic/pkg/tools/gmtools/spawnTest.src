use uo;
use os;

include "include/startEqp";
include "util/key";

var chestarray := {};

program spawntest(item)
  var rng := GetObjProperty(item,"range");
  var amt := GetObjProperty(item,"amount");
  var frq := GetObjProperty(item,"duration");
  foreach thing in ListItemsNearLocation(cint(item.x), cint(item.y), cint(item.z), CInt(rng))
    if(GetObjProperty(thing,"link") = item.serial)
      DestroyItem(thing);
    endif
  endforeach
  if((!amt) or (!frq))
    print("Chest Spawner " + item.serial + " at: " + item.x + ", " + item.y + " is improperly configured");
    return;
  endif
  var chest, current, thechest, newamt;
  CreateTreasureChests(item, amt);
  foreach thing in chestarray
    thing.graphic := thechest.objtype;
  endforeach
  while(item)
    sleep(frq);
    CleanArray();
    current := len(chestarray);
    newamt := CInt(amt - current);
    if(newamt > 0)
      CreateTreasureChests(item, newamt);
    endif
    sleep(frq);
  endwhile
endprogram   

function cleanarray()
  var newarray := {};
  foreach chest in chestarray
    if((chest.locked == 0) || (chest.usescript != ":tinkering:tinkerTraps") || (!chest))
      DestroyItem(chest);
    elseif(chest)
      newarray.append(chest);
    endif
  endforeach
  chestarray := newarray;
endfunction

function CreateTreasureChests(item, amt)
  var lvl := GetObjProperty(item,"level");
  var rng := GetObjProperty(item,"range");
  var newx;
  var newy;
  var newz;
  if(rng > 10)
    rng := 10;
  endif
  var checker;
  var total := 0;
  var type, gfx, lootgroup, chest, lockid, strength, traptype;
  while(total < amt)
    checker := 0;
    while((checker == 0) && (item))
      newx := cint(item.x + RandomInt(rng) - RandomInt(rng));
      newy := cint(item.y + RandomInt(rng) - RandomInt(rng));
      newz:= cint(GetWorldHeight(newx, newy));
      if((cint(item.x) == newx) && (cint(item.y) == newy))
        checker := 0;
      elseif(CheckLosAt(item, newx, newy, newz))
        checker := 1;
      elseif(len(ListItemsNearLocation(newx, newy, newz, 0)) > 0)
        checker := 0;
      endif
      sleepms(10);
    endwhile
    type := RandomInt(4)+1;
    case(type)
      1: gfx := 3650;
      2: gfx := 3651;
      3: gfx := 3703;
      4: gfx := 3648;
      5: gfx := 3708;
    endcase
    chest := CreateItemAtLocation(newx, newy, newz, gfx, 1);
    SetObjProperty(chest,"link", item.serial);
    chest.locked :=  1;
    chest.movable := 0;
    chest.usescript := ":tinkering:tinkerTraps";
    lockid := AllocLockId();
    SetObjProperty(chest, "lockid", lockid);
    SetObjProperty(chest, "level", lvl);
    strength := 0;
    case(lvl)
      1: lootgroup := 69;    strength :=  20;
      2: lootgroup := 70;    strength :=  40;
      3: lootgroup := 71;    strength :=  60;
      4: lootgroup := 72;    strength :=  80;
      5: lootgroup := 73;    strength := 100;
    endcase
    traptype := RandomInt(10)+1;
    if(traptype > 3)
      traptype := 3;
    else
      traptype := 2;
    endif
    SetObjProperty(chest, "trap_type", (CStr(traptype)));
    SetObjProperty(chest, "LockPickDiff", (strength - RandomInt(20)));
    SetObjProperty(chest, "trap_strength", strength);
    makeloot(chest, lootgroup);
    CreateFromGroup(chest, lootgroup);
    chestarray.append(chest); 
    total := total + 1;
  endwhile
endfunction
