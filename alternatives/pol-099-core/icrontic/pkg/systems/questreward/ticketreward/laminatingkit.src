use uo;
use os;

include "../../../../scripts/include/itemtypes";

var color_array := {
	1354,
	1402,
	1420,
	1445,
	1861,
	1638,

	1701,
	1254,
	2312,
	2411,
	2005,
	1237,
};


program uselaminatingkit (character, dyes)
	if (!ReserveItem (dyes))
		SendSysMessage (character, "You can't use this right now!");
		return;
	endif
	
	if (!dyes.movable)
		SendSysMessage (character, "You can't use this right now!");
		return;
	endif
	
	SendSysMessage (character, "Select a piece of metal armor to use this on:");
	var targetted := Target (character, TGTOPT_CHECK_LOS);
	if (!targetted)
		SendSysMessage (character, "Canceled.");
		return;
	endif
	
	if (!accessible (character, targetted) )
		SendSysMessage (character, "You can't reach that!");
		return;
	endif
	
	if (!targetted.movable)
		SendSysMessage (character, "You can't use the laminating kit on that!");
		return;
	endif
	
	if (!IsMetalArmor (targetted) and !IsMetalWeapon (targetted) and !IsMetalShield (targetted))
		SendSysMessage (character, "You can only use the laminating kit on metal weapons and armor!");
		return;
	endif
	
	//You can't use them on chaos and order shields
	if (IsMetalShield (targetted))
		if (targetted.objtype = UOBJ_ORDER_SHIELD or targetted.objtype = UOBJ_CHAOS_SHIELD)
			SendSysMessage (character, "You can only use the laminating kit on metal weapons and armor!");
			return;
		endif
	endif

	var selected_color := SelectAColor (character);
	if (!selected_color)
		return;
	endif
	
	if (! (selected_color in color_array) )
		SendSysMessage (character, "Whoops!  Big error!");
		return;
	endif
	
	targetted.color := selected_color;
	DestroyItem (dyes);	

endprogram



function SelectAColor (character)
	var gump_layout := {
		"nodispose",
		"page 0",
		"resizepic 30 30 83 200 220",
		"button 50 70 2104 2103 1 0 " + color_array[1],
		"button 50 90 2104 2103 1 0 " + color_array[2],
		"button 50 110 2104 2103 1 0 " + color_array[3],
		"button 50 130 2104 2103 1 0 " + color_array[4],
		"button 50 150 2104 2103 1 0 " + color_array[5],
		"button 50 170 2104 2103 1 0 " + color_array[6],
		
		"button 130 70 2104 2103 1 0 " + color_array[7],
		"button 130 90 2104 2103 1 0 " + color_array[8],
		"button 130 110 2104 2103 1 0 " + color_array[9],
		"button 130 130 2104 2103 1 0 " + color_array[10],
		"button 130 150 2104 2103 1 0 " + color_array[11],
		"button 130 170 2104 2103 1 0 " + color_array[12],

		"text 70 65 " + (color_array[1]-1) +" 1",
		"text 70 85 " + (color_array[2]-1) +" 2",
		"text 70 105 " + (color_array[3]-1) +" 3",
		"text 70 125 " + (color_array[4]-1) +" 4",
		"text 70 145 " + (color_array[5]-1) +" 5",
		"text 70 165 " + (color_array[6]-1) +" 6",
	
		"text 150 65 " + (color_array[7]-1) +" 7",
		"text 150 85 " + (color_array[8]-1) +" 8",
		"text 150 105 " + (color_array[9]-1) +" 9",
		"text 150 125 " + (color_array[10]-1) +" 10",
		"text 150 145 " + (color_array[11]-1) +" 11",
		"text 150 165 " + (color_array[12]-1) +" 12",
		"button 95 210 2119 2120 1 0 0",

	};
     
	var gump_data := {
		"filler",
		"One",
		"Two",
		"Three",
		"Four",
		"Five",
		"Six",
		"Seven",
		"Eight",
		"Nine",
		"Ten",
		"Eleven",
		"Twelve",
	};
	
	var gump_return := SendDialogGump (character, gump_layout, gump_data);
	if (!gump_return and gump_return[0] != 0)
		SendSysMessage (character, "You must close other gumps first!");
		return;
	endif
	
	if (gump_return[0] = 0)
		SendSysMessage (character, "Canceled.");
		return;
	endif
	
	return gump_return[0];
endfunction


















