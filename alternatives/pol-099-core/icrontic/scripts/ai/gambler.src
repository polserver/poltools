// Gambler version 1.0
// by Zulu (zulu@zuluhotel.com)   November 12, 2000

use npc;
use os;
use uo;
use util;

include "include/randname";
include "include/eventid";
include "include/client";
include "include/mrcspawn";
include "include/vetement";
include "include/attributes";

const MONEY := 0xeed;
Const REACT_THRESHOLD := 2;
const HALT_THRESHOLD := 10;

var gameOver :=0;
var dealervalue := 0;
var cardCount :=0;
var youvalue :=0;
var cardz := {};
var youace :=0;
var dealerace :=0;
var youcards :=0;
var dealercards :=0;
var dealercardhidden := array ( 0,0 );
var placecards := array( 105,145,185,225,265 );
var placevalue := array( 8,10,12,14,16 );
var placesuits := array( 9,11,13,15,17 );
var dealerskill;
var youbet := 100;
var dealmoney := 0;
var moneyamount := 0;
var player;
var me := Self();
var moneyreload := 0;
var dd;

var layout := array(
    "page 0",
    "resizepic 30 100 5120 285 166",
    "resizepic 30 266 5120 285 100",
    "text 45 109 1500 0",
    "text 45 129 70 1",
    "text 45 190 600 2",
    "text 45 210 70 3",
    "text 70 300 800 4",
    "text 140 300 800 5",
    "text 200 300 800 6",
    "button 50 303 2117 2118 1 2 101",
    "button 120 303 2117 2118 1 0 102",
    "button 180 303 2117 2118 1 0 103",
    "button 300 103 2360 2361 1 0 666",
    "resizepic 105 129 2171 35 50",
    "resizepic 145 129 2171 35 50",
    "",
    "",
    "",
    "resizepic 105 210 2171 35 50",
    "resizepic 145 210 2171 35 50",
    "",
    "",
    "",
    "text 120 155 32 8",
    "text 160 155 0 10",
    "text 200 155 32 12",
    "text 240 155 32 14",
    "text 280 155 32 16",
    "text 120 236 0 18",
    "text 160 236 0 20",
    "text 200 236 32 22",
    "text 240 236 32 24",
    "text 280 236 32 26",
    "text 112 134 1500 7",
    "text 152 134 1500 9",
    "text 192 134 1500 11",
    "text 232 134 1500 13",
    "text 272 134 1500 15",
    "text 112 215 600 17",
    "text 152 215 600 19",
    "text 192 215 600 21",
    "text 232 215 600 23",
    "text 272 216 600 25",
    "text 60 271 800 29",
    "button 90 274 2117 2118 1 0 105",
    "text 110 271 800 30",
    "button 180 274 2117 2118 1 0 104",
    "text 200 271 800 31",
    "text 63 155 1500 27",
    "text 63 235 600 28",
    "text 80 330 800 32"
);

var data := array(  
    "Dealer:",
    "30000",
    "Player:",
    "30000",
    "Deal",
    "Hit",
    "Stand",
    "10",
    "H",
    "J",
    "C",
    "",
    "",
    "",
    "",
    "",
    "",
    "A",
    "S",
    "K",
    "S",
    "",
    "",
    "",
    "",
    "",
    "",
    "20",
    "21",
    "Bet:",
    "100",
    "Double Down",
     "© 2000 Zulu Hotel"
);


if( me.name["<random>"] )
case( randomint(2) )
0: me.graphic := 400; me.gender := 0;
1: me.graphic := 401; me.gender := 1;
endcase
SetName ( me , RandomName( me ) + " the gambler");
Habille( me, "gambler" );
//start_script("NPCKeeper", me);
dealmoney := createitematlocation(me.x, me.y, me.z, 0x0e75, 1);
equipitem(me, dealmoney);
CreateItemInBackpack(me, MONEY, (randomint(100)+1)*300);
endif

EnableEvents( EVID_SPEECH , REACT_THRESHOLD );
EnableEvents (EVID_ITEM_GIVEN);
EnableEvents( EVID_ENTEREDAREA + EVID_LEFTAREA, HALT_THRESHOLD );

While (1)

    var ev;

    ev := os::wait_for_event( 120 );
    if (ev)

        case (ev.type)
            EVID_SPEECH:
            	TurnToward (ev.source);

               if (ev.text["blackjack"])
		if (moneyreload<>0)
		say ("I don't have enough money to play right now!");
		else
		Say ("So, you want to try your luck.");
		blackjack (ev.source);
		endif
               elseif ((ev.text["hello"]) or (ev.text["hi"]) or (ev.text["vendor"]))
		Say ("Let's play blackjack.");
		elseif (ev.text["help"])
		Say ("Type blackjack, if you want to play.");
		sleep(5);
		Say ("You want to have close to 21 without going over.");
		sleep(5);
		Say ("If your cards add up higher than mine, you win.");
		sleep(5);
		else
		Say ("Same to you");
		endif

            EVID_ENTEREDAREA:
		if (moneyreload==0)
                BarkIntro(ev.source);
		endif

	    EVID_LEFTAREA:
		say("You are probably not very good at blackjack!");

        endcase
    endif

   cleareventque();

    if ((moneyreload<>0) and (ReadGameClock()>moneyreload))
	CreateItemInBackpack(me, MONEY, (randomint(100)+1)*300);
	moneyreload := 0;
    endif

endwhile

function cleareventque()

var ev;
repeat
until (! (ev := os::wait_for_event(0)) );

endfunction


function BarkIntro( who )
    TurnToward( who );

say("Come and play blackjack so I can pay my taxes!");
sleep(5);
say(who.name + " do you feel lucky?");

endfunction

function playerhere(who)

var chr;

foreach chr in ListMobilesNearLocation( me.x, me.y, me.z, HALT_THRESHOLD );
  sleepms(1);
  if (who.serial = chr.serial)
    return 0;
  endif
endforeach
return 1;
endfunction

function blackjack( who )

   var gumpexit :=0, value, dd:=0;

   moneyamount := CheckMoney( who );

   If (!moneyamount)
      say ("You do not have money to play!");
      return;
   endif

newgame(who);

while (!gumpexit)
data[1]:= "Dealer: " + me.name;
dealmoney := CheckMoney( me );
data[2]:= dealmoney;
data[3]:= "Player:  " + who.name;
moneyamount := CheckMoney( who );
data[4]:= moneyamount;

if ((dealmoney<100)and(gameover==0))
moneyreload := ReadGameClock() + 38400;
say("I'm out of money, thanks for playing!");
gumpexit :=1;
gameover := 0;
return;
endif

if (playerhere(who)=1)
gumpexit := 1;
gameover := 0;
SendSysMessage( who, "I want to keep an eye on you!");
return;
endif

if (moneyamount==0)
gumpexit := 1;
gameover := 0;
return;
endif

var box := SendDialogGump( who, layout, data );

case (box[0])
	101: //deal
	    NewHand( who );
	102: //hit
	    user( who );
	103: //stand
	    dealer(who);
	    LookAtHands(who);
	104: //double down
	   if (youcards==2)
	    dd:=1;
	    GetPayment (who, youbet);
	    youbet :=cint(youbet*2);
	    data[31] :=youbet;
	    user(who);
	    dealer(who);
	    LookAtHands(who);
	   endif
	   
	105: //bet
	if(gameOver)
		SendSysMessage( who, "Hand in Play!");
	else
	    value := cint(SendTextEntryGump( who, "What is your bet", 40 ));
	    if ((!value) or (value <0) or (value >500))
	    value := 500;
	    endif
	    youbet :=value;
	    data[31] := youbet;
	endif
	    
	666:
	PlaySoundEffect(who , 0x1EA );
	gameover := 0;
	gumpexit := 1;

endcase
endwhile

endfunction


function newgame(who)

Shuffle (who);
dealerskill := randomint(3) +1;

endfunction



function GetPayment (who, price)
   
   if (who.spendgold( price ))
	CreateItemInBackpack(me, MONEY, (price));
   	return 1;
   else
	say ( "You do not have " + price + " gold coins!");
 	return 0;
   endif

endfunction


function GivePayment (who, price)
	CreateItemInBackpack( who, MONEY, (price*2) );
	me.spendgold( price*2 );
endfunction


function CheckMoney (who)

   var totalmoney := 0, backpack := EnumerateItemsInContainer (who.backpack);

   foreach item in backpack
      sleepms(1);
      if (item.objtype = MONEY)
        totalmoney := totalmoney + GetAmount(item);
      endif
   endforeach

   return totalmoney;

endfunction



function Shuffle (who)

PlaySoundEffect(who , 0x3E );

var tempcard, tempcard2, i;

for(i:=1;i<53;i:=i+1)
cardz[i]:=i;
endfor

for(i:=52;i>0;i:=i-1)
tempcard:=randomint(i)+1;
tempcard2:=cardz[tempcard];
cardz[tempcard]:=cardz[i];
cardz[i]:=tempcard2;
endfor

cardCount := 1;

endfunction



function getSuit(card)

if ((card>=1) and (card <=13))
   return "C";
elseif ((card>=14) and (card <=26))
   return "D";
elseif ((card>=27) and (card <=39))
   return "H";
else
   return "S";
endif
endfunction

function cardName(card)

while (card>13)
card := card - 13;
endwhile

if(card==1)
   return "A";
elseif(card = 11)
   return "J";
elseif(card = 12)
   return "Q";
elseif(card = 13)
   return "K";
else
   return "" + card;
endif
endfunction

function cardValue(card,value)

if(card==1)
  return 11;
endif

if(card>10)
 return 10;
endif
 
return card;
endfunction


function NewHand( who )

if (gameOver)
SendSysMessage( who, "Hand in Play!");
return;
endif

data[33] :="© 2000 Zulu Hotel";

if (dd==1)
dd :=0;
youbet :=cint(youbet/2);
endif

If (youbet>500)
 youbet := 500;
endif

if (!GetPayment (who, youbet))
	return;
endif

data[31] :=youbet;

youvalue :=0;
dealervalue := 0;

youace := 0;
dealerace := 0;
youcards :=0;
dealercards :=0;
dealercardhidden := {};

var i;

for( i := 8; i < 28; i := i + 1 )
data[i] := "";
endfor

layout[17] := "";
layout[18] := "";
layout[19] := "";

layout[22] := "";
layout[23] := "";
layout[24] := "";

var card;

card := PickACard(who, "You");
youvalue := youvalue + card;

card := PickACard(who, "Dealer");
dealervalue := dealervalue + card;

data[28] := dealervalue;

card := PickACard(who, "You");

If((youvalue+card>21) and (youace>0))
youvalue := youvalue - 10;
youace := youace - 1;
endif

youvalue := youvalue + card;

card := PickACard(who, "Dealer");

   If((dealervalue+card>21) and (dealerace>0))
      dealervalue := dealervalue - 10;
      dealerace := dealerace - 1;
   endif

dealervalue := dealervalue + card;

data[29] := youvalue;

gameOver := 1;


if((youvalue = 21) and (youcards==2))

if (dealerskill==1)
i :=1;
elseif (dealerskill==2)
i :=1.5;
else
i :=2;
endif

playerwin(who, "21, you get " + i + " times your bet!");
GivePayment ( who,Cint(youbet*i) );

showdealercard();
gameOver := 0;
elseif((dealercards==2) and (dealervalue = 21))
showdealercard();

i := 1;
if (dealerskill==2)
i := 1.5;
elseif(dealerskill==3)
i := 2;
endif

housewin(who, "21, you lose " + i + " times your bet!");

 if (i==1.5)
  i:=Cint(youbet*0.5);
 elseif (i==2)
  i:=youbet;
 else
 i:=0;
 endif
 
 GetPayment (who,youbet);
gameOver:=0;
endif

endfunction



function PickACard(who, strWho)

cardCount := cardCount + 1;

if(cardcount>52)
Shuffle (who);
endif

var card := cardz[cardCount];
var suit := getSuit(card);
var value;
var temp :=0;

if(strWho ="You")
value := cardName(card);
if (value="A")
  youace := youace +1;
endif

youcards := youcards + 1;

	data[placevalue[youcards]+10] :=value;
	data[placesuits[youcards]+10] :=suit;

if (youcards>2)
 layout[19+youcards] := "resizepic " + placecards[youcards] + " 210 2171 35 50"; //player card
endif

if ((suit="H") or (suit="D"))
temp := 32;
endif

layout[29+youcards] := "text " + (placecards[youcards]+15) + " 236 " + temp + " " + (placevalue[youcards]+10);

  value := youvalue;
else
value := cardName(card);
if (value="A")
 dealerace := dealerace +1;
endif

dealercards := dealercards + 1;

if (dealercards = 2)
dealercardhidden[1] := value;
dealercardhidden[2] := suit;
else
	data[placevalue[dealercards]] :=value;
	data[placesuits[dealercards]] :=suit;
endif

if (dealercards>2)
layout[14+dealercards] := "resizepic " + placecards[dealercards] + " 129 2171 35 50"; //dealer card
endif

if ((suit="H") or (suit="D"))
temp := 32;
endif

layout[24+dealercards] := "text " + (placecards[dealercards]+15) + " 155 " + temp + " " + placevalue[dealercards];

   value := dealervalue;
endif

while (card>13)
card := card - 13;
endwhile

return cardValue(card,value);

endfunction



function Dealer( who )

var card :=0;

if (gameOver==0)
data[33] :="Deal the Cards!";
say ("Deal the Cards!");
return;
endif

if((dealerskill==1) and (youvalue<10))
SendSysMessage( who, "Not Below Ten! Take a Hit");
return;
endif

data[placevalue[2]] := dealercardhidden[1];
data[placesuits[2]] := dealercardhidden[2];

If((dealerskill==3) and (dealervalue <= youvalue) and (dealerace>0))
elseif((dealerskill>1) and (dealervalue >= 17))
return;
endif

while(dealervalue < 17)

   card := PickACard(who, "Dealer");

   If((dealervalue+card>21) and (dealerace>0))
      dealervalue := dealervalue - 10;
      dealerace := dealerace - 1;
   endif

dealervalue := dealervalue + card;

data[28] := dealervalue;

if (dealervalue > youvalue)
break;
endif

if (dealercards==5)
break;
endif

endwhile

endfunction



function User(who)
if (gameOver==0)
data[33] :="Deal the Cards!";
say ("Deal the Cards!");
return;
endif

var card :=0;
card := PickACard(who, "You");

If((youvalue+card>21) and (youace>0))
youvalue := youvalue - 10;
youace := youace - 1;
endif

youvalue := youvalue + card;


data[29] := youvalue;

if(youvalue > 21)
housewin(who, "You Busted!");
showdealercard();
gameOver := 0;
endif

if((youvalue <=21) and (youcards==5))
playerwin(who, "You won!");
showdealercard();
gameOver := 0;
GivePayment ( who,youbet );
endif

endfunction



function LookAtHands(who)

if ((gameOver==0) or (youvalue<10) or ((dealercards+youcards)<3))
return;
endif

showdealercard();

gameOver:=0;

if((dealercards==5) and (dealervalue < 21))
housewin(who, "House Wins! Tough Luck!");

elseif((youcards==5) and (youvalue < 21))
playerwin(who, "you Win");
GivePayment ( who,youbet );

elseif(dealervalue > 21)
playerwin(who, "House Busted!");
GivePayment ( who,youbet );

elseif(youvalue > dealervalue)
playerwin(who, "You Win!");
GivePayment ( who,youbet );

elseif(dealervalue = youvalue)
bothwin(who,"Game Tied! Try Again!");
GivePayment ( who,cint(youbet/2));

else
housewin(who, "House Wins! Tough Luck!");
endif

if (dd==1)
dd :=0;
youbet :=cint(youbet/2);
endif

endfunction

function showdealercard()
	data[placevalue[2]] := dealercardhidden[1];
	data[placesuits[2]] := dealercardhidden[2];
	data[28] := dealervalue;
endfunction

function housewin(who, message)
	Say ( message );
	data[33] := message;
	PlaySoundEffect(who , 0x37 );
	moneyamount := CheckMoney( who );
endfunction

function bothwin(who, message)
	Say ( message );
	data[33] := message;
	PlaySoundEffect(who , 0x37 );
	moneyamount := CheckMoney( who );
endfunction

function playerwin(who, message)
	Say ( message );
	data[33] := message;
	PlaySoundEffect(who , 0x37 );
	moneyamount := CheckMoney( who );
endfunction
