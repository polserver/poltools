////////////////////////////////////////////////////////
//
//   StaticsExportForce - Extract Items to make Static
//
//     Author: Shinigami
//     eMail : Shinigami@gmx.net
//
//   Modifications:
//     2001/12/02 Shinigami: Fix: ObjType -> Graphic
//     2002/03/01 Shinigami: Fix: Letzte Nummer
//
////////////////////////////////////////////////////////
//
// Needs:
//   statics/statics.cfg
//
//////////////////////////

/////////////////
// Bibliotheken
/////////////////

Use uo;
Use os;
Use cfgfile;

/////////////
// Includes
/////////////

Include "include/errormsg";

///////////////
// Konstanten
///////////////

Const ERRORMSGS_GERMAN:=
  "{}StaticsExport: ExportArea aktiviert..."+
  "{}StaticsExport: ExportArea beendet."+
  "{}X1 muss kleiner-gleich X2 bzw. Y1 muss kleiner-gleich Y2 sein!"+
  "{}%% darf nur 0 bis 4095 sein!"+
  "{}%% darf nur 0 bis 6143 sein!"+
  "{}Sie muessen 4 Paramter angeben: X1 Y1 X2 Y2"+
  "{}Die Items wurden exportiert."+
  "{}Probleme beim Lesen der Configdateien!";
Const ERRORMSGS_ENGLISH:=
  "{}StaticsExport: ExportArea started..."+
  "{}StaticsExport: ExportArea done."+
  "{}X1 must be lower/equal X2 and Y1 must be lower/equal Y2!"+
  "{}%% must be between 0 and 4095!"+
  "{}%% must be between 0 and 6143!"+
  "{}U've to enter 4 Paramters: X1 Y1 X2 Y2"+
  "{}Items have been exported."+
  "{}Problems on Reading Config File!";

/////////////////
// Hauptprogram
/////////////////

Program TextCMD_StaticsExportForce(who, params)
  Set_Script_Option(SCRIPTOPT_NO_RUNAWAY, 1);

  If (Len(who)>1) // Called by StaticsCheckExport
    params:=who[2];
    who:=who[1];
  EndIf

  params:=SplitWords(params);
  If (params.size()=4)
    If ((0<=CInt(params[1])) And (CInt(params[1])<6144))
      If ((0<=CInt(params[2])) And (CInt(params[2])<4096))
        If ((0<=CInt(params[3])) And (CInt(params[3])<6144))
          If ((0<=CInt(params[4])) And (CInt(params[4])<4096))
            If ((CInt(params[1])<=CInt(params[3])) And (CInt(params[2])<=CInt(params[4])))
              PrintErrorMessage(1);
              ExportArea(who, CInt(params[1]), CInt(params[2]), CInt(params[3]), CInt(params[4]));
              PrintErrorMessage(2);
            Else
              PrintErrorMessage(3, { }, who);
            EndIf
          Else
            PrintErrorMessage(4, {"Y2"}, who);
          EndIf
        Else
          PrintErrorMessage(5, {"X2"}, who);
        EndIf
      Else
        PrintErrorMessage(4, {"Y1"}, who);
      EndIf
    Else
      PrintErrorMessage(5, {"X1"}, who);
    EndIf
  Else
    PrintErrorMessage(6, { }, who);
  EndIf
EndProgram

//////////////////////////////////////
// ExportArea - Extrahiert die Items
//////////////////////////////////////

Function ExportArea(who, x1, y1, x2, y2)
  var datafile:=ReadConfigFile("statics/statics");
  If (datafile)
    var counter:=GetNextCounterNumber(datafile);

    var elem;
    ForEach item in ListObjectsInBox(x1, y1, -127, x2, y2, 127)
      If (item.isa(POLCLASS_ITEM))
        elem:={ };
        elem.append({"NAME",  "#"});
        elem.append({"ID",    item.graphic});
        elem.append({"X",     item.x});
        elem.append({"Y",     item.y});
        elem.append({"Z",     item.z});
        elem.append({"COLOR", item.color});
        elem.append({"CONT",  -1});
        elem.append({"TYPE",  255});
        AppendConfigFileElem("statics/statics", "SECTION", "WORLDITEM "+counter, elem);

        DestroyItem(item);

        counter:=counter+1;
      EndIf
    EndForEach

    UnloadConfigFile("statics/statics");

    PrintErrorMessage(7, { }, who);
  Else
    PrintErrorMessage(8, { }, who);
  EndIf
EndFunction

/////////////////////////////////////////////////////////
// GetNextCounterNumber - Die naechste Nummer ermitteln
/////////////////////////////////////////////////////////

Function GetNextCounterNumber(datafile)
  var counter:=GetConfigStringKeys(datafile);
  If (Len(counter))
    Return (Len(counter));
  Else
    Return (0);
  EndIf
EndFunction
