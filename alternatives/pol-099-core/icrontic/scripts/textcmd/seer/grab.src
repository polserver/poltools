use uo;
use os;

include "include/objtype";
include "include/dist";

Const STARTHAIR := 0x2030;
Const ENDHAIR := 0x2060;

program textcmd_loot(me)

    if (cint(getObjProperty(me, "#looting")) > readgameclock())
        SendSysmessage(me, "You are already looting!");
        return;
    else
	SetObjProperty(me, "#looting",cint(readgameclock()) + 60);
    endif

	var Near_Items := ListItemsNearLocation(me.x, me.y, me.z,2);
        foreach item in Near_Items

	    if (distance(me,item) = 0)
		SendSysMessage(me,"You're standing on the loot!");
		EraseObjProperty(me, "#looting");
		return;
	    endif

	    if (item.objtype = UOBJ_CORPSE)
		foreach thingie in EnumerateItemsInContainer(item)
		  if (me.dead)
		    SendSysMessage(me,"Whadya know?  You really can't take it with you!");
		    EraseObjProperty(me, "#looting");
		    return;
	  	  endif
		  if (distance(me,item) > 2 )
		    SendSysMessage(me,"You're too far away to continue looting.");
		    EraseObjProperty(me, "#looting");
		    return;
		  endif
		  Grabit (me,thingie);
		endforeach
	    else
	        if (me.dead)
		  SendSysMessage(me,"Whadya know?  You really can't take it with you!");
		  EraseObjProperty(me, "#looting");
		  return;
	        endif
		if ( distance( me, item ) > 2 )
		    SendSysMessage(me,"You're too far away to continue looting.");
		    EraseObjProperty(me, "#looting");
		    return;
		endif
		Grabit(me,item);
	     endif

	endforeach
        SendSysMessage(me,"Hmm... looks like you got it all!");
	EraseObjProperty(me, "#looting");

endprogram

Function grabit (me, it)

	if (! (it.movable))
	  return;
	endif

	if (! (accessible(me,it)))
	  return;
	endif

	if (distance (me,it) =0)
	  return;
	endif

	if ((it.objtype >= STARTHAIR) and (it.objtype <= ENDHAIR))
	  return;
	endif


	if (it.amount > 1 )
	  foreach item in EnumerateItemsInContainer(me.backpack)
		if (item.objtype = it.objtype) 
		  CreateItemInBackpack( me, it.objtype, it.amount );
		  destroyitem(it);
		  PrintTextAbove (me,"*yoink*");
	    	  sleep(1);
		  return;
		endif
	  endforeach
	endif
	
	if ( moveitemtocontainer(it, me.backpack, 0, 0 ))
	    PrintTextAbove (me,"*yoink*");
	    sleep(1);
	endif

endfunction
